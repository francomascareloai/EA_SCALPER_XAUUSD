//@version=5
indicator(title="Volume Spread Analysis Custom", shorttitle="Volume", overlay=false, precision=2, format=format.volume)

//------------------------------------------------------------------------
// ***** Colors *****
//------------------------------------------------------------------------
Black   = #000000,      Gray    = #787B86,      White   = #FFFFFF
Red     = #FF0000,      Yellow  = #FFFF00,      Navy    = #413BB2,  
Green   = #00AF00,      Blue    = #0094FF,

None = color.new(Black, 100)

//------------------------------------------------------------------------
// ***** Inputs *****
//------------------------------------------------------------------------
ma0_length = input.int(30, "Moving Average", minval=1, inline="Moving Average")
maTypeInput = input.string("SMA", title="", options=["SMA", "EMA"], inline="Moving Average")

ma1_length = input.float(0.5, "", minval=0.01, inline="Moving Average Multipliers")
ma2_length = input.float(1.5, "", minval=0.01, inline="Moving Average Multipliers")
ma3_length = input.float(3.0, "", minval=0.01, inline="Moving Average Multipliers")

bColorAnalysis = input.bool(true, "Color Analysis", inline="Analysis")
bDrawAverages  = input.bool(true, "Draw Analysis",  inline="Analysis")

// Параметры ликвидности
liq_len = input.int(500, title="Кол-во свечей для расчета медианы", minval=1, group="Уровень ликвидности")
position_dollars = input.float(1000, title="Размер позиции ($)", minval=0, group="Уровень ликвидности")

// Настройка разделителя разрядов
separator_input = " "

//------------------------------------------------------------------------
// ***** Functions *****
//------------------------------------------------------------------------
ma(source, length, type) =>
    switch type
        "SMA" => ta.sma(source, length)
        "EMA" => ta.ema(source, length)

calcMedian(src, length) =>
    arr = array.new_float(0)
    for i = 0 to length - 1
        array.push(arr, src[i])
    array.sort(arr, order.ascending)
    length % 2 == 1 ? array.get(arr, math.floor(length / 2)) : (array.get(arr, length / 2 - 1) + array.get(arr, length / 2)) / 2.0

// Форматирование числа с разделителем, без дробей
formatNumber(n, sep) =>
    intPart = math.floor(n)
    intStr = str.tostring(intPart)
    res = ""
    for i = 0 to str.length(intStr) - 1
        res := res + str.substring(intStr, i, i + 1)
        if ((str.length(intStr) - i - 1) % 3 == 0) and (i != str.length(intStr) - 1)
            res := res + sep
    res

//------------------------------------------------------------------------
// ***** Dollar Volume Calculation *****
//------------------------------------------------------------------------
dollarVolume = volume * close

//------------------------------------------------------------------------
// ***** Calculations *****
//------------------------------------------------------------------------
ma0 = ma(dollarVolume, ma0_length, maTypeInput)
ma1 = ma0 * ma1_length
ma2 = ma0 * ma2_length
ma3 = ma0 * ma3_length

plot0 = plot(0, title="Baseline", color=None, linewidth=1, editable=false, display=display.none)

//------------------------------------------------------------------------
// ***** Assign Colors *****
//------------------------------------------------------------------------
histColor = not bColorAnalysis ? (close > close[1] ? color.new(#26a69a, 25) : color.new(#ef5350, 25)) : 
  dollarVolume < ma1 ? Navy   :
  dollarVolume < ma2 ? Green  : 
  dollarVolume < ma3 ? Yellow : 
  dollarVolume > ma3 ? Red    : 
  Gray

fillColor1 = color.new(Black,  90)
fillColor2 = color.new(Yellow, 90)
fillColor3 = color.new(Red,    50)

//------------------------------------------------------------------------
// ***** Plot Everything *****
//------------------------------------------------------------------------
ma0plot = plot(ma0, title="Moving Average 0", color=Blue)
ma1plot = plot(bDrawAverages ? ma1 : na, title="Moving Average 1", color=color.new(Red, 100))
ma2plot = plot(bDrawAverages ? ma2 : na, title="Moving Average 2", color=color.new(Red, 100))
ma3plot = plot(bDrawAverages ? ma3 : na, title="Moving Average 3", color=color.new(Red, 100))

fill(plot0,   ma1plot, title="Low Volume Background",  color=fillColor1)
fill(ma1plot, ma2plot, title="Med Volume Background",  color=fillColor2)
fill(ma2plot, ma3plot, title="High Volume Background", color=fillColor3)

plotcandle(0, dollarVolume, 0, dollarVolume, "Dollar Volume", color=histColor, wickcolor=na, bordercolor=Black)

//------------------------------------------------------------------------
// ***** Liquidity Analysis *****
//------------------------------------------------------------------------
medianVolume = calcMedian(volume, liq_len)
medianVolumeUSDT = medianVolume * close
liqThreshold = position_dollars * 10
liqStatus = medianVolumeUSDT < liqThreshold ? "Low" : "High"
cellColor = medianVolumeUSDT < liqThreshold ? color.red : color.green

// Таблица
var table infoTable = table.new(position.bottom_right, 1, 2, frame_color=color.black, frame_width=1)
if barstate.isconfirmed
    table.cell(infoTable, 0, 0, liqStatus, text_color=color.white, bgcolor=cellColor, text_size=size.normal)
    table.cell(infoTable, 0, 1, formatNumber(medianVolumeUSDT, separator_input) + " USDT", text_color=color.white, bgcolor=color.gray, text_size=size.normal)
// ==========================================================================================

// === Dashboard with Telegram Link ===
var table myTable = table.new(position.top_center, 1, 1, border_width=1, frame_color=color.black, bgcolor=color.white)

// Add Telegram Message to Dashboard
table.cell(myTable, 0, 0, "Join Telegram @free_fx_pro", bgcolor=color.blue, text_color=color.white, text_size=size.normal)