//@version=6
indicator(title="<[HEMA & ATR Predictive]>", overlay = true)

// ВХОДНЫЕ ПАРАМЕТРЫ
alphaL = input.int(8, "Длина Alpha", group = "НАСТРОЙКИ HEMA")
gammaL = input.int(5, "Длина Gamma", group = "НАСТРОЙКИ HEMA")
atrMult = input.float(0.02, "Множитель ATR", step = 0.01, group = "НАСТРОЙКИ ATR")
atrleng = input.int(10, "Длина ATR", group = "НАСТРОЙКИ ATR")
src = input.source(hl2, "Источник", group = "ОСНОВНЫЕ")
paint = input.bool(false, "Раскрашивать свечи?", group = "СТИЛЬ")

// ЦВЕТОВАЯ СХЕМА
colScheme = input.string("По умолчанию", "Цветовая схема", 
     options = ["По умолчанию", "Современная", "Холодная", 
               "Альтернативная", "Яркая"], group = "СТИЛЬ")

[bull, bear, neutral] = switch colScheme
    "По умолчанию" => [#00ff73, #ff0040, #606060]
    "Современная" => [#23d7e4, #b30f61, #707070]
    "Холодная" => [#00ffcc, #2f00ff, #505050]
    "Альтернативная" => [#00ff80, #ff6600, #505050]
    "Яркая" => [#e8ec00, #f200fa, #505050]

// РАСЧЕТ ИНДИКАТОРА
alpha = 2 / (alphaL + 1)
gamma = 2 / (gammaL + 1)

var float b = 0.0
var float hema = 0.0
hema := (1 - alpha) * (nz(hema[1]) + nz(b[1], src)) + alpha * src
b := (1 - gamma) * nz(b[1]) + gamma * (hema - nz(hema[1]))

atrValue = ta.atr(atrleng)
neutralThreshold = atrValue * atrMult
hemaChange = hema - hema[1]
inNeutralZone = math.abs(hemaChange) < neutralThreshold

// ЛОГИКА ЦВЕТА И СИГНАЛОВ
var color hemaColor = na
var color prevColor = na

// Текущий цвет
if inNeutralZone
    hemaColor := prevColor
else
    hemaColor := hemaChange > 0 ? bull : bear

// ПРЕДСИГНАЛЫ 
bullSignal = hemaChange > 0 and hemaChange[1] <= 0 and not inNeutralZone
bearSignal = hemaChange < 0 and hemaChange[1] >= 0 and not inNeutralZone

prevColor := hemaColor

// ОТОБРАЖЕНИЕ
plot(hema, "HEMA", hemaColor, 4)

// Сигналы 
plotshape(bullSignal ? 1 : na, "Лонг", shape.triangleup, location.belowbar, bull, text = "Л", textcolor = bull, size = size.small, offset = -1)
plotshape(bearSignal ? 1 : na, "Шорт", shape.triangledown, location.abovebar, bear, text = "Ш", textcolor = bear, size = size.small, offset = -1)

barcolor(paint ? hemaColor : na)

//------