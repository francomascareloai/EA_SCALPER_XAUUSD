# Timeframe Flow - indicator for MetaTrader 4

Source: https://www.mql5.com/en/code/36346

* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 5](/en/code/mt5)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt5/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt5/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt5/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt5/libraries)
* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 4](/en/code/mt4)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt4/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt4/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt4/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt4/libraries)

* [![](https://c.mql5.com/i/sidebar/storage.svg)Storage](/en/code/storage)

Watch [how to download](https://youtu.be/rloNyFVtHuA?list=PLltlMLQ7OLeKwyQwC8FhiKwjl9syKhOCK) trading robots for free

Find us on [Telegram](https://t.me/mql5dev)!  
 Join our fan page

Interesting script?  
So post a [link](/en/code/36346) to it -  
let others appraise it

You liked the script? Try it in the [MetaTrader 5](https://download.mql5.com/cdn/web/metaquotes.ltd/mt5/mt5setup.exe?utm_source=www.mql5.com&utm_campaign=download) terminal

[to pocket](#pocket "Pocket allows you to insert a complete content description to the appropriate comment")

![Indicators](https://c.mql5.com/i/code/indicator.png)

# Timeframe Flow - indicator for MetaTrader 4

[I Kai Wu](/en/users/alf0878502)

Views:
:   19061

Rating:
:   (19)

Published:
:   6 September 2021, 17:32

Updated:
:   14 September 2021, 07:07
:   [Timeframe Flow.mq4](/en/code/download/36346/timeframe_flow.mq4 "Timeframe Flow.mq4")
    (10.46 KB)
    [view](# "view")

    [Download as ZIP](/en/code/download/36346.zip "Download all attachments in the single ZIP archive") [How to download code from MetaEditor](https://www.metatrader5.com/en/metaeditor/help/workspace/toolbox#codebase)

    ![MQL5 - Language of trade strategies built-in the MetaTrader 5 client terminal](https://c.mql5.com/i/registerlandings/logo-2.png)

    You are missing trading opportunities:

    * Free trading apps
    * Over 8,000 signals for copying
    * Economic news for exploring financial markets

    Registration
    Log in

    latin characters without spaces

    a password will be sent to this email

    An error occurred

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup&amp;reg=1)

    You agree to [website policy](/en/about/privacy) and [terms of use](/en/about/terms)

    If you do not have an account, please [register](https://www.mql5.com/en/auth_register)

    Allow the use of cookies to log in to the MQL5.com website.

    Please enable the necessary setting in your browser, otherwise you will not be able to log in.

     

    [Forgot your login/password?](https://www.mql5.com/en/auth_forgotten?return=popup)

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup)
:   ![MQL5 Freelance](https://c.mql5.com/i/code/icon_freelance.svg)
    Need a robot or indicator based on this code? Order it on Freelance
    [Go to Freelance](/en/job/new "Go to Freelance")

Sharing this trend indicator made by me. Called "Timeframe Flow".

It will draw a user defined higher time-frame candle on every shift on your current time-frame.

Meaning you can have H4, H12 or D1 candles on every shift of your H1 Charts.

Or you can have D2, D3 or D5 candles on every shift of your Daily Charts.

Combined with the current time-frame ATR Bands, you have a trend indicator with some filter.

You may also use the Mid Line crosses as exit( Calculated using Higher timeframe candles displayed in the indicator(Close + Open)/2 ).

[![TFF](https://c.mql5.com/18/99/111__3.JPG "TFF")](https://c.mql5.com/18/99/111__2.JPG "https://c.mql5.com/18/99/111__2.JPG")

**Last comments |
[Go to discussion](/en/forum/377093)**
(4)

![Valeriy Medvedev](https://c.mql5.com/avatar/2024/11/67255cda-744b.jpg "Valeriy Medvedev")

**[Valeriy Medvedev](/en/users/ich_hub)**
|
13 Sep 2021 at 11:49

```mql5
//+------------------------------------------------------------------+
//|                                               Timeframe Flow.mq4 |
//|                                                           Jas Wu |
//|                                                                  |
//+------------------------------------------------------------------+
#property description "Timeframe Flow"
#property description "By Jas Wu"
#property version   "1.01"
#property strict
#property indicator_separate_window
#property indicator_buffers 16
#property indicator_plots   8

#property indicator_label1  "FlatBody"
#property indicator_type1   DRAW_HISTOGRAM
#property indicator_color1  clrGray
#property indicator_width1  8

#property indicator_label2  "BullBody"
#property indicator_type2   DRAW_HISTOGRAM
#property indicator_color2  clrLime
#property indicator_width2  8

#property indicator_label3  "BearBody"
#property indicator_type3   DRAW_HISTOGRAM
#property indicator_color3  clrWheat
#property indicator_width3  8

#property indicator_label4  "BodyOpenForAll"
#property indicator_type4   DRAW_HISTOGRAM
#property indicator_color4  clrBlack
#property indicator_width4  8

#property indicator_label5  "FlatWick"
#property indicator_type5   DRAW_HISTOGRAM
#property indicator_color5  clrGray
#property indicator_width5  2

#property indicator_label6  "BullWick"
#property indicator_type6   DRAW_HISTOGRAM
#property indicator_color6  clrLime
#property indicator_width6  2

#property indicator_label7  "BearWick"
#property indicator_type7   DRAW_HISTOGRAM
#property indicator_color7  clrWheat
#property indicator_width7  2

#property indicator_label8  "LowWick"
#property indicator_type8   DRAW_HISTOGRAM
#property indicator_color8  clrBlack
#property indicator_width8  2

extern string NNN = "--------Input Any Number below to micmic Higher Timeframe-------"; //-
extern int Timeframez = 2; //23 = 24x Current, 2=3x Current, 5=6x Current, 11=12x Current
extern int MAP = 8; //MidLine Period
extern int Shift = 0; //MidLine Shift
input  ENUM_MA_METHOD MaMethod = MODE_EMA; //MA Matheod
extern string FFF = "--------------ATR Bands Settings-------------"; //-
extern int ATRP = 100;// ATR Band Period
extern double ATRM = 1.0; //ATR Band Multiplier
color HLC = clrDodgerBlue; //high low Color Buffer
int HLCOff = 500; //high low Arrow Offset
color Ran = clrRed; //Both Side Equal Color
int RanOff = 750; //Both Side Equal Offset
color IIII = clrOrange; //Higher high or Lower low Color
int IIIOff = 300; //Higher high or Lower low Offset
extern string DDDD = "-----------Color Settings-------------"; //-
extern color UpperBand = clrMediumBlue; //Uppder Band Breakout Arrow Color
extern color LowerBand = clrRed; //Lower Band Breakout Arrow Color
extern color UpMid = clrGreen; //Price Cross MidLine Upward Arrow Color
extern color DnMid = clrRed; //Price Cross MidLine Downward Arrow Color
extern color ATRBColor = clrDeepPink; //ATR Band's Color
extern color MidLColor = clrDodgerBlue; //Mid Line Color

int barstocursor=0;
string UprDown = "";

double LLHH[], HHLL[], Range[], HighWick[], HL_Same[], FlatBody[], BullBody[], BearBody[], BodyOpen[], FlatWick[], BullWick[], BearWick[], LowWick[], StoreClose[], Baseline[], DownCross[], UpCross[], ATRUP[], ATRDN[], ATR_UCross[], ATR_DCross[], DualPierce[];
int mamethod;
//+------------------------------------------------------------------+
//| Custom indicator initialization function                         |
//+------------------------------------------------------------------+
int OnInit()
  {
//--- indicator buffers mapping
   ChartSetInteger(0,CHART_EVENT_MOUSE_MOVE,true);
   IndicatorShortName("Timeframe Flow "+" = Current Timeframe "+IntegerToString(_Period)+" Multiplies by "+IntegerToString((Timeframez+1))+"  ");
   IndicatorBuffers(16);
   IndicatorDigits(6);

// body
   SetIndexBuffer(0,FlatBody);
   SetIndexLabel(0, NULL);
   SetIndexBuffer(1,BullBody);
   SetIndexLabel(1, NULL);
   SetIndexBuffer(2,BearBody);
   SetIndexLabel(2, NULL);
   SetIndexBuffer(3,BodyOpen);
   SetIndexLabel(3, NULL);
// wick
   SetIndexBuffer(4,FlatWick);
   SetIndexLabel(4, NULL);
   SetIndexBuffer(5,BullWick);
   SetIndexLabel(5, NULL);
   SetIndexBuffer(6,BearWick);
   SetIndexLabel(6, NULL);
   SetIndexBuffer(7,LowWick);
   SetIndexLabel(7, NULL);

   SetIndexStyle(8,DRAW_NONE, STYLE_SOLID, 1, clrSpringGreen);
   SetIndexBuffer(8,StoreClose);
   SetIndexLabel(8, NULL); //(close + open) * 0.5

   SetIndexStyle(9,DRAW_LINE, STYLE_SOLID, 1, MidLColor);
   SetIndexBuffer(9,Baseline);
   SetIndexLabel(9, "MidLine");

   SetIndexBuffer(10,DownCross);
   SetIndexStyle(10,DRAW_ARROW, STYLE_SOLID, 1, DnMid);
   SetIndexArrow(10, 110);
   SetIndexLabel(10, NULL);

   SetIndexBuffer(11,UpCross);
   SetIndexStyle(11,DRAW_ARROW, STYLE_SOLID, 1, UpMid);
   SetIndexArrow(11, 110);
   SetIndexLabel(11, NULL);

   SetIndexStyle(12,DRAW_LINE, STYLE_SOLID, 1, ATRBColor);
   SetIndexBuffer(12,ATRUP);
   SetIndexLabel(12, "ATR_UpBand");

   SetIndexStyle(13,DRAW_LINE, STYLE_SOLID, 1, ATRBColor);
   SetIndexBuffer(13,ATRDN);
   SetIndexLabel(13, "ATR_DnBand");

   SetIndexBuffer(14,ATR_UCross);
   SetIndexStyle(14,DRAW_ARROW, STYLE_SOLID, 2, UpperBand);
   SetIndexArrow(14, 162);
   SetIndexLabel(14, NULL);

   SetIndexBuffer(15,ATR_DCross);
   SetIndexStyle(15,DRAW_ARROW, STYLE_SOLID, 2, LowerBand);
   SetIndexArrow(15, 162);
   SetIndexLabel(15, NULL);

//---
   return(INIT_SUCCEEDED);
  }
//+------------------------------------------------------------------+
//| Custom indicator iteration function                              |
//+------------------------------------------------------------------+
int OnCalculate(const int rates_total,
                const int prev_calculated,
                const datetime &time[],
                const double &open[],
                const double &high[],
                const double &low[],
                const double &close[],
                const long &tick_volume[],
                const long &volume[],
                const int &spread[])
  {
//---
   int ExtCountedBars=IndicatorCounted();
   if(ExtCountedBars<0)
      return(-1);
   int pos;
   int limit=Bars-Timeframez-2;
   if(ExtCountedBars>2)
      limit=Bars-ExtCountedBars-1;
   pos=limit;

   for(int t=0; t<=limit; t++)//Main Loop for drawing Timeframe Flow Candles
     {
      if(close[t] > open[t+Timeframez])
        {
         BullBody[t] = close[t];
         BodyOpen[t] = open[t+Timeframez];
         BullWick[t] = high[iHighest(NULL, 0, MODE_HIGH, 1+Timeframez, t)];
         LowWick[t] = low[iLowest(NULL, 0, MODE_LOW, 1+Timeframez, t)];
         StoreClose[t] = (close[t]+open[t+Timeframez])*0.5;
         BearBody[t] = EMPTY_VALUE;
         BearWick[t] = EMPTY_VALUE;
         FlatBody[t] = EMPTY_VALUE;
         FlatWick[t] = EMPTY_VALUE;
        }

      if(close[t] < open[t+Timeframez])
        {
         BearBody[t] = open[t+Timeframez];
         BodyOpen[t] = close[t];
         BearWick[t] = high[iHighest(NULL, 0, MODE_HIGH, 1+Timeframez, t)];
         LowWick[t] = low[iLowest(NULL, 0, MODE_LOW, 1+Timeframez, t)];
         StoreClose[t] = (close[t]+open[t+Timeframez])*0.5;
         BullBody[t] = EMPTY_VALUE;
         BullWick[t] = EMPTY_VALUE;
         FlatBody[t] = EMPTY_VALUE;
         FlatWick[t] = EMPTY_VALUE;
        }

      if(close[t] == open[t+Timeframez])
        {
         FlatBody[t] = close[t];
         BodyOpen[t] = close[t];
         FlatWick[t] = high[iHighest(NULL, 0, MODE_HIGH, 1+Timeframez, t)];
         LowWick[t] = low[iLowest(NULL, 0, MODE_LOW, 1+Timeframez, t)];
         StoreClose[t] = close[t];
         BearBody[t] = EMPTY_VALUE;
         BearWick[t] = EMPTY_VALUE;
         BullBody[t] = EMPTY_VALUE;
         BullWick[t] = EMPTY_VALUE;
        }

     }

//Draw Midline(Moving Average) based on Timeframe Flow Candles-------------------
   for(int t=0; t<=limit; t++)
     {
      Baseline[t] = iMAOnArray(StoreClose, 0, MAP, Shift, mamethod, t);
     }

//Arrows for MidLine Crosses-------------------------------------------
   for(int t=0; t<=limit; t++)
     {
      if(close[t] > Baseline[t] && close[t+1] < Baseline[t+1])
        {
         UpCross[t] = Baseline[t];
         DownCross[t] = EMPTY_VALUE;
        }
      else
         if(close[t] < Baseline[t] && close[t+1] > Baseline[t+1])
           {
            DownCross[t] = Baseline[t];
            UpCross[t] = EMPTY_VALUE;
           }

      if(close[t] > Baseline[t])
         DownCross[t] = EMPTY_VALUE;
      if(close[t] < Baseline[t])
         UpCross[t] = EMPTY_VALUE;
     }
//Arrows for MidLine Crosses-------------------------------------------


//Constructing ATR Bands--------------------------------------------
   for(int t=0; t<=limit; t++)
     {
      ATRUP[t] = Baseline[t]+iATR(_Symbol, _Period, ATRP, t)*ATRM;
      ATRDN[t] = Baseline[t]-iATR(_Symbol, _Period, ATRP, t)*ATRM;
     }
//Constructing ATR Bands--------------------------------------------

//Arrows for Band Breakouts--------------------------------------------
   for(int t=0; t<=limit; t++)
     {

      if(BullBody[t] != EMPTY_VALUE && BearBody[t] == EMPTY_VALUE && BullBody[t] > ATRUP[t])   //Bull Candle close Above ATR Up Band--------------
        {
         ATR_UCross[t] = ATRUP[t];
         ATR_DCross[t] = EMPTY_VALUE;
        }
      else
         if(BearBody[t] != EMPTY_VALUE && BullBody[t] == EMPTY_VALUE && BodyOpen[t] > ATRUP[t])   //Bear Candle close Above ATR Up Band(Rarely Ever Happens)-----------------
           {
            ATR_UCross[t] = ATRUP[t];
            ATR_DCross[t] = EMPTY_VALUE;
           }
         else
            ATR_UCross[t] = EMPTY_VALUE;


      if(BearBody[t] != EMPTY_VALUE && BullBody[t] == EMPTY_VALUE && BodyOpen[t] < ATRDN[t])   //Bear Candle close Below ATR Down Band--------------
        {
         ATR_DCross[t] = ATRDN[t];
         ATR_UCross[t] = EMPTY_VALUE;
        }
      else
         if(BullBody[t] != EMPTY_VALUE && BearBody[t] == EMPTY_VALUE && BullBody[t] < ATRDN[t])   //Bull Candle close Below ATR Dn Band(Rarely Ever Happens)-----------------
           {
            ATR_DCross[t] = ATRDN[t];
            ATR_UCross[t] = EMPTY_VALUE;
           }
         else
            ATR_DCross[t] = EMPTY_VALUE;

     }
//Arrows for Band Breakouts--------------------------------------------


//--- return value of prev_calculated for next call
   return(rates_total);
  }
```

Hello. I slightly corrected some inaccuracies in the code.

![I Kai Wu](https://c.mql5.com/avatar/2019/1/5C5150B9-5B54.png "I Kai Wu")

**[I Kai Wu](/en/users/alf0878502)**
|
14 Sep 2021 at 07:05

**Valeriy Medvedev [#](/en/forum/377093#comment_24587525):**  
 Hello. I slightly corrected some inaccuracies in the code.

Thank you! I will update this using this.

![zetauser](https://c.mql5.com/avatar/2017/6/59324150-16DF.jpg "zetauser")

**[zetauser](/en/users/zetauser)**
|
27 Sep 2021 at 17:24

Hi, you have this indicador for mt5?

![anita.svr](https://c.mql5.com/avatar/avatar_na2.png "anita.svr")

**[anita.svr](/en/users/anita.svr)**
|
13 Sep 2024 at 18:31

Reliable indicator, better than the commercial ones i've tried, never redraws or repaints. Helps me stay in the trend longer, one has to play with the settings though. Thank you for your remarkable talent and generosity!

![Tops and bottoms in trend](https://c.mql5.com/i/code/expert.png)
[Tops and bottoms in trend](/en/code/36244)

with rsi .

![IND - RiskPerTrade - MT4](https://c.mql5.com/i/code/indicator.png)
[IND - RiskPerTrade - MT4](/en/code/36240)

Building good trading habits by seeing things in terms of percentage not in terms of money.

![ATR with Smoothing](https://c.mql5.com/i/code/indicator.png)
[ATR with Smoothing](/en/code/36438)

ATR with 4 smoothing options : RMA, EMA, WMA, SMA

![Trailing stop MT4](https://c.mql5.com/i/code/expert.png)
[Trailing stop MT4](/en/code/36559)

Use trailing stop point with MT4