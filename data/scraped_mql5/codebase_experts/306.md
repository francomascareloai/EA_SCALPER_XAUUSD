# MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + CCI - expert for MetaTrader 5

Source: https://www.mql5.com/en/code/306

* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 5](/en/code/mt5)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt5/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt5/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt5/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt5/libraries)
* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 4](/en/code/mt4)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt4/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt4/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt4/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt4/libraries)

* [![](https://c.mql5.com/i/sidebar/storage.svg)Storage](/en/code/storage)

Watch [how to download](https://youtu.be/rloNyFVtHuA?list=PLltlMLQ7OLeKwyQwC8FhiKwjl9syKhOCK) trading robots for free

Find us on [Telegram](https://t.me/mql5dev)!  
 Join our fan page

Interesting script?  
So post a [link](/en/code/306) to it -  
let others appraise it

You liked the script? Try it in the [MetaTrader 5](https://download.mql5.com/cdn/web/metaquotes.ltd/mt5/mt5setup.exe?utm_source=www.mql5.com&utm_campaign=download) terminal

[to pocket](#pocket "Pocket allows you to insert a complete content description to the appropriate comment")

![Experts](https://c.mql5.com/i/code/expert.png)

# MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + CCI - expert for MetaTrader 5

[MetaQuotes](/en/users/metaquotes)  |
[English](javascript:void(false);)

[Русский](/ru/code/306) [中文](/zh/code/306) [Español](/es/code/306) [Deutsch](/de/code/306) [日本語](/ja/code/306) [Português](/pt/code/306) [한국어](/ko/code/306) [Français](/fr/code/306) [Italiano](/it/code/306) [Türkçe](/tr/code/306)

Views:
:   8885

Rating:
:   (20)

Published:
:   10 March 2011, 10:54

Updated:
:   22 November 2016, 07:32
:   \MQL5\Include\Expert\Signal\MySignals\

    [acandlepatterns.mqh](/en/code/download/306/acandlepatterns.mqh "acandlepatterns.mqh")
    (20.34 KB)
    [view](# "view")

    [acbe\_be\_cci.mqh](/en/code/download/306/acbe_be_cci.mqh "acbe_be_cci.mqh")
    (7.99 KB)
    [view](# "view")

    [expert\_abe\_be\_cci.mq5](/en/code/download/306/expert_abe_be_cci.mq5 "expert_abe_be_cci.mq5")
    (6.63 KB)
    [view](# "view")

    [Download as ZIP](/en/code/download/306.zip "Download all attachments in the single ZIP archive") [How to download code from MetaEditor](https://www.metatrader5.com/en/metaeditor/help/workspace/toolbox#codebase)

    ![MQL5 - Language of trade strategies built-in the MetaTrader 5 client terminal](https://c.mql5.com/i/registerlandings/logo-2.png)

    You are missing trading opportunities:

    * Free trading apps
    * Over 8,000 signals for copying
    * Economic news for exploring financial markets

    Registration
    Log in

    latin characters without spaces

    a password will be sent to this email

    An error occurred

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup&amp;reg=1)

    You agree to [website policy](/en/about/privacy) and [terms of use](/en/about/terms)

    If you do not have an account, please [register](https://www.mql5.com/en/auth_register)

    Allow the use of cookies to log in to the MQL5.com website.

    Please enable the necessary setting in your browser, otherwise you will not be able to log in.

     

    [Forgot your login/password?](https://www.mql5.com/en/auth_forgotten?return=popup)

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup)
:   ![MQL5 Freelance](https://c.mql5.com/i/code/icon_freelance.svg)
    Need a robot or indicator based on this code? Order it on Freelance
    [Go to Freelance](/en/job/new "Go to Freelance")

The [MQL5 Wizard](https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate "https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate") allows creating ready-made Expert Advisors based on the [Standard library](/en/docs/standardlibrary/expertclasses) classes delivered together with the client terminal (see [Creating Ready-Made Expert Advisors in MQL5 Wizard](https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate "https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate") for the details). It allows to check your trade ideas quickly, all you need is to create your own trading signals class. The structure of this class and example can be found in the article [MQL5 Wizard: How to Create a Module of Trading Signals](/en/articles/226).

The generic idea is the following: the class of trading signals is derived from [CExpertSignal](/en/docs/standardlibrary/expertclasses/expertbaseclasses/cexpertsignal), the next, it's necessary to override the [LongCondition()](/en/docs/standardlibrary/expertclasses/expertbaseclasses/cexpertsignal/cexpertsignallongcondition) and [ShortCondition()](/en/docs/standardlibrary/expertclasses/expertbaseclasses/cexpertsignal/cexpertsignalshortcondition) virtual methods with your own methods.

There is a book ["Strategies of best traders](https://www.mql5.com/go?link=http://www.tora-centre.ru/book/strategy.htm "http://www.tora-centre.ru/book/strategy.htm")" (in Russian), there are many trading strategies are considered there, we will focus on reversal candlestick patterns, confirmed by [Stochastic](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/so "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/so"), [CCI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci"), [MFI](https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation "https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation") and [RSI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi") oscillators.

The best way is to create the separate class, derived from [CExpertSignal](/en/docs/standardlibrary/expertclasses/expertbaseclasses/cexpertsignal) for checking of formation of candlestick patterns. For confirmation of trade signals, generated by candlestick patterns, it's sufficient to write the class, derived from CCandlePattern and add the necessary features (for example, confirmation by oscillators) there.

Here we will consider the signals, based "Bullish Engulfing/Bearish Engulfing" reversal candlestick pattern, confirmed by [CCI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci") indicator. The module of trade signals is based on the [CCandlePattern](/en/code/291) class, it's simple example of its use for creation of trade signals with candlestick patterns.

  

**1. "Bullish Engulfing" and "Bearish Engulfing" reversal candlestick patterns**

**1.1. Bullish Engulfing**

"Bullish Engulfing" reversal pattern forms in downtrend when a small black candlestick if followed by a large white candlestick that completely eclipses ("engulfs") the candlestick of the previous day. The shadows (tails) of the small candlestick are short, which enables the body of the large candlestick to cover the entire candlestick from the previous day.

![Fig. 1. "Bullish Engulfing" candlestick pattern](https://c.mql5.com/18/2/bullish_engulfing_en__2.png "Fig. 1. \"Bullish Engulfing\" candlestick pattern")

Fig. 1. "Bullish Engulfing" candlestick pattern

The recognition of "Bullish Engulfing" pattern is implemented in CheckPatternBullishEngulfing() method of [CCandlePattern](/en/code/291) class:

```mql5
//+------------------------------------------------------------------+
//| Checks formation of "Bullish Engulfing" candlestick pattern      |
//+------------------------------------------------------------------+
bool CCandlePattern::CheckPatternBullishEngulfing()
  {
//--- Bullish Engulfing
   if((Open(2)>Close(2))             &&  // previous candle is bearish
      (Close(1)-Open(1)>AvgBody(1))  &&  // body of the bullish candle is higher than average value of the body 
      (Close(1)>Open(2))             &&  // close price of the bullish candle is higher than open price of the bearish candle
      (MidOpenClose(2)<CloseAvg(2))  &&  // downtrend
      (Open(1)<Close(2)))                // open price of the bullish candle is lower than close price of the bearish
      return(true);
//---
   return(false);
  }
```

The CheckCandlestickPattern(CANDLE\_PATTERN\_BULLISH\_ENGULFING) method of [CCandlePattern](/en/code/291) class is used to check formation of "Bullish Engulfing" candlestick pattern.

  

**1.2. Bearish Engulfing**

"Bearish Engulfing" reversal pattern forms in uptrend when a small white candlestick if followed by a large black candlestick that completely eclipses ("engulfs") the candlestick of the previous day. The shadows (tails) of the small candlestick are short, which enables the body of the large candlestick to cover the entire candlestick from the previous day.

![Fig. 2. "Bearish Engulfing" candlestick pattern ](https://c.mql5.com/18/2/bearish_engulfing_en__2.png "Fig. 2. \"Bearish Engulfing\" candlestick pattern ")

Fig. 2. "Bearish Engulfing" candlestick pattern

The recognition of "Bearish Engulfing" pattern is implemented in CheckPatternBearishEngulfing() method of [CCandlePattern](/en/code/291) class:

```mql5
//+------------------------------------------------------------------+
//| Checks formation of "Bearish Engulfing" candlestick pattern      |
//+------------------------------------------------------------------+
bool CCandlePattern::CheckPatternBearishEngulfing()
  {
//--- Bearish Engulfing
   if((Open(2)<Close(2))            &&  // previous candle is bearish
      (Open(1)-Close(1)>AvgBody(1)) &&  // body of the candle is higher than average value of the body
      (Close(1)<Open(2))            &&  // close price of the bearish candle is lower than open price of the bullish candle
      (MidOpenClose(2)>CloseAvg(2)) &&  // uptrend
      (Open(1)>Close(2)))               // Open price of the bearish candle is higher than close price of the bullish candle
      return(true);
//---
   return(false);
  }
```

The CheckCandlestickPattern(CANDLE\_PATTERN\_BEARISH\_ENGULFING) method of [CCandlePattern](/en/code/291) class is used to check formation of "Bearish Engulfing" candlestick pattern.

  

**2. Trade signals, confirmed by CCI indicator**

The trading signals to open long or short position must be confirmed by [CCI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci") indicator. The value of CCI must be greater/lower than critical levels (-50 for long position and 50 for short position).

The closing of opened position depends on the values of CCI. It can be done in 2 cases:

1. if CCI line has reached the opposite critical level (80 for long position and -80 for short position)
2. if the reverse signal isn't confirmed (when CCI reaches the following levels: -80 for long position and 80 for short position)

![Fig. 3. "Bullish Englulfing" pattern, confirmed by CCI indicator](https://c.mql5.com/18/2/Image_CCI__1.png "Fig. 3. \"Bullish Englulfing\" pattern, confirmed by CCI indicator")

Fig. 3. "Bullish Englulfing" pattern, confirmed by CCI indicator

  

* int CBE\_BE\_CCI::LongCondition() - checks conditions to open long position (returns 80) and close of the short position (returns 40);
* int CBE\_BE\_CCI::ShortCondition() - checks conditions to open short position (returns 80) and close of the long position (returns 40).

  

**2.1. Open long position/Close short position**

1. The formation of "Bullish Engulfing" pattern must be confirmed by [CCI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci") indicator: CCI(1)<-50 (the value of the CCI of the last completed bar must be less than -50).
2. The short position must be closed if the [CCI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci") indicator has crossed upward the critical level -80 or crossed downward the critical level 80.

```mql5
//+------------------------------------------------------------------+
//| Checks conditions for entry and exit from market                 |
//| 1) Market entry (open long position, result=80)                  |
//| 2) Market exit (close short position, result=40)                 |
//+------------------------------------------------------------------+
int CBE_BE_CCI::LongCondition()
  {
   int result=0;
//--- idx can be used to determine Expert Advisor work mode
//--- idx=0 - in this case EA checks trade conditions at each tick
//--- idx=1 - in this case EA checks trade consition only at news bars
   int idx   =StartIndex();
//--- checking of conditions to open long position
//--- formation of Bullish Engulfing pattern and CCI<-50
   if(CheckCandlestickPattern(CANDLE_PATTERN_BULLISH_ENGULFING) && (CCI(1)<-50))
     result=80;
//--- checking of conditions to close short position
//--- signal line crossover of overbought/oversold levels (downward -80, downward -80)
   if(((CCI(1)>-80) && (CCI(2)<-80)) || ((CCI(1)<80) && (CCI(2)>80)))
     result=40;
//--- return result
   return(result);
  }
```

  

**2.2. Open short position/Close long position**

1. The formation of "Bearish Engulfing" pattern must be confirmed by [CCI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci") indicator: CCI(1)>50 (the value of the CCI indicator of the last completed bar must be greater than 50).
2. The long position must be closed if [CCI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci") indicator has crossed downward the -80 or 80 levels.

```mql5
//+------------------------------------------------------------------+
//| Checks conditions for entry and exit from market                 |
//| 1) Market entry (open short position, result=80)                 |
//| 2) Market exit (close long position, result=40)                  |
//+------------------------------------------------------------------+
int CBE_BE_CCI::ShortCondition()
  {
   int result=0;
//--- idx can be used to determine Expert Advisor work mode
//--- idx=0 - in this case EA checks trade conditions at each tick
//--- idx=1 - in this case EA checks trade consition only at news bars
   int idx   =StartIndex();
//--- checking of conditions to open short position
//--- formation of Bearish Engulfing pattern and CCI>50
   if(CheckCandlestickPattern(CANDLE_PATTERN_BEARISH_ENGULFING) && (CCI(1)>50))
     result=80;
//--- checking of conditions to close long position
//--- signal line crossover of overbought/oversold levels (downward -80, downward 80)
   if(((CCI(1)<80) && (CCI(2)>80)) || ((CCI(1)<-80) && (CCI(2)>-80)))
     result=40;
//--- return result
   return(result);
  }
```

  
  

**2.3. Creating Expert Advisor using MQL5 Wizard**

The CBE\_BE\_CCI class isn't included in the Standard Library classes, to use it, it's necessary to download the acbe\_be\_cci.mqh file (see attachments) and save it to the client\_terminal\_data\folder\MQL5\Include\Expert\Signal\MySignals. The same should be done with the candlepatterns.mqh file. You can use it in MQL5 Wizard after restart of the MetaEditor.

To create an Expert Advisor launch [MQL5 Wizard](https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate "https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate"):

![Fig. 4. Creating Expert Advisor using MQL5 Wizard](https://c.mql5.com/18/2/MQL5_Wizard_step_1__2.png "Fig. 4. Creating Expert Advisor using MQL5 Wizard")

Fig. 4. Creating Expert Advisor using MQL5 Wizard

Let's specify the name of the Expert Advisor:

![Fig. 5. General properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_2_BEBE_CCI.png "Fig. 5. General properties of the Expert Advisor")

Fig. 5. General properties of the Expert Advisor

After that we need to select the modules of trade signals used.

![Fig. 6. Signal properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_3__2.png "Fig. 6. Signal properties of the Expert Advisor")

Fig. 6. Signal properties of the Expert Advisor

In our case we use only one module of trade signals.

Adding the "**Signals based on Bullish Engulfing/Bearish Engulfing confirmed by CCI**" module of trading signals:

![Fig. 7. Signal properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_3_2_BEBE_CCI.png "Fig. 7. Signal properties of the Expert Advisor")

Fig. 7. Signal properties of the Expert Advisor

Module of trade signals added:

![Fig. 8. Signal properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_3_3_BEBE_CCI.png "Fig. 8. Signal properties of the Expert Advisor")

Fig. 8. Signal properties of the Expert Advisor

You can select any trailing properties, but we will use "Trailing Stop not used":

![Fig. 9. Trailing properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_4__3.png "Fig. 9. Trailing properties of the Expert Advisor")

Fig. 9. Trailing properties of the Expert Advisor

Concerning the money management properties, we will use "Trading with fixed trade volume":

![Fig. 10. Money management properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_5__3.png "Fig. 10. Money management properties of the Expert Advisor")

Fig. 10. Money management properties of the Expert Advisor

By pressing the "Finish" button, we will get the code of the generated Expert Advisor, located in Expert\_ABE\_BE\_CCI.mq5, it will be saved in terminal\_data\_folder\MQL5\Experts\.

The default input parameters of the generated Expert Advisor:

```mql5
//--- inputs for main signal
input int            Signal_ThresholdOpen   =10;     // Signal threshold value to open [0...100]
input int            Signal_ThresholdClose  =10;     // Signal threshold value to close [0...100]
input double         Signal_PriceLevel      =0.0;    // Price level to execute a deal
input double         Signal_StopLevel       =50.0;   // Stop Loss level (in points)
input double         Signal_TakeLevel       =50.0;   // Take Profit level (in points)
```

must be replaced to:

```mql5
//--- inputs for main signal
input int            Signal_ThresholdOpen   =40;     // Signal threshold value to open [0...100]
input int            Signal_ThresholdClose  =20;     // Signal threshold value to close [0...100]
input double         Signal_PriceLevel      =0.0;    // Price level to execute a deal
input double         Signal_StopLevel       =0.0;    // Stop Loss level (in points)
input double         Signal_TakeLevel       =0.0;    // Take Profit level (in points)
```

The Signal\_ThresholdOpen/Signal\_ThresholdClose input parameters allow to specify threshold levels for open and close of positions.

In code of the LongCondition() and ShortCondition() methods of the trade signals class we have specified
the fixed values of the threshold:

* Open position: 80;
* Close position: 40.

The Expert Advisor, generated by MQL5 Wizard open and close position using the "votes" from the modules of trade signals. The vote of the main module (as container, it consist of all the modules added) is also used, but its LongCondition() and ShortCondition() methods always return 0.

The vote results of the main module is also used in "votes" averaging. In our case we have:  main module + 1 module of trade signals, so we need to take this fact into account when setting of the threshold values. Because of this fact the ThresholdOpen and ThresholdClose must be set as 40=(0+80)/2 and 20=(0+40)/2.

The values of Signal\_StopLevel and Signal\_TakeLevel input parameters is set to 0, it means that closing of the positions will be done only when closing conditions will be true.

  

**2.4. History backtesting results**

Let's consider backtesting of the Expert Advisor on historical data (EURUSD H1, testing period: 2010.01.01-2011.03.16, PeriodCCI=49, MA\_period=11).

In creation of Expert Advisor we used the fixed volume ([Trading Fixed Lot](/en/docs/standardlibrary/expertclasses/samplemmclasses/cmoneyfixedlot), 0.1), Trailing Stop algorithm is not used ([Trailing not used](/en/docs/standardlibrary/expertclasses/sampletrailingclasses/ctrailingnone)).

![Fig. 11. Testing results of the Expert Advisor, based on Bullish Engulfing/Bearish Engulfing + CCI](https://c.mql5.com/18/2/BEBE_CCI_Test_Results.png "Fig. 11. Testing results of the Expert Advisor, based on Bullish Engulfing/Bearish Engulfing + CCI")

Fig. 11. Testing results of the Expert Advisor, based on Bullish Engulfing/Bearish Engulfing + CCI

  

The best set of input parameters can be found using the [Strategy Tester](https://www.metatrader5.com/en/automated-trading/strategy-tester "https://www.metatrader5.com/en/automated-trading/strategy-tester") of MetaTrader 5 client terminal.

The code of the Expert Advisor, created by MQL5 Wizard is attached in expert\_abe\_be\_cci.mq5.

Translated from Russian by MetaQuotes Ltd.   
Original code: [https://www.mql5.com/ru/code/306](/ru/code/306)

**Last comments |
[Go to discussion](/en/forum/3378)**
(5)

![rasif000](https://c.mql5.com/avatar/avatar_na2.png "rasif000")

**[rasif000](/en/users/rasif000)**
|
11 Mar 2011 at 23:19

I'm not able to create this EA. Please Help me to make this EA. I'm newbie in this section. Please Guide me step by step. Thanks

![muller](https://c.mql5.com/avatar/avatar_na2.png "muller")

**[muller](/en/users/razoff)**
|
12 Mar 2011 at 11:45

**rasif000:**

what do you mean ? this EA is already created :)

The first thing to do is: read the entire article, slowly if you need to understand it, everything is well explained about how/where to install the packages. Do your own effort.

![kono1](https://c.mql5.com/avatar/avatar_na2.png "kono1")

**[kono1](/en/users/kono1)**
|
20 Sep 2011 at 14:45

Hi, I tried this EA out and is good, it appears to compile OK.

However, when I tested on the Strategy Tester, I notice this error message in the Journal:

2011.09.20 20:27:31 Core 1 2011.01.04 00:00:00   CExpertBase::SetOtherSeries: changing of timeseries is forbidden  
2011.09.20 20:27:31 Core 1 2011.01.04 00:00:00   CExpertBase::SetPriceSeries: changing of timeseries is forbidden

I tried to debug but don't know enough about MQL5. Please help locate [the bug](/en/forum/628 "Get in touch with developers using Service Desk!").

Many thanx in advance.

Kon

![SeriousRacoon](https://c.mql5.com/avatar/2016/12/584241BB-DA92.jpg "SeriousRacoon")

**[SeriousRacoon](/en/users/seriousracoon)**
|
14 Jan 2017 at 11:59

Поскольку при расчете "голосов" производится усреднение по количеству присутствующих модулей (основной + один добавленный модуль), то значения порогов следует указывать с учетом данного обстоятельства. По этой причине значения величин Signal\_ThresholdOpen и Signal\_ThresholdClose должны быть установлены в 40=(0+80)/2 и 20=(0+40)/2 соответственно.

The parent signal does not participate in voting if its LongCondition and ShortCondition methods return the same values, or if the parent signal's weight is set to zero:

double result=m\_weight\*(LongCondition()-ShortCondition());  
 int    number=(result==0.0)? 0 : 1;

The non-overridden LongCondition and ShortCondition methods of the [CExpertSignal class](https://www.mql5.com/en/docs/standardlibrary/expertclasses/expertbaseclasses/cexpertsignal "Standard library: Class CExpertSignal") always return zero, so the statement "40=(0+80)/2 and 20=(0+40)/2 " is incorrect. Threshold values don't need to be divided by 2 (but testing with different values is quite interesting, sometimes you can see unusual effects).

I would still understand if this article was a hacked-up handiwork of local boneheads, but it's very strange that the developers themselves don't know which heel they're scratching.

![filipenax](https://c.mql5.com/avatar/avatar_na2.png "filipenax")

**[filipenax](/en/users/filipenax)**
|
28 Nov 2017 at 17:28

**Automated-Trading:**  

[MQL5 Wizard - Trading Signals Based on "Bearish Engulfing/Bearish Engulfing" + CCI](/en/code/306):

Author: [MetaQuotes Software Corp.](/pt/users/MetaQuotes "MetaQuotes")

  
 Dear Sirs, I am new here in MT5, and I tried to install your EA, but I faced the following screen. Could you help me? Best regards,

![MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + Stochastic](https://c.mql5.com/i/code/expert.png)
[MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + Stochastic](/en/code/305)

Trade signals based on "Bullish Engulfing/Bearish Engulfing" candlestick pattern, confirmed by Stochastic indicator is considered. The code of the Expert Advisor based on this strategy can be generated automatically using the MQL5 Wizard.

![CDownLoadHistory class](https://c.mql5.com/i/code/library.png)
[CDownLoadHistory class](/en/code/304)

The CDownLoadHistory class provides the methods of historical data downloading.

![MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + MFI](https://c.mql5.com/i/code/expert.png)
[MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + MFI](/en/code/307)

Trade signals based on "Bullish Engulfing/Bearish Engulfing" candlestick pattern, confirmed by Market Facilitation Index (MFI) indicator is considered. The code of the Expert Advisor based on this strategy can be generated automatically using the MQL5 Wizard.

![MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + RSI](https://c.mql5.com/i/code/expert.png)
[MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + RSI](/en/code/308)

Trade signals based on "Bullish Engulfing/Bearish Engulfing" candlestick pattern, confirmed by Relative Strength Index (RSI) indicator is considered. The code of the Expert Advisor based on this strategy can be generated automatically using the MQL5 Wizard.