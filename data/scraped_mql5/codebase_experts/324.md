# MQL5 Wizard - Trade Signals Based on Morning/Evening Stars + RSI - expert for MetaTrader 5

Source: https://www.mql5.com/en/code/324

* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 5](/en/code/mt5)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt5/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt5/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt5/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt5/libraries)
* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 4](/en/code/mt4)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt4/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt4/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt4/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt4/libraries)

* [![](https://c.mql5.com/i/sidebar/storage.svg)Storage](/en/code/storage)

Watch [how to download](https://youtu.be/rloNyFVtHuA?list=PLltlMLQ7OLeKwyQwC8FhiKwjl9syKhOCK) trading robots for free

Find us on [Facebook](https://www.facebook.com/mql5.community/)!  
 Join our fan page

Interesting script?  
So post a [link](/en/code/324) to it -  
let others appraise it

You liked the script? Try it in the [MetaTrader 5](https://download.mql5.com/cdn/web/metaquotes.ltd/mt5/MetaTrader5.pkg.zip?utm_source=www.mql5.com&utm_campaign=download) terminal

[to pocket](#pocket "Pocket allows you to insert a complete content description to the appropriate comment")

![Experts](https://c.mql5.com/i/code/expert.png)

# MQL5 Wizard - Trade Signals Based on Morning/Evening Stars + RSI - expert for MetaTrader 5

[MetaQuotes](/en/users/metaquotes)  |
[English](javascript:void(false);)

[Русский](/ru/code/324) [中文](/zh/code/324) [Español](/es/code/324) [Deutsch](/de/code/324) [日本語](/ja/code/324) [Português](/pt/code/324) [한국어](/ko/code/324) [Français](/fr/code/324) [Italiano](/it/code/324) [Türkçe](/tr/code/324)

Views:
:   53958

Rating:
:   (82)

Published:
:   28 March 2011, 13:06

Updated:
:   22 November 2016, 07:32
:   \MQL5\Include\Expert\Signal\MySignals\

    [acandlepatterns.mqh](/en/code/download/324/acandlepatterns.mqh "acandlepatterns.mqh")
    (20.34 KB)
    [view](# "view")

    [acms\_es\_rsi.mqh](/en/code/download/324/acms_es_rsi.mqh "acms_es_rsi.mqh")
    (7.95 KB)
    [view](# "view")

    [expert\_ams\_es\_rsi.mq5](/en/code/download/324/expert_ams_es_rsi.mq5 "expert_ams_es_rsi.mq5")
    (6.63 KB)
    [view](# "view")

    [Download as ZIP](/en/code/download/324.zip "Download all attachments in the single ZIP archive") [How to download code from MetaEditor](https://www.metatrader5.com/en/metaeditor/help/workspace/toolbox#codebase)

    ![MQL5 - Language of trade strategies built-in the MetaTrader 5 client terminal](https://c.mql5.com/i/registerlandings/logo-2.png)

    You are missing trading opportunities:

    * Free trading apps
    * Over 8,000 signals for copying
    * Economic news for exploring financial markets

    Registration
    Log in

    latin characters without spaces

    a password will be sent to this email

    An error occurred

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup&amp;reg=1)

    You agree to [website policy](/en/about/privacy) and [terms of use](/en/about/terms)

    If you do not have an account, please [register](https://www.mql5.com/en/auth_register)

    Allow the use of cookies to log in to the MQL5.com website.

    Please enable the necessary setting in your browser, otherwise you will not be able to log in.

     

    [Forgot your login/password?](https://www.mql5.com/en/auth_forgotten?return=popup)

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup)
:   ![MQL5 Freelance](https://c.mql5.com/i/code/icon_freelance.svg)
    Need a robot or indicator based on this code? Order it on Freelance
    [Go to Freelance](/en/job/new "Go to Freelance")

The [MQL5 Wizard](https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate "https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate") allows creating ready-made Expert Advisors based on the [Standard library](/en/docs/standardlibrary/expertclasses) classes delivered together with the client terminal (see [Creating Ready-Made Expert Advisors in MQL5 Wizard](https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate "https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate") for the details). It allows to check your trade ideas quickly, all you need is to create your own trading signals class. The structure of this class and example can be found in the article [MQL5 Wizard: How to Create a Module of Trading Signals](/en/articles/226).

The generic idea is the following: the class of trading signals is derived from [CExpertSignal](/en/docs/standardlibrary/expertclasses/expertbaseclasses/cexpertsignal), the next, it's necessary to override the [LongCondition()](/en/docs/standardlibrary/expertclasses/expertbaseclasses/cexpertsignal/cexpertsignallongcondition) and [ShortCondition()](/en/docs/standardlibrary/expertclasses/expertbaseclasses/cexpertsignal/cexpertsignalshortcondition) virtual methods with your own methods.

There is a book ["Strategies of best traders](/go?link=http://www.tora-centre.ru/book/strategy.htm "http://www.tora-centre.ru/book/strategy.htm")" (in Russian), there are many trading strategies are considered there, we will focus on reversal candlestick patterns, confirmed by [Stochastic](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/so "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/so"), [CCI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci"), [MFI](https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation "https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation") and [RSI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi") oscillators.

The best way is to create the separate class, derived from [CExpertSignal](/en/docs/standardlibrary/expertclasses/expertbaseclasses/cexpertsignal) for checking of formation of candlestick patterns. For confirmation of trade signals, generated by candlestick patterns, it's sufficient to write the class, derived from CCandlePattern and add the necessary features (for example, confirmation by oscillators) there.

Here we will consider the signals, based on "Morning Star/Evening Star" (Morning Doji Star/Evening Doji Star) reversal candlestick patterns, confirmed by [RSI](https://www.metatrader5.com/ru/terminal/help/indicators/oscillators/rsi "https://www.metatrader5.com/ru/terminal/help/indicators/oscillators/rsi") indicator. The module of trade signals is based on the [CCandlePattern](/en/code/291) class, it's simple example of its use for creation of trade signals with candlestick patterns.

  

****1. "Morning Star" and "Evening Star" reversal candlestick patterns****

**1.1. Morning Star**

This pattern indicates the reversal of downtrend, it consists of three candles (Fig. 1). After a long black candle there is a candle (the color isn't important) with a small body, which lies outside the body of the black candle. The small body of a candle means that strengths of the bulls and bears are equal and market is ready to change the trend.

The third candle of the pattern is the bullish candle, it's body isn't overlapped with the body of the second candle, and close price lies inside the body of the first (bearish) candle. The resulting candle of the model is also plotted at Figure 1.

For the case if the second candle is doji-like-candle, the model is named "Morning Doji Star".

![Fig. 1. "Morning Star" and "Morning Doji Star" candlestick patterns](https://c.mql5.com/18/2/morning_stars_en__5.png "Fig. 1. \"Morning Star\" and \"Morning Doji Star\" candlestick patterns")

Fig. 1. "Morning Star" and "Morning Doji Star" candlestick patterns

The identifying of "Morning Star" pattern is implemented in CheckPatternMorningStar() and CheckPatternMorningDoji() methods of [CCandlePattern](/en/code/291) class:

```mql5
//+------------------------------------------------------------------+
//| Check formation of the "Morning Star" pattern                    |
//+------------------------------------------------------------------+
bool CCandlePattern::CheckPatternMorningStar()
  {
//--- Morning Star
   if((Open(3)-Close(3)>AvgBody(1))             && // bearish candle, its body greater than average candle body
      (MathAbs(Close(2)-Open(2))<AvgBody(1)*0.5) && // second candle has small body (lower than half of the average body)
      (Close(2)<Close(3))                       && // close of the second candle is lower than close of the first 
      (Open(2)<Open(3))                         && // open of the second canlde is lower than open of the first
      (Close(1)>MidOpenClose(3)))                  // close of the last completed candle is higher than center of the first 
      return(true);
//---
   return(false);
  }
//+------------------------------------------------------------------+
//| Check formation of the "Morning Doji Star" pattern               |
//+------------------------------------------------------------------+
bool CCandlePattern::CheckPatternMorningDoji()
  {
//--- Morning Doji Star
   if((Open(3)-Close(3)>AvgBody(1)) && // bearish candle with body greater than average candle body 
      (AvgBody(2)<AvgBody(1)*0.1)   && // the second candle has a very small body (doji) 
      (Close(2)<Close(3))           && // close of the second candle is lower than close of the first 
      (Open(2)<Open(3))             && // open of the second candle is lower than open of the first
      (Open(1)>Close(2))            && // up gap at the last completed candle
      (Close(1)>Close(2)))             // close of the last completed candle is higher than close of the second
      return(true);
//---
   return(false);
  }
```

The CheckCandlestickPattern(CANDLE\_PATTERN\_MORNING\_STAR) and CheckCandlestickPattern(CANDLE\_PATTERN\_MORNING\_DOJI) methods of [CCandlePattern](/en/code/291) class are used to check formation of "Morning Star" and "Morning Doji Star" candlestick patterns.

  

**1.2. Evening Star**

This pattern indicates the reversal of uptrend, it consists of three candles (Fig. 2). After a long white candles there is a candle (the color isn't important) with a small body, which lies outside the body of the white candle. The small body of a candle means that strengths of the bulls and bears are equal and market is ready to change the trend.

The third candle of the pattern is the bearish candle, it's body isn't overlapped with the body of the second candle, and close price lies inside the body of the first (bullish) candle. The resulting candle of the model is also plotted at Figure 2.

For the case if the second candle is doji-like-candle, the model is named "Evening Doji Star".

![Fig. 2. "Evening Star" and "Evening Doji Star" candlestick patterns](https://c.mql5.com/18/2/evening_stars_en__5.png "Fig. 2. \"Evening Star\" and \"Evening Doji Star\" candlestick patterns")

Fig. 2. "Evening Star" and "Evening Doji Star" candlestick patterns

Here are the methods for identifying of "Evening Star" and "Evening Doji Star" patterns:

```mql5
//+------------------------------------------------------------------+
//| Check formation of the "Evening Star" pattern                    |
//+------------------------------------------------------------------+
bool CCandlePattern::CheckPatternEveningStar()
  {
//--- Evening Star
   if((Close(3)-Open(3)>AvgBody(1))             && // bullish candle with body higher than average body 
      (MathAbs(Close(2)-Open(2))<AvgBody(1)*0.5) && // second candle has a small body (less than half of the average)
      (Close(2)>Close(3))                       && // close of the second candle is higher than close of the first
      (Open(2)>Open(3))                         && // open of the second candle is higher than open of the first
      (Close(1)<MidOpenClose(3)))                  // close of the last completed candle is lower than center of the first 
      return(true);
//---
   return(false);
  }
//+------------------------------------------------------------------+
//| Check formation of the "Evening Doji Star" pattern               |
//+------------------------------------------------------------------+
bool CCandlePattern::CheckPatternEveningDoji()
  {
//--- Evening Doji Star
   if((Close(3)-Open(3)>AvgBody(1)) && // bullish candle with body higher than average 
      (AvgBody(2)<AvgBody(1)*0.1)   && // second candle has a very small body (doji) 
      (Close(2)>Close(3))           && // close of the second candle is higher than close of the first
      (Open(2)>Open(3))             && // opend of the second candle is higher than open of the first
      (Open(1)<Close(2))            && // down gap at the last completed candle
      (Close(1)<Close(2)))             // close of the last completed candle is lower than close of the second 
      return(true);
//---
   return(false);
  }
```

The CheckCandlestickPattern(CANDLE\_PATTERN\_EVENING\_STAR) and CheckCandlestickPattern(CANDLE\_PATTERN\_EVENING\_DOJI) methods of [CCandlePattern](/en/code/291) class are used to check formation of "Evening Star" and "Evening Doji Star" candlestick patterns.

  

**2. Trade signals, confirmed by RSI indicator**

The trading signals to open long or short position must be confirmed by [RSI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi") indicator. The value of RSI must be lower/greater than critical levels (40 for long position and 60 for short position).

The closing of opened position depends on the values of [RSI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi"). It can be done in 2 cases:

1. if RSI has reached the opposite critical level (70 for long position and 30 for short position)
2. if the reverse signal isn't confirmed (when RSI reaches the following levels: 30 for long position and 70 for short position)

![Fig. 3. "Morning Star" pattern, confirmed by RSI indicator](https://c.mql5.com/18/2/Image_MS_ES_RSI__1.png "Fig. 3. \"Morning Star\" pattern, confirmed by RSI indicator")

Fig. 3. "Morning Star" pattern, confirmed by RSI indicator

* int CMS\_ES\_RSI::LongCondition() - checks conditions to open long position (returns 80) and close of the short position (returns 40);
* int CMS\_ES\_RSI::ShortCondition() - checks conditions to open short position (returns 80) and close of the long position (returns 40).

  

**2.1. Open long position/Close short position**

1. The formation of "Morning Star" pattern must be confirmed by [RSI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi") indicator: 
   RSI(1)<40 (the value of the RSI of the last completed bar must be less than 40).
2. The short position must be closed if [RSI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi") indicator has crossed upward the critical levels 70 or 30.

```mql5
//+------------------------------------------------------------------+
//| Checks conditions for entry and exit from market                 |
//| 1) Market entry (open long position, result=80)                  |
//| 2) Market exit (close short position, result=40)                 |
//+------------------------------------------------------------------+
int CMS_ES_RSI::LongCondition()
  {
   int result=0;
//--- idx can be used to determine Expert Advisor work mode
//--- idx=0 - in this case EA checks trade conditions at each tick
//--- idx=1 - in this case EA checks trade consition only at news bars
   int idx   =StartIndex();
//--- checking of conditions to open long position
//--- formation of Morning Star pattern and RSI<30
  if(CheckCandlestickPattern(CANDLE_PATTERN_MORNING_STAR) && (RSI(1)<40))
     result=80;
//--- checking of conditions to close short position
//--- signal line crossover of overbought/oversold levels (upward 30, upward 70)
  if(((RSI(1)>30) && (RSI(2)<30)) || ((RSI(1)>70) && (RSI(2)<70)))
     result=40;
//--- return result
   return(result);
  }
```

**2.2. Open short position/Close long position**

1. The formation of "Evening Star" pattern must be confirmed by [RSI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi") indicator: RSI(1)>60 (the value of the RSI indicator of the last completed bar must be greater than 60).
2. The long position must be closed if [RSI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi") indicator has crossed downward the critical levels 70 or 30.

```mql5
//+------------------------------------------------------------------+
//| Checks conditions for entry and exit from market                 |
//| 1) Market entry (open short position, result=80)                 |
//| 2) Market exit (close long position, result=40)                  |
//+------------------------------------------------------------------+
int CMS_ES_RSI::ShortCondition()
  {
   int result=0;
//--- idx can be used to determine Expert Advisor work mode
//--- idx=0 - in this case EA checks trade conditions at each tick
//--- idx=1 - in this case EA checks trade consition only at news bars
   int idx   =StartIndex();
//--- checking of conditions to open short position
//--- formation of Evening Star pattern and RSI>60
  if(CheckCandlestickPattern(CANDLE_PATTERN_EVENING_STAR) && (RSI(1)>60))
     result=80;
//--- checking of conditions to close long position
//--- signal line crossover of overbought/oversold levels (downward 70, downward 30)
   if(((RSI(1)<70) && (RSI(2)>70)) || ((RSI(1)<30) && (RSI(2)>30)))
     result=40;
//--- return result
   return(result);
  }
```

  
  

**2.3. Creating Expert Advisor using MQL5 Wizard**

The CMS\_ES\_RSI class isn't included in the Standard Library classes, to use it, it's necessary to download the acms\_es\_rsi.mqh file (see attachments) and save it to the client\_terminal\_data\_folder\MQL5\Include\Expert\Signal\MySignals. The same should be done with the candlepatterns.mqh file. You can use it in MQL5 Wizard after restart of the MetaEditor.

To create an Expert Advisor launch [MQL5 Wizard](https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate "https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate"):

![Fig. 4. Creating Expert Advisor using MQL5 Wizard](https://c.mql5.com/18/2/MQL5_Wizard_step_1__36.png "Fig. 4. Creating Expert Advisor using MQL5 Wizard")

Fig. 4. Creating Expert Advisor using MQL5 Wizard

Let's specify the name of the Expert Advisor:

![Fig. 5. General properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_2_MSES_RSI.png "Fig. 5. General properties of the Expert Advisor")

Fig. 5. General properties of the Expert Advisor

After that we need to select the modules of trade signals used.

![Fig. 6. Signal properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_3__36.png "Fig. 6. Signal properties of the Expert Advisor")

Fig. 6. Signal properties of the Expert Advisor

In our case we use only one module of trade signals.

Adding the "**Signals based on Morning/Evening Stars confirmed by RSI**" module of trading signals:

![Fig. 7. Signal properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_3_2_MSES_RSI.png "Fig. 7. Signal properties of the Expert Advisor")

Fig. 7. Signal properties of the Expert Advisor

Module of trade signals added:

![Fig. 8. Signal properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_3_3_MSES_RSI.png "Fig. 8. Signal properties of the Expert Advisor")

Fig. 8. Signal properties of the Expert Advisor

You can select any trailing properties, but we will use "Trailing Stop not used":

![Fig. 9. Trailing properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_4__37.png "Fig. 9. Trailing properties of the Expert Advisor")

Fig. 9. Trailing properties of the Expert Advisor

Concerning the money management properties, we will use "Trading with fixed trade volume":

![Fig. 10. Money management properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_5__37.png "Fig. 10. Money management properties of the Expert Advisor")

Fig. 10. Money management properties of the Expert Advisor

By pressing the "Finish" button, we will get the code of the generated Expert Advisor, located in Expert\_AMS\_ES\_RSI.mq5, it will be saved in terminal\_data\_folder\MQL5\Experts\.

The default input parameters of the generated Expert Advisor:

```mql5
//--- inputs for main signal
input int            Signal_ThresholdOpen   =10;     // Signal threshold value to open [0...100]
input int            Signal_ThresholdClose  =10;     // Signal threshold value to close [0...100]
input double         Signal_PriceLevel      =0.0;    // Price level to execute a deal
input double         Signal_StopLevel       =50.0;   // Stop Loss level (in points)
input double         Signal_TakeLevel       =50.0;   // Take Profit level (in points)
```

must be replaced to:

```mql5
//--- inputs for main signal
input int            Signal_ThresholdOpen   =40;     // Signal threshold value to open [0...100]
input int            Signal_ThresholdClose  =20;     // Signal threshold value to close [0...100]
input double         Signal_PriceLevel      =0.0;    // Price level to execute a deal
input double         Signal_StopLevel       =0.0;    // Stop Loss level (in points)
input double         Signal_TakeLevel       =0.0;    // Take Profit level (in points)
```

The Signal\_ThresholdOpen/Signal\_ThresholdClose input parameters allow to specify threshold levels for open and close of positions.

In code of the LongCondition() and ShortCondition() methods of the trade signals class we have specified
the fixed values of the threshold:

* Open position: 80;
* Close position: 40.

The Expert Advisor, generated by MQL5 Wizard open and close position using the "votes" from the modules of trade signals. The vote of the main module (as container, it consist of all the modules added) is also used, but its LongCondition() and ShortCondition() methods always return 0.

The vote results of the main module is also used in "votes" averaging. In our case we have:  main module + 1 module of trade signals, so we need to take this fact into account when setting of the threshold values. Because of this fact the ThresholdOpen and ThresholdClose must be set as 40=(0+80)/2 and 20=(0+40)/2.

The values of  Signal\_StopLevel and Signal\_TakeLevel input parameters is set to 0, it means that closing of the positions will be done only when closing conditions will be true.

  

**2.4. History backtesting results**

Let's consider backtesting of the Expert Advisor on historical data (EURUSD H1, testing period: 2010.01.01-2011.03.04, PeriodK=47, PeriodD=9, PeriodSlow=13, MA\_period=5).

In creation of Expert Advisor we used the fixed volume ([Trading Fixed Lot](/en/docs/standardlibrary/expertclasses/samplemmclasses/cmoneyfixedlot), 0.1), Trailing Stop algorithm is not used ([Trailing not used](/en/docs/standardlibrary/expertclasses/sampletrailingclasses/ctrailingnone)).

![Fig. 11. Testing results of the Expert Advisor, based on Morning/Evening Stars + RSI](https://c.mql5.com/18/2/MSES_RSI_Test_Results.png "Fig. 11. Testing results of the Expert Advisor, based on Morning/Evening Stars + RSI")

Fig. 11. Testing results of the Expert Advisor, based on Morning/Evening Stars + RSI

  

The best set of input parameters can be found using the [Strategy Tester](https://www.metatrader5.com/en/automated-trading/strategy-tester "https://www.metatrader5.com/en/automated-trading/strategy-tester") of MetaTrader 5 client terminal.

The study of parameters is greatly simplified using the [3D-Visualization mode of Strategy Tester](/en/forum/3448), added starting from 419 build (Fig. 12):

![Fig. 12. Balance as a function of PeriodRSI and MA_period input parameters.](https://c.mql5.com/18/2/3D__1.png "Fig. 12. Balance as a function of PeriodRSI and MA_period input parameters.")

Fig. 12. Balance as a function of PeriodRSI and MA\_period input parameters

The code of the Expert Advisor, created by MQL5 Wizard is attached in expert\_ms\_es\_rsi.mq5.

Translated from Russian by MetaQuotes Ltd.   
Original code: [https://www.mql5.com/ru/code/324](/ru/code/324)

**Last comments |
[Go to discussion](/en/forum/3528)**
(14)

![vlad-mir](https://c.mql5.com/avatar/avatar_na2.png "vlad-mir")

**[vlad-mir](/en/users/vlad-mir)**
|
25 Dec 2013 at 07:51

The error " Core 1 tester stopped because OnInit failed".

Before that " 2013.12.25 10:47:33 Core 1 2013.01.01 00:00:00 CExpert::ValidationSettings: error money parameters"

"2013.12.25 10:47:33 Core 1 2013.01.01 00:00:00 [CMoneyFixedLot::ValidationSettings:](https://www.mql5.com/en/docs/standardlibrary/expertclasses/samplemmclasses/cmoneyfixedlot "Standard library: Class CMoneyFixedLot") lot amount must be in the range from 1.000000 to 1000.000000"

![Zhang Zhang](https://c.mql5.com/avatar/avatar_na2.png "Zhang Zhang")

**[Zhang Zhang](/en/users/mihawk)**
|
30 Mar 2015 at 17:01

bool CCandlePattern::CheckPatternPiercingLine()  
  {

      (Close(1)<Open(2))            &&   
      (MidOpenClose(2)<CloseAvg(2)) && // downtrend  
      (Open(1)<Low(2)))                // close inside previous body  
      return(true);  
//---  
   return(false);  
  }

![Stefano Oddo](https://c.mql5.com/avatar/avatar_na2.png "Stefano Oddo")

**[Stefano Oddo](/en/users/stefanoddo)**
|
29 Nov 2016 at 17:23

This EA  close itself order?

![Vasyl Calko](https://c.mql5.com/avatar/2016/5/5730A116-8431.png "Vasyl Calko")

**[Vasyl Calko](/en/users/lender)**
|
24 Dec 2016 at 04:23

there are logical errors  in the CheckPatternMorningDoji() and CheckPatternEveningDoji()  methods in the lines:

(AvgBody(2)<AvgBody(1)\*0.1) && // body of the second doji candle (less than a tenth of the average body size)

и

(AvgBody(2)<AvgBody(1)\*0.1) && // body of the second doji candle (less than a tenth of the average candle body size)

respectively.

Should be:

([MathAbs](https://www.mql5.com/en/docs/math/mathabs "MQL5 documentation: MathAbs function")(Close(2)-Open(2))<AvgBody(1)\*0.1) && // the body of the second doji candle (less than a tenth of the average body size)

и

(MathAbs(Close(2)-Open(2))<AvgBody(1)\*0.1) && // body of the second doji candlestick (less than a tenth of the average candlestick body)

the same error has also appeared in "Class for working with candlestick patterns" https://www.mql5.com/en/code/291.

![bastien andrew](https://c.mql5.com/avatar/2019/9/5D6FB96D-FF98.png "bastien andrew")

**[bastien andrew](/en/users/bastienandrew)**
|
5 Sep 2019 at 11:47

This isnt working on my computer how do i go about it? I try to compile and find 38 errors

![MQL5 Wizard - Trade Signals Based on Morning/Evening Stars + MFI](https://c.mql5.com/i/code/expert.png)
[MQL5 Wizard - Trade Signals Based on Morning/Evening Stars + MFI](/en/code/323)

Trade signals based on "Morning Star/Evening Star" candlestick patterns, confirmed by Market Facilitation Index (MFI) indicator is considered. The code of the Expert Advisor based on this strategy can be generated automatically using the MQL5 Wizard.

![MQL5 Wizard - Trade Signals Based on Morning/Evening Stars + CCI](https://c.mql5.com/i/code/expert.png)
[MQL5 Wizard - Trade Signals Based on Morning/Evening Stars + CCI](/en/code/322)

Trade signals based on "Morning Star/Evening Star" candlestick patterns, confirmed by Commodity Channel Index (CCI) indicator is considered. The code of the Expert Advisor based on this strategy can be generated automatically using the MQL5 Wizard.

![iChartsSwitchSymbol](https://c.mql5.com/i/code/indicator.png)
[iChartsSwitchSymbol](/en/code/326)

When the chart symbol is changed, it will change symbols for all other charts.

![RouletteGame](https://c.mql5.com/i/code/expert.png)
[RouletteGame](/en/code/271)

The Roulette Game.