# Fib_SR_6 - indicator for MetaTrader 5

Source: https://www.mql5.com/en/code/23196

* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 5](/en/code/mt5)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt5/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt5/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt5/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt5/libraries)
* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 4](/en/code/mt4)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt4/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt4/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt4/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt4/libraries)

* [![](https://c.mql5.com/i/sidebar/storage.svg)Storage](/en/code/storage)

Watch [how to download](https://youtu.be/rloNyFVtHuA?list=PLltlMLQ7OLeKwyQwC8FhiKwjl9syKhOCK) trading robots for free

Find us on [Facebook](https://www.facebook.com/mql5.community/)!  
 Join our fan page

Interesting script?  
So post a [link](/en/code/23196) to it -  
let others appraise it

You liked the script? Try it in the [MetaTrader 5](https://download.mql5.com/cdn/web/metaquotes.ltd/mt5/MetaTrader5.pkg.zip?utm_source=www.mql5.com&utm_campaign=download) terminal

[to pocket](#pocket "Pocket allows you to insert a complete content description to the appropriate comment")

![Indicators](https://c.mql5.com/i/code/indicator.png)

# Fib\_SR\_6 - indicator for MetaTrader 5

[Nikolay Kositsin](/en/users/godzilla)  |
[English](javascript:void(false);)

[Русский](/ru/code/23196) [中文](/zh/code/23196) [Español](/es/code/23196) [Deutsch](/de/code/23196) [日本語](/ja/code/23196) [Português](/pt/code/23196)

Views:
:   9119

Rating:
:   (16)

Published:
:   2 January 2019, 14:34
:   [Fib\_SR\_6.mq5](/en/code/download/23196/fib_sr_6.mq5 "Fib_SR_6.mq5")
    (17 KB)
    [view](# "view")

    [Download as ZIP](/en/code/download/23196.zip "Download all attachments in the single ZIP archive") [How to download code from MetaEditor](https://www.metatrader5.com/en/metaeditor/help/workspace/toolbox#codebase)

    ![MQL5 - Language of trade strategies built-in the MetaTrader 5 client terminal](https://c.mql5.com/i/registerlandings/logo-2.png)

    You are missing trading opportunities:

    * Free trading apps
    * Over 8,000 signals for copying
    * Economic news for exploring financial markets

    Registration
    Log in

    latin characters without spaces

    a password will be sent to this email

    An error occurred

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup&amp;reg=1)

    You agree to [website policy](/en/about/privacy) and [terms of use](/en/about/terms)

    If you do not have an account, please [register](https://www.mql5.com/en/auth_register)

    Allow the use of cookies to log in to the MQL5.com website.

    Please enable the necessary setting in your browser, otherwise you will not be able to log in.

     

    [Forgot your login/password?](https://www.mql5.com/en/auth_forgotten?return=popup)

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup)
:   ![MQL5 Freelance](https://c.mql5.com/i/code/icon_freelance.svg)
    Need a robot or indicator based on this code? Order it on Freelance
    [Go to Freelance](/en/job/new "Go to Freelance")

**Original author:**

Eli hayun

The [Fib\_SR](/en/code/23195) with two additional support and resistance zones.

![Fig.1. The Fib_SR_6 indicator](https://c.mql5.com/18/77/picture_1__4.png "Fig.1. The Fib_SR_6 indicator")

Fig.1. The Fib\_SR\_6 indicator

Translated from Russian by MetaQuotes Ltd.   
Original code: [https://www.mql5.com/ru/code/23196](/ru/code/23196)

**Last comments |
[Go to discussion](/en/forum/297364)**
(1)

![zemo](https://c.mql5.com/avatar/2018/2/5A79AE10-E9F2.JPG "zemo")

**[zemo](/en/users/zemo)**
|
3 Dec 2018 at 13:42

Hi Mr Nicholas,

I added "relative timeframes".

```mql5
//+------------------------------------------------------------------+
//|Fib_SR_6.mq5 |
//|Copyright © 2006, Eli hayun |
//| http://www.elihayun.com |
//+------------------------------------------------------------------+
//---- indicator authorship
#property copyright "Copyright © 2006, Eli hayun."
//---- link to author's website
#property link      "http://www.elihayun.com"
//---- indicator version number
#property version   "1.00"
//---- draw indicator in the main window
#property indicator_chart_window 
//---- no buffers are used for calculation and drawing of the indicator
#property indicator_buffers 0
//---- zero graphical constructions used
#property indicator_plots   0
//+----------------------------------------------+ 
//| declaring constants |
//+----------------------------------------------+
#define  RESET     0            // Constant for returning to the terminal the command to recalculate the indicator
#define  FIB_RES3 "FIB_RES_3"
#define  FIB_RES2 "FIB_RES_2"
#define  FIB_RES1 "FIB_RES_1"
#define  FIB_SUP1 "FIB_SUP_1"
#define  FIB_SUP2 "FIB_SUP_2"
#define  FIB_SUP3 "FIB_SUP_3"


//---
//
enum enTimeFrames
  {
   tf_cu  = PERIOD_CURRENT, // Current time frame
   tf_m1  = PERIOD_M1,      // 1 minute
   tf_m2  = PERIOD_M2,      // 2 minutes
   tf_m3  = PERIOD_M3,      // 3 minutes
   tf_m4  = PERIOD_M4,      // 4 minutes
   tf_m5  = PERIOD_M5,      // 5 minutes
   tf_m6  = PERIOD_M6,      // 6 minutes
   tf_m10 = PERIOD_M10,     // 10 minutes
   tf_m12 = PERIOD_M12,     // 12 minutes
   tf_m15 = PERIOD_M15,     // 15 minutes
   tf_m20 = PERIOD_M20,     // 20 minutes
   tf_m30 = PERIOD_M30,     // 30 minutes
   tf_h1  = PERIOD_H1,      // 1 hour
   tf_h2  = PERIOD_H2,      // 2 hours
   tf_h3  = PERIOD_H3,      // 3 hours
   tf_h4  = PERIOD_H4,      // 4 hours
   tf_h6  = PERIOD_H6,      // 6 hours
   tf_h8  = PERIOD_H8,      // 8 hours
   tf_h12 = PERIOD_H12,     // 12 hours
   tf_d1  = PERIOD_D1,      // daily
   tf_w1  = PERIOD_W1,      // weekly
   tf_mn  = PERIOD_MN1,     // monthly
   tf_cp1 = -1,             // Next higher time frame
   tf_cp2 = -2,             // Second higher time frame
   tf_cp3 = -3,             // Third higher time frame
   tf_cp4 = -4,             // Fourth higher time frame
   tf_cp5 = -5,             // Fifth higher time frame
   tf_cp6 = -6              // Sixth higher time frame
  };

ENUM_TIMEFRAMES _tfsPer[]={PERIOD_M1,PERIOD_M2,PERIOD_M3,PERIOD_M4,PERIOD_M5,PERIOD_M6,PERIOD_M10,PERIOD_M12,PERIOD_M15,PERIOD_M20,PERIOD_M30,PERIOD_H1,PERIOD_H2,PERIOD_H3,PERIOD_H4,PERIOD_H6,PERIOD_H8,PERIOD_H12,PERIOD_D1,PERIOD_W1,PERIOD_MN1};
  
//
//---
// 

//+----------------------------------------------+
//|| Indicator input parameters |
//+----------------------------------------------+
input enTimeFrames      InpTimeframe  =  tf_cp1; // Indicator Next timeframe for calculating the indicator
input uint NumberofBar = 1; // Bar number to calculate the indicator
input double Ratio1 = 0.618; // first ratio
input double Ratio2 = 1.382; // second ratio
input double Ratio3 = 2.764; // second ratio
input double Step = 0.50; // extension step
input color Color_Res3 = clrAqua; // colour of the third resistance zone
input color Color_Res2 = clrLime; // colour of the second resistance zone
input color Color_Res1 = clrGreen; // colour of the first resistance zone
input color Color_Sup1 = clrRed; // colour of the first support zone
input color Color_Sup2 = clrMagenta; // colour of the second support zone
input color Color_Sup3 = clrYellow; // colour of the third support zone
input uint RightTail = 60; // protrusion of rectangles beyond the zero bar to the right in minutes
input uint LeftTail = 60; // protrusion of rectangles behind the starting bar to the left in minutes
//+----------------------------------------------+
uint SecondRightTail,SecondLeftTail;
//--- global variables
ENUM_TIMEFRAMES   Timeframe;


//+------------------------------------------------------------------+
//| Creating a rectangular object|
//+------------------------------------------------------------------+
void CreateRectangle
(
 long     chart_id,      // chart identifier
 string   name,          // object name
 int      nwin,          // window index
 datetime time1,         // time 1
 double   price1,        // price 1
 datetime time2,         // time 2
 double   price2,        // price 2
 color    Color,         // line colour
 bool     background,    // background display of the line
 string   text           // text
 )
//---- 
  {
//----
   ObjectCreate(chart_id,name,OBJ_RECTANGLE,nwin,time1,price1,time2,price2);
   ObjectSetInteger(chart_id,name,OBJPROP_COLOR,Color);
   ObjectSetInteger(chart_id,name,OBJPROP_FILL,true);
   ObjectSetString(chart_id,name,OBJPROP_TEXT,text);
   ObjectSetInteger(chart_id,name,OBJPROP_BACK,background);
   ObjectSetString(chart_id,name,OBJPROP_TOOLTIP,"\n"); //prohibit tooltip
   ObjectSetInteger(chart_id,name,OBJPROP_BACK,true); //object in the background
//----
  }
//+------------------------------------------------------------------+
//|| Resetting a rectangular object |
//+------------------------------------------------------------------+
void SetRectangle
(
 long     chart_id,      // chart identifier
 string   name,          // object name
 int      nwin,          // window index
 datetime time1,         // time 1
 double   price1,        // price 1
 datetime time2,         // time 2
 double   price2,        // price 2
 color    Color,         // line colour
 bool     background,    // background display of the line
 string   text           // text
 )
//---- 
  {
//----
   if(ObjectFind(chart_id,name)==-1) CreateRectangle(chart_id,name,nwin,time1,price1,time2,price2,Color,background,text);
   else
     {
      ObjectSetString(chart_id,name,OBJPROP_TEXT,text);
      ObjectMove(chart_id,name,0,time1,price1);
      ObjectMove(chart_id,name,1,time2,price2);
     }
//----
  }
//+------------------------------------------------------------------+
//| Custom indicator initialisation function |
//+------------------------------------------------------------------+ 
int OnInit()
  {
//---Calculate indicator timeframe 
   Timeframe = MathMax(timeFrameGet((int)InpTimeframe),_Period);
  
//----
   SecondRightTail=RightTail*60;
   SecondLeftTail=LeftTail*60;
//---- determining the accuracy of displaying indicator values
   IndicatorSetInteger(INDICATOR_DIGITS,_Digits);
//---- create labels to display in DataWindow and name to display in separate subwindow and tooltip
   IndicatorSetString(INDICATOR_SHORTNAME,"Fib_SR");
//---- initialisation complete
   return(INIT_SUCCEEDED);
  }
//+------------------------------------------------------------------+
//| Custom indicator deinitialisation function |
//+------------------------------------------------------------------+ 
void OnDeinit(const int reason)
  {
//----
   ObjectDelete(0,FIB_SUP3);
   ObjectDelete(0,FIB_SUP2);
   ObjectDelete(0,FIB_SUP1);
   ObjectDelete(0,FIB_RES1);
   ObjectDelete(0,FIB_RES2);
   ObjectDelete(0,FIB_RES3);
//----
   ChartRedraw(0);
  }
//+------------------------------------------------------------------+
//| Custom indicator iteration function |
//+------------------------------------------------------------------+
int OnCalculate(
                const int rates_total,    // the amount of history in bars on the current tick
                const int prev_calculated,// the amount of history in bars on the previous tick
                const datetime &time[],
                const double &open[],
                const double& high[],     // price array of price maxima for indicator calculation
                const double& low[],      // price array of price minima for indicator calculation
                const double &close[],
                const long &tick_volume[],
                const long &volume[],
                const int &spread[]
                )
  {
//---- 
   if(prev_calculated==rates_total && NumberofBar) return(rates_total);
//----
   double nClose[1],nHigh[1],nLow[1];
   datetime nTime[1];
   int to_copy;

//---- indexing of elements in arrays as in timeseries 
   ArraySetAsSeries(time,true);
//----
   to_copy=1;
//----
   if(CopyTime(NULL,Timeframe,0,to_copy,nTime)<to_copy)return(RESET);
   if(CopyClose(NULL,Timeframe,NumberofBar,to_copy,nClose)<to_copy)return(RESET);
   if(CopyHigh(NULL,Timeframe,NumberofBar,to_copy,nHigh)<to_copy)return(RESET);
   if(CopyLow(NULL,Timeframe,NumberofBar,to_copy,nLow)<to_copy)return(RESET);
//---- 
   double C=nClose[0];
   double H=nHigh[0];
   double L=nLow[0];
   double R=(H-L);
//----
   C=(H+L+C)/3;
   double D=C+R*Step;
   double B=C-R*Step;
   double E=C+R*2*Step;
   double A=C-R*2*Step;
//----
   double R2=R*Ratio2;
   double R1=R*Ratio1;
   double B1=C-R1;
   double A1=C-R2;
//----
   double D1=C+R1;
   double E1=C+R2;
//---- 
   double R3=R*Ratio3;
   double F=C+R*4*Step;
   double G=C-R*4*Step;  
   double F1=C+R3;
   double G1=C-R3;
//----
   SetRectangle(0,FIB_RES3,0,nTime[0]-SecondLeftTail,F,time[0]+SecondRightTail,F1,Color_Res3,true,FIB_RES3);
   SetRectangle(0,FIB_RES2,0,nTime[0]-SecondLeftTail,E,time[0]+SecondRightTail,E1,Color_Res2,true,FIB_RES2);
   SetRectangle(0,FIB_RES1,0,nTime[0]-SecondLeftTail,D,time[0]+SecondRightTail,D1,Color_Res1,true,FIB_RES1);
   SetRectangle(0,FIB_SUP1,0,nTime[0]-SecondLeftTail,B,time[0]+SecondRightTail,B1,Color_Sup1,true,FIB_SUP1);
   SetRectangle(0,FIB_SUP2,0,nTime[0]-SecondLeftTail,A,time[0]+SecondRightTail,A1,Color_Sup2,true,FIB_SUP2);
   SetRectangle(0,FIB_SUP3,0,nTime[0]-SecondLeftTail,G,time[0]+SecondRightTail,G1,Color_Sup3,true,FIB_SUP3);
//----
   ChartRedraw(0);
   return(rates_total);
  }
//+------------------------------------------------------------------+


//
ENUM_TIMEFRAMES timeFrameGet(int period)
  {
   int _shift=(period<0?MathAbs(period):0);
   if(_shift>0 || period==tf_cu) period=_Period;
   int i; for(i=0;i<ArraySize(_tfsPer);i++) if(period==_tfsPer[i]) break;

   return(_tfsPer[(int)MathMin(i+_shift,ArraySize(_tfsPer)-1)]);
  }
//
//---
```

![Fib_SR](https://c.mql5.com/i/code/indicator.png)
[Fib\_SR](/en/code/23195)

The indicator shows possible resistance and support zones in the form of colored rectangles

![SpearmanRankCorrelation_Histogram_Alerts](https://c.mql5.com/i/code/indicator.png)
[SpearmanRankCorrelation\_Histogram\_Alerts](/en/code/23192)

The SpearmanRankCorrelation\_Histogram indicator with the ability to send alerts, emails and push notifications when trading signals emerge

![jMaster RSI](https://c.mql5.com/i/code/expert.png)
[jMaster RSI](/en/code/23199)

The Expert Advisor is based on the RSI Custom Smoothing indicator.

![Day Trading PAMXA](https://c.mql5.com/i/code/expert.png)
[Day Trading PAMXA](/en/code/23201)

The strategy is based on two indicators calculated on two timeframes: iAO (Awesome Oscillator, AO) on the D1 TF and iStochastic (Stochastic Oscillator, Stoh) on H1.