# ALGLIB - Numerical Analysis Library - library for MetaTrader 5

Source: https://www.mql5.com/en/code/1146

* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 5](/en/code/mt5)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt5/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt5/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt5/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt5/libraries)
* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 4](/en/code/mt4)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt4/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt4/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt4/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt4/libraries)

* [![](https://c.mql5.com/i/sidebar/storage.svg)Storage](/en/code/storage)

Watch [how to download](https://youtu.be/rloNyFVtHuA?list=PLltlMLQ7OLeKwyQwC8FhiKwjl9syKhOCK) trading robots for free

Find us on [Twitter](https://x.com/mql5com)!  
 Join our fan page

Interesting script?  
So post a [link](/en/code/1146) to it -  
let others appraise it

You liked the script? Try it in the [MetaTrader 5](https://download.mql5.com/cdn/web/metaquotes.ltd/mt5/mt5setup.exe?utm_source=www.mql5.com&utm_campaign=download) terminal

[to pocket](#pocket "Pocket allows you to insert a complete content description to the appropriate comment")

![Libraries](https://c.mql5.com/i/code/library.png)

# ALGLIB - Numerical Analysis Library - library for MetaTrader 5

Sergey Bochkanov  |
[English](javascript:void(false);)

[Русский](/ru/code/1146) [中文](/zh/code/1146) [Español](/es/code/1146) [Deutsch](/de/code/1146) [日本語](/ja/code/1146) [Português](/pt/code/1146) [한국어](/ko/code/1146) [Français](/fr/code/1146) [Italiano](/it/code/1146) [Türkçe](/tr/code/1146)

Published by:
:   [MetaQuotes](/en/users/metaquotes)

Views:
:   49592

Rating:
:   (127)

Published:
:   12 October 2012, 08:48

Updated:
:   8 September 2023, 17:34
:   \MQL5\Include\Math\Alglib\

    [alglib.mqh](/en/code/download/1146/alglib.mqh "alglib.mqh")
    (2420.29 KB)
    [view](# "view")

    [alglibinternal.mqh](/en/code/download/1146/alglibinternal.mqh "alglibinternal.mqh")
    (579.73 KB)
    [view](# "view")

    [alglibmisc.mqh](/en/code/download/1146/alglibmisc.mqh "alglibmisc.mqh")
    (119.42 KB)
    [view](# "view")

    [ap.mqh](/en/code/download/1146/ap.mqh "ap.mqh")
    (89.98 KB)
    [view](# "view")

    [arrayresize.mqh](/en/code/download/1146/arrayresize.mqh "arrayresize.mqh")
    (4.88 KB)
    [view](# "view")

    [bitconvert.mqh](/en/code/download/1146/bitconvert.mqh "bitconvert.mqh")
    (13.49 KB)
    [view](# "view")

    [dataanalysis.mqh](/en/code/download/1146/dataanalysis.mqh "dataanalysis.mqh")
    (1124.84 KB)
    [view](# "view")

    [delegatefunctions.mqh](/en/code/download/1146/delegatefunctions.mqh "delegatefunctions.mqh")
    (21.25 KB)
    [view](# "view")

    [diffequations.mqh](/en/code/download/1146/diffequations.mqh "diffequations.mqh")
    (32.41 KB)
    [view](# "view")

    [fasttransforms.mqh](/en/code/download/1146/fasttransforms.mqh "fasttransforms.mqh")
    (92.23 KB)
    [view](# "view")

    [integration.mqh](/en/code/download/1146/integration.mqh "integration.mqh")
    (116.61 KB)
    [view](# "view")

    [interpolation.mqh](/en/code/download/1146/interpolation.mqh "interpolation.mqh")
    (1430.87 KB)
    [view](# "view")

    [linalg.mqh](/en/code/download/1146/linalg.mqh "linalg.mqh")
    (1455.11 KB)
    [view](# "view")

    [matrix.mqh](/en/code/download/1146/matrix.mqh "matrix.mqh")
    (45.38 KB)
    [view](# "view")

    [optimization.mqh](/en/code/download/1146/optimization.mqh "optimization.mqh")
    (2246.73 KB)
    [view](# "view")

    [solvers.mqh](/en/code/download/1146/solvers.mqh "solvers.mqh")
    (295.08 KB)
    [view](# "view")

    [specialfunctions.mqh](/en/code/download/1146/specialfunctions.mqh "specialfunctions.mqh")
    (235.55 KB)
    [view](# "view")

    [statistics.mqh](/en/code/download/1146/statistics.mqh "statistics.mqh")
    (407.4 KB)
    [view](# "view")

    \MQL5\Scripts\UnitTests\Alglib\

    [TestClasses.mq5](/en/code/download/1146/TestClasses.mq5 "TestClasses.mq5")
    (30.42 KB)
    [view](# "view")

    [TestClasses.mqh](/en/code/download/1146/TestClasses.mqh "TestClasses.mqh")
    (3032.44 KB)
    [view](# "view")

    [TestInterfaces.mq5](/en/code/download/1146/TestInterfaces.mq5 "TestInterfaces.mq5")
    (17.49 KB)
    [view](# "view")

    [TestInterfaces.mqh](/en/code/download/1146/TestInterfaces.mqh "TestInterfaces.mqh")
    (679.69 KB)
    [view](# "view")

    [UseAlglib.mq5](/en/code/download/1146/UseAlglib.mq5 "UseAlglib.mq5")
    (6.2 KB)
    [view](# "view")

    [Expand (23)](javascript:void(false))
    [Collapse (23)](javascript:void(false))

    [Download as ZIP](/en/code/download/1146.zip "Download all attachments in the single ZIP archive") [How to download code from MetaEditor](https://www.metatrader5.com/en/metaeditor/help/workspace/toolbox#codebase)

    ![MQL5 - Language of trade strategies built-in the MetaTrader 5 client terminal](https://c.mql5.com/i/registerlandings/logo-2.png)

    You are missing trading opportunities:

    * Free trading apps
    * Over 8,000 signals for copying
    * Economic news for exploring financial markets

    Registration
    Log in

    latin characters without spaces

    a password will be sent to this email

    An error occurred

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup&amp;reg=1)

    You agree to [website policy](/en/about/privacy) and [terms of use](/en/about/terms)

    If you do not have an account, please [register](https://www.mql5.com/en/auth_register)

    Allow the use of cookies to log in to the MQL5.com website.

    Please enable the necessary setting in your browser, otherwise you will not be able to log in.

     

    [Forgot your login/password?](https://www.mql5.com/en/auth_forgotten?return=popup)

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup)
:   ![MQL5 Freelance](https://c.mql5.com/i/code/icon_freelance.svg)
    Need a robot or indicator based on this code? Order it on Freelance
    [Go to Freelance](/en/job/new "Go to Freelance")

**Real author:**

Sergey Bochkanov. ALGLIB project website - [http://www.alglib.net/](/go?link=https://www.alglib.net/ "http://www.alglib.net/"). The library dates [back to 1999](/go?link=http://www.alglib.net/about/history.php "http://www.alglib.net/about/history.php").

### ALGLIB is one of the largest and most complete mathematical libraries

Do you need to make a fast Fourier transform or to solve a differential equation system? Do you perform a complex data analysis trying to gather all methods in one place as a source code? Then [ALGLIB](/go?link=https://www.alglib.net/ "http://www.alglib.net/") library of numerical methods is for you!

ALGLIB is currently one of the best libraries of the multi-language algorithms. Below are ALGLIB features mentioned on the official website:

ALGLIB is a cross-platform numerical analysis and data processing library. It supports several programming languages (C++, C#, Pascal, VBA) and several operating systems (Windows, Linux, Solaris). ALGLIB features include:

* Linear algebra (direct algorithms, EVD/SVD)
* Solvers (linear and nonlinear)
* Interpolation
* Optimization
* Fast Fourier transforms
* Numerical integration
* Linear and nonlinear least-squares fitting
* Ordinary differential equations
* Special functions
* Statistics (descriptive statistics, hypothesis testing)
* Data analysis (classification/regression, including neural networks)
* Multiple precision versions of linear algebra, interpolation optimization and others algorithms (using MPFR for floating point computations)

Why to choose ALGLIB? Because it is:

* **portable**. It can be compiled almost anywhere using almost any compiler (see [compatibility matrix](/go?link=http://www.alglib.net/about/compatibility.php "http://www.alglib.net/about/compatibility.php") for more info).
* **easy to use**. It supports many programming languages. If you use one language, you don't need to study another (FORTRAN, for example) to compile and link an external library.
* **open source**. It can be used for free under GPL 2+.
* **suited for commercial users too**. Those who want to use ALGLIB in commercial applications can buy [commercial license](/go?link=https://www.alglib.net/commercial.php "http://www.alglib.net/commercial.php") without copyleft requirement.

ALGLIB library is being constantly enhanced, new functions and improvements according to users' comments are implemented regularly. The latest version is 4.0.

Besides, the library contains the large collection of test cases covering the major part of the proposed methods' functionality. That will allow you to carry the tests and [report detected errors](/go?link=http://bugs.alglib.net/my_view_page.php "http://bugs.alglib.net/my_view_page.php") to the project's authors.

CAlglib class static functions should be used to work with the library - all library functions are moved to CAlglib system class as static functions.

testclasses.mq5 and testinterfaces.mq5 test cases scripts are attached together with a simple usealglib.mq5 demo script. Include files of the same name (testclasses.mqh and testinterfaces.mqh) are used to launch test cases. They should be placed to \MQL5\Scripts\Alglib\Testcases\.

**Note:** testclasses.mq5 script execution takes quite a long time (about 8 minutes).

Below is more detailed information about ALGLIB MQL5 ported library packages:

| Packages | Description |
| --- | --- |
| **alglib.mqh** | The main library package includes custom functions.These functions should be called for working with the library. |
| **alglibinternal.mqh** | Additional classes for the operation of other library packages. |
| **alglibmisc.mqh** | The package contains the following classes:   1. CHighQualityRand - high quality random number generator. 2. CNearestNeighbor - KD-trees generation and solution. |
| **ap.mqh** | The package also contains additional classes necessary for other packages. |
| **bitconvert.mqh** | С++ basic classes and functions that are absent in MQL5 as built-in:   1. BitConverter - class for transforming int and double type figures into an array of bits and vice versa. 2. CInfOrNaN - NaNs and plus/minus infinity verification and generation class. 3. ArrayReverse - array elements reverse function. 4. GetSelectionString - function for receiving the string generated from char type array elements. 5. MathSign - number sign calculation function. 6. MathSinh - hyperbolic sine calculation function. 7. MathCosh - hyperbolic sine calculation function. 8. MathTanh - hyperbolic tangent calculation function. |
| **dataanalysis.mqh** | Classes of data analysis:   1. CBdSS - error functions calculation. 2. CDForest - working with the forests of solution trees. 3. CKMeans - clustering using k-means++ algorithm. 4. CLDA - linear discriminant analysis. 5. CLinReg - linear regression. 6. CMLPBase - multilayer perceptron (neural networks). 7. CLogit - multinomial logit regression. 8. CMarkovCPD - Markov chains for population data. 9. CMLPTrain - training of multilayer perceptron. 10. CMLPE - ensembles of neural networks. 11. CPCAnalysis - principal components method. |
| **delegatefunctions.mqh** | The package contains the classes created as replacement for the delegates. The objects of these classes are the functions optimized in several library methods. |
| **diffequations.mqh** | The class for solving ordinary differential equations:   1. CODESolver - solving ordinary differential equations. |
| **fasttransforms.mqh** | Fast transformation classes:   1. CFastFourierTransform - fast Fourier transform. 2. CConv - convolution. 3. CCorr - cross-correlation. 4. CFastHartleyTransform - fast Hartley transform. |
| **integration.mqh** | Classes for numerical integration:   1. CGaussQ - Gaussian quadrature. 2. CGaussKronrodQ - Gauss–Kronrod quadrature formulas. 3. CAutoGK - adaptive integrator. |
| **interpolation.mqh** | Interpolation, approximation and numerical differentiation classes:   1. CIDWInt - interpolation and approximation by the inverse average weighted distance. 2. CRatInt - rational interpolation. 3. CPolInt - polynomial interpolation. 4. CSpline1D - one-dimensional spline interpolation. 5. CLSFit - approximation using linear or non-linear least squares method. 6. CPSpline - parametric spline interpolation. 7. CSpline2D - two-dimensional spline interpolation. |
| **linalg.mqh** | Classes for calculation of some linear algebra operations:   1. COrtFac - QR/LQ decompositions, decompositions of Hessenberg, as well as of bi- and tridiagonal matrices. 2. CEigenVDetect - finding the eigenvalues ​​and eigenvectors. 3. CMatGen - random matrices generation. 4. CTrFac - LU and Cholesky decompositions. 5. CRCond - estimation of a matrix dependence value. 6. CMatInv - matrix inversion. 7. CBdSingValueDecompose - singular decomposition of a bidiagonal matrix. 8. CSingValueDecompose - matrix singular decomposition. 9. CFbls - quick basic linear solutions. 10. CMatDet - calculation of a matrix determinant. 11. CSpdGEVD - finding the eigenvalues ​​and eigenvectors in generalized symmetric matrices. 12. CInverseUpdate - matrices inversion and update. 13. CSchur - Issai Schur decomposition. |
| **matrix.mqh** | Classes of matrices: integer, continuous and complex. |
| **optimization.mqh** | Dimensional and multidimensional optimization classes:   1. CMinCG - optimization using conjugate gradient method. 2. CMinBLEIC - optimization with linear constraints in the form of equations and inequations. 3. CMinLBFGS - optimization using successive generation and classification of the function's quadratic model. 4. CMinQP - quadratic programming with linear constraints in the form of equations and inequations. 5. CMinLM - optimization using Levenberg-Marquardt algorithm. 6. CMinComp - backward compatibility functions. |
| **solvers.mqh** | Classes for solving linear and non-linear equation systems:   1. CDenseSolver - solving linear equation systems. 2. CNlEq - solving non-linear equation systems. |
| **specialfunctions.mqh** | Classes of distribution functions, integrals and polynomials:   1. CGammaFunc - Gamma function. 2. CIncGammaF - incomplete Gamma function. 3. CBetaF - Beta function. 4. CIncBetaF - incomplete beta function. 5. CPsiF - psi-function. 6. CAiryF - Airy function. 7. CBessel - Bessel functions of integral order. 8. CJacobianElliptic - Jacobian elliptic functions. 9. CDawson - Dawson integral. 10. CTrigIntegrals - trigonometric integrals. 11. CElliptic - elliptic integrals of the first and second kind. 12. CExpIntegrals - exponential integrals. 13. CFresnel - Fresnel integrals. 14. CHermite - Hermite polynomials. 15. CChebyshev - Chebyshev polynomials. 16. CLaguerre - Laguerre polynomials. 17. CLegendre - Legendre polynomials. 18. CChiSquareDistr - chi-square distribution. 19. CBinomialDistr - binomial distribution. 20. CNormalDistr - normal distribution. 21. CPoissonDistr - Poisson distribution. 22. CStudenttDistr - Student t-distribution. 23. CFDistr - F-distribution. |
| **statistics.mqh** | Classes of statistical data analysis:   1. CBaseStat - basic statistical methods. 2. CCorrTests - test for correlation ratio significance. 3. CJarqueBera - JarqueBera croterion. 4. CMannWhitneyU - Mann-Whitney U-criterion. 5. CSignTest - sign test. 6. CStudentTests - Student t-tests. 7. CVarianceTests - F-test and chi-square test. 8. CWilcoxonSignedRank - Wilcoxon W-criterion. |

### 

**Code:**

Library functions have detailed comments on their usage.

```mql5
//+------------------------------------------------------------------+
//| Calculation of the distribution moments: mean, variance,         |
//| skewness, kurtosis.                                              |
//| INPUT PARAMETERS:                                                |
//|     X       -   sample                                           |
//|     N       -   N>=0, sample size:                               |
//|                 * if given, only leading N elements of X are     |
//|                   processed                                      |
//|                 * if not given, automatically determined from    |
//|                   size of X                                      |
//| OUTPUT PARAMETERS                                                |
//|     Mean    -   mean.                                            |
//|     Variance-   variance.                                        |
//|     Skewness-   skewness (if variance<>0; zero otherwise).       |
//|     Kurtosis-   kurtosis (if variance<>0; zero otherwise).       |
//+------------------------------------------------------------------+
static bool CBaseStat::SampleMoments(const double &cx[],const int n,double &mean,
                                     double &variance,double &skewness,double &kurtosis)
  {
//--- check
   if(!CAp::Assert(n>=0,__FUNCTION__+": the error variable"))
      return(false);
//--- check
   if(!CAp::Assert(CAp::Len(cx)>=n,__FUNCTION__+": length(x)<n"))
      return(false);
//--- check
   if(!CAp::Assert(CApServ::IsFiniteVector(cx,n),__FUNCTION__+": x is not finite vector"))
      return(false);
//--- create variables
   double v=0;
   double v1=0;
   double v2=0;
   double stddev=0;
//--- Init, special case 'N=0'
   mean=0;
   variance=0;
   skewness=0;
   kurtosis=0;
//--- check
   if(n<=0)
      return(true);
//--- Mean
   for(int i=0;i<n;i++)
      mean+=cx[i];
   mean/=n;
//--- Variance (using corrected two-pass algorithm)
   if(n!=1)
     {
      //--- calculation
      for(int i=0;i<n;i++)
         v1+=CMath::Sqr(cx[i]-mean);
      for(int i=0;i<n;i++)
         v2+=cx[i]-mean;
      v2=CMath::Sqr(v2)/n;
      variance=(v1-v2)/(n-1);
      //--- calculation
      stddev=MathSqrt(variance);
     }
   else
      variance=EMPTY_VALUE;
//--- Skewness and kurtosis
   if(stddev!=0)
     {
      //--- calculation
      for(int i=0;i<n;i++)
        {
         v=(cx[i]-mean)/stddev;
         v2=CMath::Sqr(v);
         skewness+=v2*v;
         kurtosis+=CMath::Sqr(v2);
        }
      //--- change values
      skewness=skewness/n;
      kurtosis=kurtosis/n-3;
     }
//--- successful execution
   return(true);
  }
```

**CAlglib class static functions should be used** to work with the library. Below is usealglib.mq5 function script's source code for calculation of some statistical trading parameters:

```mql5
//+------------------------------------------------------------------+
//|                                                    UseAlglib.mq5 |
//|                        Copyright 2012, MetaQuotes Software Corp. |
//|                                              https://www.mql5.com |
//+------------------------------------------------------------------+
#property copyright "Copyright 2012, MetaQuotes Software Corp."
#property link      "https://www.mql5.com"
#property version   "1.00"
//+------------------------------------------------------------------+
//| Connecting the libraries                                         |
//+------------------------------------------------------------------+
#include <Math\Alglib\alglib.mqh>
#include <Trade\DealInfo.mqh>
#include <Arrays\ArrayDouble.mqh>
//+------------------------------------------------------------------+
//| Script program start function                                    |
//+------------------------------------------------------------------+
void OnStart()
  {
//--- object to access data on deals
   CDealInfo     deal;
//--- object for storing profit/loss of each deal
   CArrayDouble *profit=new CArrayDouble;
//--- objects for storing the balance
   CArrayDouble *balance_total=new CArrayDouble;
//--- initial balance
   double        balance=0;
//--- receive history
   HistorySelect(0,TimeCurrent());
//--- total amount of deals
   int deals_total=HistoryDealsTotal();
//--- receive data on deals' profit and balance
   for(int i=0;i<deals_total;i++)
     {
      //--- move to the deal having i index
      deal.SelectByIndex(i);
      //--- receive initial balance
      if(deal.DealType()==DEAL_TYPE_BALANCE)
        {
         if(NormalizeDouble(deal.Profit()+deal.Swap(),2)>=0.0)
            if(balance==0.0)
               balance=deal.Profit();
        }
      //--- receive profit and balance
      if(deal.DealType()==DEAL_TYPE_BUY || deal.DealType()==DEAL_TYPE_SELL)
         if(deal.Entry()==DEAL_ENTRY_OUT || deal.Entry()==DEAL_ENTRY_INOUT)
           {
            profit.Add(NormalizeDouble(deal.Profit()+deal.Swap()+deal.Commission(),2));
            balance_total.Add(balance);
            balance=balance+NormalizeDouble(deal.Profit()+deal.Swap()+deal.Commission(),2);
           }
     }
   balance_total.Add(balance_total.At(balance_total.Total()-1)+profit.At(balance_total.Total()-1));
//--- copy data on the balance to double type array
   double arr_balance[];
   ArrayResize(arr_balance,balance_total.Total());
   for(int i=0;i<balance_total.Total();i++)
      arr_balance[i]=balance_total.At(i);
//--- copy data on profit to double type array
   double arr_profit[];
   ArrayResize(arr_profit,profit.Total());
   for(int i=0;i<profit.Total();i++)
      arr_profit[i]=profit.At(i);
//--- linear regression
//--- number of independent variables
   int nvars=1;
//--- sample volume
   int npoints=balance_total.Total();
//--- create the matrix of parameters for the linear regression
   CMatrixDouble xy(npoints,nvars+1);
   for(int i=0;i<npoints;i++)
     {
      xy[i].Set(0,i);
      xy[i].Set(1,arr_balance[i]);
     }
//--- variable for detecting calculations result (successful, unsuccessful)
   int info;
//--- class objects necessary for storing data on calculations
   CLinearModelShell lm;
   CLRReportShell    ar;
//--- arrays for storing regression results
   double lr_coeff[];
   double lr_values[];
   ArrayResize(lr_values,npoints);
//--- calculate linear regression ratios
   CAlglib::LRBuild(xy,npoints,nvars,info,lm,ar);
//--- receive linear regression ratios
   CAlglib::LRUnpack(lm,lr_coeff,nvars);
//--- receive recovered linear regression values
   for(int i=0;i<npoints;i++)
      lr_values[i]=lr_coeff[0]*i+lr_coeff[1];
//--- calculate Expected Payoff
   double exp_payoff,tmp1,tmp2,tmp3;
   CAlglib::SampleMoments(arr_profit,exp_payoff,tmp1,tmp2,tmp3);
//--- calculate HPR array
   double HPR[];
   ArrayResize(HPR,balance_total.Total()-1);
   for(int i=0;i<balance_total.Total()-1;i++)
      HPR[i]=balance_total.At(i+1)/balance_total.At(i);
//--- calculate standard deviation and math expectation from HPR
   double AHPR,SD;
   CAlglib::SampleMoments(HPR,AHPR,SD,tmp2,tmp3);
   SD=MathSqrt(SD);
//--- calculate LR Correlation
   double lr_corr=CAlglib::PearsonCorr2(arr_balance,lr_values);
//--- receive LR Standard Error
   double lr_stand_err=0;
   for(int i=0;i<npoints;i++)
     {
      double delta=MathAbs(arr_balance[i]-lr_values[i]);
      lr_stand_err=lr_stand_err+delta*delta;
     }
   lr_stand_err=MathSqrt(lr_stand_err/(npoints-2));
//--- calculate Sharpe Ratio
   double sharpe_ratio=(AHPR-1)/SD;
//--- print
   PrintFormat("-----------------------------------------------");
   PrintFormat("Correlation function: y = %.2fx + %.2f",lr_coeff[0],lr_coeff[1]);
//--- parameters
   PrintFormat("Expected Payoff = %.2f",exp_payoff);
   PrintFormat("AHPR = %.4f",AHPR);
   PrintFormat("Sharpe Ratio = %.2f",sharpe_ratio);
   PrintFormat("LR Correlation = %.2f",lr_corr);
   PrintFormat("LR Standard Error = %.2f",lr_stand_err);
   PrintFormat("-----------------------------------------------");
//--- delete objects
   delete profit;
   delete balance_total;
  }
//+------------------------------------------------------------------+
```

We receive the following result:

![](https://c.mql5.com/18/150/4699969046469.png)

Translated from Russian by MetaQuotes Ltd.   
Original code: [https://www.mql5.com/ru/code/1146](/ru/code/1146)

**Last comments |
[Go to discussion](/en/forum/8264)**
(66)

![Bsel Muchury](https://c.mql5.com/avatar/2023/11/6562C336-E250.png "Bsel Muchury")

**[Bsel Muchury](/en/users/btraderandesen)**
|
4 Jan 2024 at 15:07

good artide

![ndnz2018](https://c.mql5.com/avatar/avatar_na2.png "ndnz2018")

**[ndnz2018](/en/users/ndnz2018)**
|
26 Feb 2024 at 15:39

Hello,

i get the following error with the last alglib update (2023.09.08 17:34). It's caused by function MNLTrainH.

fatal compiler error: code generation failed

Here the minimal example to reproduce it:

```mql5
#include <math\alglib\alglib.mqh>
CLogitModelShell log_mod;
CMNLReportShell log_mod_rep;
CMatrixDouble log_mod_data;
int log_mod_info = 0;
void OnStart()
  {
      CAlglib::MNLTrainH(log_mod_data, 10, 3, 2, log_mod_info, log_mod, log_mod_rep);   
  }
```

I hope somebody can help.

Greetings

cn

![ndnz2018](https://c.mql5.com/avatar/avatar_na2.png "ndnz2018")

**[ndnz2018](/en/users/ndnz2018)**
|
28 Feb 2024 at 16:22

Hello,

//--- evaluate the function and gradient at stp  
//--- and compute the directional derivative.  
x=wa+s\*stp+0;

![Wendel Resende](https://c.mql5.com/avatar/2025/5/6817B94B-2667.png "Wendel Resende")

**[Wendel Resende](/en/users/wendelresende)**
|
17 Jun 2025 at 18:23

Hello, doesn't this library need the [Vector](https://www.mql5.com/en/docs/matrix/matrix_types " MQL5 Documentation: Matrix and vector types").mqh file?

![Clemence Benjamin](https://c.mql5.com/avatar/2025/3/67df27c6-2936.png "Clemence Benjamin")

**[Clemence Benjamin](/en/users/billionaire2024)**
|
6 Sep 2025 at 14:06

**Automated-Trading:**  

[ALGLIB - Numerical Analysis Library](/en/code/1146):

Author: [MetaQuotes](/en/users/MetaQuotes)

Thank you for the good work.

![Ask Bid Ticks](https://c.mql5.com/i/code/expert.png)
[Ask Bid Ticks](/en/code/820)

Ask Bid Ticks is a high-precision, real-time tick data solution for microstructure analysis (a tick data collector). It works with local computer time.

![Daily Range](https://c.mql5.com/i/code/indicator.png)
[Daily Range](/en/code/1166)

This indicator calculates and shows daily highest and lowest prices, display them as two set of lines.

![AML Adaptive Market Level](https://c.mql5.com/i/code/indicator.png)
[AML Adaptive Market Level](/en/code/1026)

Adaptive Market Level shows the current reference level of the market price. The level is shifted only in case of the price trend movement.

![BubblesAndDrops](https://c.mql5.com/i/code/indicator.png)
[BubblesAndDrops](/en/code/1021)

The indicator shows possible price movement direction.