# ATRStops_v1.1_Alert - indicator for MetaTrader 5

Source: https://www.mql5.com/en/code/17881

* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 5](/en/code/mt5)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt5/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt5/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt5/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt5/libraries)
* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 4](/en/code/mt4)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt4/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt4/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt4/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt4/libraries)

* [![](https://c.mql5.com/i/sidebar/storage.svg)Storage](/en/code/storage)

Watch [how to download](https://youtu.be/rloNyFVtHuA?list=PLltlMLQ7OLeKwyQwC8FhiKwjl9syKhOCK) trading robots for free

Find us on [Telegram](https://t.me/mql5dev)!  
 Join our fan page

Interesting script?  
So post a [link](/en/code/17881) to it -  
let others appraise it

You liked the script? Try it in the [MetaTrader 5](https://download.mql5.com/cdn/web/metaquotes.ltd/mt5/MetaTrader5.pkg.zip?utm_source=www.mql5.com&utm_campaign=download) terminal

[to pocket](#pocket "Pocket allows you to insert a complete content description to the appropriate comment")

![Indicators](https://c.mql5.com/i/code/indicator.png)

# ATRStops\_v1.1\_Alert - indicator for MetaTrader 5

[Nikolay Kositsin](/en/users/godzilla)  |
[English](javascript:void(false);)

[Русский](/ru/code/17881) [中文](/zh/code/17881) [Español](/es/code/17881) [Deutsch](/de/code/17881) [日本語](/ja/code/17881) [Português](/pt/code/17881)

Views:
:   7033

Rating:
:   (26)

Published:
:   29 May 2017, 13:45
:   [ATRStops\_v1.1\_Alert.mq5](/en/code/download/17881/atrstops_v1.1_alert.mq5 "ATRStops_v1.1_Alert.mq5")
    (31.93 KB)
    [view](# "view")

    [Download as ZIP](/en/code/download/17881.zip "Download all attachments in the single ZIP archive") [How to download code from MetaEditor](https://www.metatrader5.com/en/metaeditor/help/workspace/toolbox#codebase)

    ![MQL5 - Language of trade strategies built-in the MetaTrader 5 client terminal](https://c.mql5.com/i/registerlandings/logo-2.png)

    You are missing trading opportunities:

    * Free trading apps
    * Over 8,000 signals for copying
    * Economic news for exploring financial markets

    Registration
    Log in

    latin characters without spaces

    a password will be sent to this email

    An error occurred

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup&amp;reg=1)

    You agree to [website policy](/en/about/privacy) and [terms of use](/en/about/terms)

    If you do not have an account, please [register](https://www.mql5.com/en/auth_register)

    Allow the use of cookies to log in to the MQL5.com website.

    Please enable the necessary setting in your browser, otherwise you will not be able to log in.

     

    [Forgot your login/password?](https://www.mql5.com/en/auth_forgotten?return=popup)

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup)
:   ![MQL5 Freelance](https://c.mql5.com/i/code/icon_freelance.svg)
    Need a robot or indicator based on this code? Order it on Freelance
    [Go to Freelance](/en/job/new "Go to Freelance")

**Real author**: [igorad](https://www.mql5.com/en/users/igorad "Igor Durkin")

A trend indicator implemented as NRTR, with the possibility to generate alerts and send emails or push-notifications. Average True Range is used for constructing a moving average.

This indicator was first implemented in MQL4 and published in [Code Base at mql4.com](/en/code/8043 "ATRStops_v1[1].1 - indicator for MetaTrader 4") on 07.04.2008.

![Fig.1 Indicator ATRStops_v1.1_Alert](https://c.mql5.com/18/57/picture__62.png "Fig.1 Indicator ATRStops_v1.1_Alert")

Fig.1 Indicator ATRStops\_v1.1\_Alert

Translated from Russian by MetaQuotes Ltd.   
Original code: [https://www.mql5.com/ru/code/17881](/ru/code/17881)

**Last comments |
[Go to discussion](/en/forum/193565)**
(9)

![Альберт Туйкин](https://c.mql5.com/avatar/avatar_na2.png "Альберт Туйкин")

**[Альберт Туйкин](/en/users/darkcorp)**
|
21 Apr 2017 at 09:12

I apologise, I added a screenshot.

For example, on the RTS index chart everything works, but on the RTS futures chart there is only a blue line.

![Nikolay Kositsin](https://c.mql5.com/avatar/2014/6/538DE448-E682.jpeg "Nikolay Kositsin")

**[Nikolay Kositsin](/en/users/godzilla)**
|
22 Apr 2017 at 05:14

**Альберт Туйкин:**  

I apologise, I added a screenshot.

For example, everything works on the RTS index chart, but on the RTS futures chart there is only a blue line.

It is clear that the author of the code has set the sizes of global bottom and ceiling too small in the source code. The code will be changed soon.

```mql5
//+------------------------------------------------------------------+
//|ATRStops_v1.1_Alert.mq5 |
//|Copyright © 2006, Forex-TSD.com |
//| Written by IgorAD,igorad2003@yahoo.co.uk | 
//| http://finance.groups.yahoo.com/group/TrendLaboratory | 
//+------------------------------------------------------------------+
//---- indicator authorship
#property copyright "Copyright © 2006, Forex-TSD.com "
//---- link to author's website
#property link      "http://www.forex-tsd.com/"
//---- indicator version number
#property version   "1.10"
//---- draw indicator in the main window
#property indicator_chart_window
//---- 4 buffers are used for calculation and drawing of the indicator
#property indicator_buffers 4
//---- used 4 graphical constructions
#property indicator_plots   4
//+----------------------------------------------+
//|| Parameters of drawing of the bullish indicator |
//+----------------------------------------------+
//---- drawing of indicator 1 as a line
#property  indicator_type1   DRAW_LINE
//---- blue colour is used as indicator line colour
#property  indicator_color1  clrBlue
//---- indicator line 1 - solid
#property  indicator_style1  STYLE_SOLID
//---- indicator 1 line thickness is 2
#property  indicator_width1  2
//---- display indicator line marker
#property  indicator_label1  "Upper ATRStops_v1.1"
//+----------------------------------------------+
//|| Bearish indicator rendering parameters |
//+----------------------------------------------+
//---- drawing of indicator 2 as a line
#property  indicator_type2   DRAW_LINE
//---- IndianRed colour is used as indicator line colour
#property  indicator_color2  clrIndianRed
//---- indicator line 2 - solid
#property  indicator_style2  STYLE_SOLID
//---- indicator 2 line thickness is 2
#property  indicator_width2  2
//---- display indicator line marker
#property  indicator_label2  "Lower ATRStops_v1.1"
//+----------------------------------------------+
//|| Parameters of drawing of the bullish indicator |
//+----------------------------------------------+
//---- drawing of indicator 3 as an icon
#property  indicator_type3   DRAW_ARROW
//---- colour Blue is used as indicator colour
#property  indicator_color3  clrBlue
//---- thickness of indicator 3 is 4
#property  indicator_width3  4
//---- display indicator label
#property  indicator_label3  "Buy ATRStops_v1.1"
//+----------------------------------------------+
//|| Bearish indicator rendering parameters |
//+----------------------------------------------+
//---- drawing of indicator 4 as an icon
#property  indicator_type4   DRAW_ARROW
//---- the colour Red is used as indicator colour
#property  indicator_color4  clrRed
//---- thickness of indicator 4 is 4
#property  indicator_width4  4
//---- display indicator label
#property  indicator_label4  "Sell ATRStops_v1.1"
//+----------------------------------------------+
//|| Indicator input parameters |
//+----------------------------------------------+
input uint   Length=10;           // Indicator period
input uint   ATRPeriod=5;         // ATR indicator period
input double Kv=2.5;              // ATR volatility
input int    Shift=0;             // Shift of the indicator horizontally in bars
input uint NumberofBar=1;         // Bar number for signalling
input bool SoundON=true;          // Alert resolution
input uint NumberofAlerts=2;      // Number of alerts
input bool EMailON=false;         // Enable mailing of the signal
input bool PushON=false;          // Enable sending a signal to the mobile phone
//+----------------------------------------------+
//---- declaration of dynamic arrays, which are further
//---- will be used as indicator buffers
double ExtMapBufferUp[];
double ExtMapBufferDown[];
double ExtMapBufferUp1[];
double ExtMapBufferDown1[];
//---- declaration of integer variables for indicator handles
int ATR_Handle;
//---- declaration of integer data start variables
int min_rates_total;
//+------------------------------------------------------------------+
//| Custom indicator initialisation function |
//+------------------------------------------------------------------+ 
int OnInit()
  {
//---- get ATR indicator handle
   ATR_Handle=iATR(NULL,0,ATRPeriod);
   if(ATR_Handle==INVALID_HANDLE)
     {
      Print(" Failed to get ATR indicator handle");
      return(1);
     }

//---- initialisation of data start variables
   min_rates_total=int(ATRPeriod+Length);

//---- turning dynamic array ExtMapBufferUp[] into indicator buffer
   SetIndexBuffer(0,ExtMapBufferUp,INDICATOR_DATA);
//---- shifting indicator 1 horizontally by Shift
   PlotIndexSetInteger(0,PLOT_SHIFT,Shift);
//---- shifting the start of the indicator drawing countdown 1
   PlotIndexSetInteger(0,PLOT_DRAW_BEGIN,min_rates_total);
//---- indexing of items in buffers, as in timeseries 
   ArraySetAsSeries(ExtMapBufferUp,true);
//---- setting of indicator values that will not be visible on the chart
   PlotIndexSetDouble(0,PLOT_EMPTY_VALUE,EMPTY_VALUE);

//---- turning dynamic array ExtMapBufferDown[] into indicator buffer
   SetIndexBuffer(1,ExtMapBufferDown,INDICATOR_DATA);
//---- shifting indicator 2 horizontally by Shift
   PlotIndexSetInteger(1,PLOT_SHIFT,Shift);
//---- shifting the start of the indicator drawing countdown 2
   PlotIndexSetInteger(1,PLOT_DRAW_BEGIN,min_rates_total);
//---- indexing of items in buffers, as in timeseries 
   ArraySetAsSeries(ExtMapBufferDown,true);
//---- setting of indicator values that will not be visible on the chart
   PlotIndexSetDouble(1,PLOT_EMPTY_VALUE,EMPTY_VALUE);

//---- turning dynamic array ExtMapBufferUp1[] into indicator buffer
   SetIndexBuffer(2,ExtMapBufferUp1,INDICATOR_DATA);
//---- shifting indicator 1 horizontally by Shift
   PlotIndexSetInteger(2,PLOT_SHIFT,Shift);
//---- shift of the indicator drawing start 3
   PlotIndexSetInteger(2,PLOT_DRAW_BEGIN,min_rates_total);
//---- indexing of items in buffers, as in timeseries 
   ArraySetAsSeries(ExtMapBufferUp1,true);
//---- setting of indicator values that will not be visible on the chart
   PlotIndexSetDouble(2,PLOT_EMPTY_VALUE,EMPTY_VALUE);
//---- character for indicator
   PlotIndexSetInteger(2,PLOT_ARROW,175);

//---- turning dynamic array ExtMapBufferDown1[] into indicator buffer
   SetIndexBuffer(3,ExtMapBufferDown1,INDICATOR_DATA);
//---- shifting indicator 2 horizontally by Shift
   PlotIndexSetInteger(3,PLOT_SHIFT,Shift);
//---- shift of the indicator drawing start 4
   PlotIndexSetInteger(3,PLOT_DRAW_BEGIN,min_rates_total);
//---- indexing of items in buffers, as in timeseries 
   ArraySetAsSeries(ExtMapBufferDown1,true);
//---- setting of indicator values that will not be visible on the chart
   PlotIndexSetDouble(3,PLOT_EMPTY_VALUE,EMPTY_VALUE);
//---- character for indicator
   PlotIndexSetInteger(3,PLOT_ARROW,175);

//---- initialisation of the variable for the short indicator name
   string shortname;
   StringConcatenate(shortname,"ATRStops_v1.1(",Length,", ",ATRPeriod,", ",DoubleToString(Kv,4),", ",Shift,")");
//--- creating a name to be displayed in a separate subwindow and tooltip
   IndicatorSetString(INDICATOR_SHORTNAME,shortname);
//--- determining the accuracy of displaying the indicator values
   IndicatorSetInteger(INDICATOR_DIGITS,_Digits);
//----
   return(0);
  }
//+------------------------------------------------------------------+
//| Custom indicator iteration function |
//+------------------------------------------------------------------+
int OnCalculate(const int rates_total,    // amount of history in bars on the current tick
                const int prev_calculated,// amount of history in bars on the previous tick
                const datetime &time[],
                const double &open[],
                const double& high[],     // price array of price maxima for indicator calculation
                const double& low[],      // price array of price minima for indicator calculation
                const double &close[],
                const long &tick_volume[],
                const long &volume[],
                const int &spread[])
  {
//---- check the number of bars for sufficiency for calculation
   if(BarsCalculated(ATR_Handle)<rates_total || rates_total<min_rates_total) return(0);

//---- local variable declarations 
   double ATR[];
   double smin0,smax0;
   int limit,to_copy,bar,trend0;
   static double smin1,smax1;
   static int trend1;

//---- indexing of elements in arrays, as in timeseries 
   ArraySetAsSeries(close,true);
   ArraySetAsSeries(high,true);
   ArraySetAsSeries(low,true);
   ArraySetAsSeries(ATR,true);

//---- calculating the start limit number for the bar recalculation cycle
   if(prev_calculated>rates_total || prev_calculated<=0) // check for the first start of indicator calculation
     {
      limit=rates_total-min_rates_total-1;               // start number for calculation of all bars
      trend1=0;
      smin1=-999999;
      smax1=+999999;
     }
   else
     {
      limit=rates_total-prev_calculated;                 // start number for calculating new bars
     }

   to_copy=int(limit+Length);
//---- copy newly appeared data into arrays
   if(CopyBuffer(ATR_Handle,0,0,to_copy,ATR)<=0) return(0);

//---- main indicator calculation cycle
   for(bar=limit; bar>=0; bar--)
     {      
      ExtMapBufferUp[bar]=EMPTY_VALUE;
      ExtMapBufferDown[bar]=EMPTY_VALUE;
      ExtMapBufferUp1[bar]=EMPTY_VALUE;
      ExtMapBufferDown1[bar]=EMPTY_VALUE;
      
      smin0=-999999;
      smax0=+999999;
      
      for(int iii=0; iii<int(Length); iii++)
        {
         int barx=bar+iii;
         smin0=MathMax(smin0,high[barx]-Kv*ATR[barx]);
         smax0=MathMin(smax0,low[barx]+Kv*ATR[barx]);
        }
        
      trend0=trend1;
      if(close[bar]>smax1) trend0=+1;
      if(close[bar]<smin1) trend0=-1;

      if(trend0>0)
        {
         if(smin0<smin1) smin0=smin1;
         ExtMapBufferUp[bar]=smin0;
        }
        
      if(trend0<0)
        {
         if(smax0>smax1) smax0=smax1;
         ExtMapBufferDown[bar]=smax0;
        }

      if(ExtMapBufferUp[bar+1]==EMPTY_VALUE && ExtMapBufferUp[bar]!=EMPTY_VALUE) ExtMapBufferUp1[bar]=ExtMapBufferUp[bar];
      if(ExtMapBufferDown[bar+1]==EMPTY_VALUE && ExtMapBufferDown[bar]!=EMPTY_VALUE) ExtMapBufferDown1[bar]=ExtMapBufferDown[bar];

      if(bar>0)
        {
         smin1=smin0;
         smax1=smax0;
         trend1=trend0;
        }
     }
//--- 
   BuySignal("ATRStops_v1.1_Alert",ExtMapBufferUp1,rates_total,prev_calculated,close,spread);
   SellSignal("ATRStops_v1.1_Alert",ExtMapBufferDown1,rates_total,prev_calculated,close,spread);
//--- 
   return(rates_total);
  }
//+------------------------------------------------------------------+
//| Buy signal function|
//+------------------------------------------------------------------+
void BuySignal(string SignalSirname,      // indicator name text for mail and push signals
               double &BuyArrow[],        // indicator buffer with buy signals
               const int Rates_total,     // current number of bars
               const int Prev_calculated, // number of bars on the previous tick
               const double &Close[],     // closing price
               const int &Spread[])       // spread
  {
//---
   static uint counter=0;
   if(Rates_total!=Prev_calculated) counter=0;

   bool BuySignal=false;
   bool SeriesTest=ArrayGetAsSeries(BuyArrow);
   int index;
   if(SeriesTest) index=int(NumberofBar);
   else index=Rates_total-int(NumberofBar)-1;
   if(NormalizeDouble(BuyArrow[index],_Digits) && BuyArrow[index]!=EMPTY_VALUE) BuySignal=true;
   if(BuySignal && counter<=NumberofAlerts)
     {
      counter++;
      MqlDateTime tm;
      TimeToStruct(TimeCurrent(),tm);
      string text=TimeToString(TimeCurrent(),TIME_DATE)+" "+string(tm.hour)+":"+string(tm.min);
      SeriesTest=ArrayGetAsSeries(Close);
      if(SeriesTest) index=int(NumberofBar);
      else index=Rates_total-int(NumberofBar)-1;
      double Ask=Close[index];
      double Bid=Close[index];
      SeriesTest=ArrayGetAsSeries(Spread);
      if(SeriesTest) index=int(NumberofBar);
      else index=Rates_total-int(NumberofBar)-1;
      Bid+=Spread[index]*_Point;
      string sAsk=DoubleToString(Ask,_Digits);
      string sBid=DoubleToString(Bid,_Digits);
      string sPeriod=GetStringTimeframe(ChartPeriod());
      if(SoundON) Alert("BUY signal \n Ask=",Ask,"\n Bid=",Bid,"\n currtime=",text,"\n Symbol=",Symbol()," Period=",sPeriod);
      if(EMailON) SendMail(SignalSirname+": BUY signal alert","BUY signal at Ask="+sAsk+", Bid="+sBid+", Date="+text+" Symbol="+Symbol()+" Period="+sPeriod);
      if(PushON) SendNotification(SignalSirname+": BUY signal at Ask="+sAsk+", Bid="+sBid+", Date="+text+" Symbol="+Symbol()+" Period="+sPeriod);
     }

//---
  }
//+------------------------------------------------------------------+
//| Sell signal function|
//+------------------------------------------------------------------+
void SellSignal(string SignalSirname,      // indicator name text for mail and push signals
                double &SellArrow[],       // indicator buffer with buy signals
                const int Rates_total,     // current number of bars
                const int Prev_calculated, // number of bars on the previous tick
                const double &Close[],     // closing price
                const int &Spread[])       // spread
  {
//---
   static uint counter=0;
   if(Rates_total!=Prev_calculated) counter=0;

   bool SellSignal=false;
   bool SeriesTest=ArrayGetAsSeries(SellArrow);
   int index;
   if(SeriesTest) index=int(NumberofBar);
   else index=Rates_total-int(NumberofBar)-1;
   if(NormalizeDouble(SellArrow[index],_Digits) && SellArrow[index]!=EMPTY_VALUE) SellSignal=true;
   if(SellSignal && counter<=NumberofAlerts)
     {
      counter++;
      MqlDateTime tm;
      TimeToStruct(TimeCurrent(),tm);
      string text=TimeToString(TimeCurrent(),TIME_DATE)+" "+string(tm.hour)+":"+string(tm.min);
      SeriesTest=ArrayGetAsSeries(Close);
      if(SeriesTest) index=int(NumberofBar);
      else index=Rates_total-int(NumberofBar)-1;
      double Ask=Close[index];
      double Bid=Close[index];
      SeriesTest=ArrayGetAsSeries(Spread);
      if(SeriesTest) index=int(NumberofBar);
      else index=Rates_total-int(NumberofBar)-1;
      Bid+=Spread[index]*_Point;
      string sAsk=DoubleToString(Ask,_Digits);
      string sBid=DoubleToString(Bid,_Digits);
      string sPeriod=GetStringTimeframe(ChartPeriod());
      if(SoundON) Alert("SELL signal \n Ask=",Ask,"\n Bid=",Bid,"\n currtime=",text,"\n Symbol=",Symbol()," Period=",sPeriod);
      if(EMailON) SendMail(SignalSirname+": SELL signal alert","SELL signal at Ask="+sAsk+", Bid="+sBid+", Date="+text+" Symbol="+Symbol()+" Period="+sPeriod);
      if(PushON) SendNotification(SignalSirname+": SELL signal at Ask="+sAsk+", Bid="+sBid+", Date="+text+" Symbol="+Symbol()+" Period="+sPeriod);
     }
//---
  }
//+------------------------------------------------------------------+
//| Get timeframe as a string|
//+------------------------------------------------------------------+
string GetStringTimeframe(ENUM_TIMEFRAMES timeframe)
  {
//----
   return(StringSubstr(EnumToString(timeframe),7,-1));
//----
  }
//+------------------------------------------------------------------+
```

![Альберт Туйкин](https://c.mql5.com/avatar/avatar_na2.png "Альберт Туйкин")

**[Альберт Туйкин](/en/users/darkcorp)**
|
24 Apr 2017 at 08:33

Thanks, it worked!

![gkb](https://c.mql5.com/avatar/avatar_na2.png "gkb")

**[gkb](/en/users/gkb)**
|
3 Dec 2022 at 07:46

Hi,

Can you add English language in properties, so we can understand it.

Thanks

![Fernando Carreiro](https://c.mql5.com/avatar/2025/9/68d40cf8-38fb.png "Fernando Carreiro")

**[Fernando Carreiro](/en/users/fmic)**
|
3 Dec 2022 at 11:10

**gkb [#](/en/forum/193565#comment_43585695):** Hi, Can you add English language in properties, so we can understand it.Thanks

The original publication was in the Russian *CodeBase* in 2017. *MetaQuotes* is the one that made a translation of the publication.

Translated from Russian by MetaQuotes Ltd.   
Original code: [https://www.mql5.com/ru/code/17881](/ru/code/17881)

So I doubt the original author will do that. You could use Google translate and change the code comments into English yourself.

![XCCXCandleKeltnerPluse_HTF](https://c.mql5.com/i/code/indicator.png)
[XCCXCandleKeltnerPluse\_HTF](/en/code/17880)

The XCCXCandleKeltnerPluse indicator with the timeframe selection option available in its input parameters.

![XRSXCandleKeltnerPluse_HTF](https://c.mql5.com/i/code/indicator.png)
[XRSXCandleKeltnerPluse\_HTF](/en/code/17879)

The XRSXCandleKeltnerPluse indicator with the timeframe selection option available in its input parameters.

![XOSignal_HTF_Signal](https://c.mql5.com/i/code/indicator.png)
[XOSignal\_HTF\_Signal](/en/code/17890)

The XOSignal\_HTF\_Signal indicator shows a trend direction or a trade signal generated by the XOSignal indicator at the chosen bar as a graphical object with colored trend indication or deal direction. It also triggers alerts and plays audio signals.

![WATR_HTF_Signal](https://c.mql5.com/i/code/indicator.png)
[WATR\_HTF\_Signal](/en/code/17891)

The WATR\_HTF\_Signal indicator shows a trend direction or a trade signal generated by the WATR indicator at the chosen bar as a graphical object with colored trend indication or deal direction. It also triggers alerts and plays audio signals.