# MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + MFI - expert for MetaTrader 5

Source: https://www.mql5.com/en/code/307

* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 5](/en/code/mt5)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt5/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt5/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt5/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt5/libraries)
* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 4](/en/code/mt4)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt4/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt4/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt4/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt4/libraries)

* [![](https://c.mql5.com/i/sidebar/storage.svg)Storage](/en/code/storage)

Watch [how to download](https://youtu.be/rloNyFVtHuA?list=PLltlMLQ7OLeKwyQwC8FhiKwjl9syKhOCK) trading robots for free

Find us on [Twitter](https://x.com/mql5com)!  
 Join our fan page

Interesting script?  
So post a [link](/en/code/307) to it -  
let others appraise it

You liked the script? Try it in the [MetaTrader 5](https://download.mql5.com/cdn/web/metaquotes.ltd/mt5/mt5setup.exe?utm_source=www.mql5.com&utm_campaign=download) terminal

[to pocket](#pocket "Pocket allows you to insert a complete content description to the appropriate comment")

![Experts](https://c.mql5.com/i/code/expert.png)

# MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + MFI - expert for MetaTrader 5

[MetaQuotes](/en/users/metaquotes)  |
[English](javascript:void(false);)

[Русский](/ru/code/307) [中文](/zh/code/307) [Español](/es/code/307) [Deutsch](/de/code/307) [日本語](/ja/code/307) [Português](/pt/code/307) [한국어](/ko/code/307) [Français](/fr/code/307) [Italiano](/it/code/307) [Türkçe](/tr/code/307)

Views:
:   13888

Rating:
:   (23)

Published:
:   10 March 2011, 10:55

Updated:
:   22 November 2016, 07:32
:   \MQL5\Include\Expert\Signal\MySignals\

    [acandlepatterns.mqh](/en/code/download/307/acandlepatterns.mqh "acandlepatterns.mqh")
    (20.34 KB)
    [view](# "view")

    [acbe\_be\_mfi.mqh](/en/code/download/307/acbe_be_mfi.mqh "acbe_be_mfi.mqh")
    (8.02 KB)
    [view](# "view")

    [expert\_abe\_be\_mfi.mq5](/en/code/download/307/expert_abe_be_mfi.mq5 "expert_abe_be_mfi.mq5")
    (6.63 KB)
    [view](# "view")

    [Download as ZIP](/en/code/download/307.zip "Download all attachments in the single ZIP archive") [How to download code from MetaEditor](https://www.metatrader5.com/en/metaeditor/help/workspace/toolbox#codebase)

    ![MQL5 - Language of trade strategies built-in the MetaTrader 5 client terminal](https://c.mql5.com/i/registerlandings/logo-2.png)

    You are missing trading opportunities:

    * Free trading apps
    * Over 8,000 signals for copying
    * Economic news for exploring financial markets

    Registration
    Log in

    latin characters without spaces

    a password will be sent to this email

    An error occurred

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup&amp;reg=1)

    You agree to [website policy](/en/about/privacy) and [terms of use](/en/about/terms)

    If you do not have an account, please [register](https://www.mql5.com/en/auth_register)

    Allow the use of cookies to log in to the MQL5.com website.

    Please enable the necessary setting in your browser, otherwise you will not be able to log in.

     

    [Forgot your login/password?](https://www.mql5.com/en/auth_forgotten?return=popup)

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup)
:   ![MQL5 Freelance](https://c.mql5.com/i/code/icon_freelance.svg)
    Need a robot or indicator based on this code? Order it on Freelance
    [Go to Freelance](/en/job/new "Go to Freelance")

The [MQL5 Wizard](https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate "https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate") allows creating ready-made Expert Advisors based on the [Standard library](/en/docs/standardlibrary/expertclasses) classes delivered together with the client terminal (see [Creating Ready-Made Expert Advisors in MQL5 Wizard](https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate "https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate") for the details). It allows to check your trade ideas quickly, all you need is to create your own trading signals class. The structure of this class and example can be found in the article [MQL5 Wizard: How to Create a Module of Trading Signals](/en/articles/226).

The generic idea is the following: the class of trading signals is derived from [CExpertSignal](/en/docs/standardlibrary/expertclasses/expertbaseclasses/cexpertsignal), the next, it's necessary to override the [LongCondition()](/en/docs/standardlibrary/expertclasses/expertbaseclasses/cexpertsignal/cexpertsignallongcondition) and [ShortCondition()](/en/docs/standardlibrary/expertclasses/expertbaseclasses/cexpertsignal/cexpertsignalshortcondition) virtual methods with your own methods.

There is a book ["Strategies of best traders](https://www.mql5.com/go?link=http://www.tora-centre.ru/book/strategy.htm "http://www.tora-centre.ru/book/strategy.htm")" (in Russian), there are many trading strategies are considered there, we will focus on reversal candlestick patterns, confirmed by [Stochastic](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/so "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/so"), [CCI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/cci"), [MFI](https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation "https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation") and [RSI](https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi "https://www.metatrader5.com/en/terminal/help/indicators/oscillators/rsi") oscillators.

The best way is to create the separate class, derived from [CExpertSignal](/en/docs/standardlibrary/expertclasses/expertbaseclasses/cexpertsignal) for checking of formation of candlestick patterns. For confirmation of trade signals, generated by candlestick patterns, it's sufficient to write the class, derived from CCandlePattern and add the necessary features (for example, confirmation by oscillators) there.

Here we will consider the signals, based "Bullish Engulfing/Bearish Engulfing" reversal candlestick pattern, confirmed by [Market Facilitation Index (MFI)](https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation "https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation") indicator. The module of trade signals is based on the [CCandlePattern](/en/code/291) class, it's simple example of its use for creation of trade signals with candlestick patterns.

  

**1. "Bullish Engulfing" and "Bearish Engulfing" reversal candlestick patterns**

**1.1. Bullish Engulfing**

"Bullish Engulfing" reversal pattern forms in downtrend when a small black candlestick if followed by a large white candlestick that completely eclipses ("engulfs") the candlestick of the previous day. The shadows (tails) of the small candlestick are short, which enables the body of the large candlestick to cover the entire candlestick from the previous day.

![Fig. 1. "Bullish Engulfing" candlestick pattern](https://c.mql5.com/18/2/bullish_engulfing_en__4.png "Fig. 1. \"Bullish Engulfing\" candlestick pattern")

Fig. 1. "Bullish Engulfing" candlestick pattern

The recognition of "Bullish Engulfing" pattern is implemented in CheckPatternBullishEngulfing() method of [CCandlePattern](/en/code/291) class:

```mql5
//+------------------------------------------------------------------+
//| Checks formation of "Bullish Engulfing" candlestick pattern       |
//+------------------------------------------------------------------+
bool CCandlePattern::CheckPatternBullishEngulfing()
  {
//--- Bullish Engulfing
   if((Open(2)>Close(2))             &&  // previous candle is bearish
      (Close(1)-Open(1)>AvgBody(1))  &&  // body of the bullish candle is higher than average value of the body 
      (Close(1)>Open(2))             &&  // close price of the bullish candle is higher than open price of the bearish candle
      (MidOpenClose(2)<CloseAvg(2))  &&  // downtrend
      (Open(1)<Close(2)))                // open price of the bullish candle is lower than close price of the bearish
      return(true);
//---
   return(false);
  }
```

The CheckCandlestickPattern(CANDLE\_PATTERN\_BULLISH\_ENGULFING) method of [CCandlePattern](/en/code/291) class is used to check formation of "Bullish Engulfing" candlestick pattern.

  

**1.2. Bearish Engulfing**

"Bearish Engulfing" reversal pattern forms in uptrend when a small white candlestick if followed by a large black candlestick that completely eclipses ("engulfs") the candlestick of the previous day. The shadows (tails) of the small candlestick are short, which enables the body of the large candlestick to cover the entire candlestick from the previous day.

![Fig. 2. "Bearish Engulfing" candlestick pattern](https://c.mql5.com/18/2/bearish_engulfing_en__4.png "Fig. 2. \"Bearish Engulfing\" candlestick pattern")

Fig. 2. "Bearish Engulfing" candlestick pattern

  

The recognition of "Bearish Engulfing" pattern is implemented in CheckPatternBearishEngulfing() method of [CCandlePattern](/en/code/291) class:

```mql5
//+------------------------------------------------------------------+
//| Checks formation of "Bearish Engulfing" candlestick pattern       |
//+------------------------------------------------------------------+
bool CCandlePattern::CheckPatternBearishEngulfing()
  {
//--- Bearish Engulfing
   if((Open(2)<Close(2))            &&  // previous candle is bearish
      (Open(1)-Close(1)>AvgBody(1)) &&  // body of the candle is higher than average value of the body
      (Close(1)<Open(2))            &&  // close price of the bearish candle is lower than open price of the bullish candle
      (MidOpenClose(2)>CloseAvg(2)) &&  // uptrend
      (Open(1)>Close(2)))               // Open price of the bearish candle is higher than close price of the bullish candle
      return(true);
//---
   return(false);
  }
```

The CheckCandlestickPattern(CANDLE\_PATTERN\_BEARISH\_ENGULFING) method of [CCandlePattern](/en/code/291) class is used to check formation of "Bearish Engulfing" candlestick pattern.

  

**2. Trade signals, confirmed by MFI indicator**

The trading signals to open long or short position must be confirmed by [MFI](https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation "https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation") indicator. The value of MFI indicator must be lower than 40 (for long positions) or greater than 60 (for short positions).

The closing of opened position depends on the values of MFI indicator. It can be done in 2 cases:

1. if MFI has reached the opposite critical level (70 for long position and 30 for short position)
2. if the reverse signal isn't confirmed (when MFI indicator reaches the following levels: 30 for long position and 70 for short position)

![Fig. 3. "Bullish Engulfing" pattern, confirmed by MFI indicator](https://c.mql5.com/18/2/Image_MFI.png "Fig. 3. \"Bullish Engulfing\" pattern, confirmed by MFI indicator")

Fig. 3. "Bullish Engulfing" pattern, confirmed by MFI indicator

  

* int CBE\_BE\_MFI::LongCondition() - checks conditions to open long position (returns 80) and close of the short position (returns 40);
* int CBE\_BE\_MFI::ShortCondition() - checks conditions to open short position (returns 80) and close of the long position (returns 40).

  

**2.1. Open long position/Close short position**

1. The formation of "Bullish Engulfing" pattern must be confirmed by [MFI](https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation "https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation") indicator: MFi(1)<40 (the value of MFI indicator of the last completed bar must be less than 40).
2. The short position must be closed if [MFI](https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation "https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation") indicator has crossed upward the critical levels (70 or 30).

```mql5
//+------------------------------------------------------------------+
//| Checks conditions for entry and exit from market                 |
//| 1) Market entry (open long position, result=80)                  |
//| 2) Market exit (close short position, result=40)                 |
//+------------------------------------------------------------------+
int CBE_BE_MFI::LongCondition()
  {
   int result=0;
//--- idx can be used to determine Expert Advisor work mode
//--- idx=0 - in this case EA checks trade conditions at each tick
//--- idx=1 - in this case EA checks trade consition only at news bars
   int idx   =StartIndex();
//--- checking of conditions to open long position
//--- formation of Bullish Engulfing pattern and MFI<40
  if(CheckCandlestickPattern(CANDLE_PATTERN_BULLISH_ENGULFING) && (MFI(1)<40))
     result=80;
//--- checking of conditions to close short position
//--- signal line crossover of overbought/oversold levels (upward 30, upward 70)
  if(((MFI(1)>30) && (MFI(2)<30)) || ((MFI(1)>70) && (MFI(2)<70)))
     result=40;
//--- return result
   return(result);
  }
```

**2.2. Open short position/Close long position**

1. The formation of "Bearish Engulfing" pattern must be confirmed by [MFI](https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation "https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation") indicator: MFI(1)>60 (the value of MFI indicator of the last completed bar must be greater than 60).
2. The long position must be closed if [MFI](https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation "https://www.metatrader5.com/en/terminal/help/indicators/bw_indicators/market_facilitation") indicator has crossed upward the critical levels (70 or 30).

```mql5
//+------------------------------------------------------------------+
//| Checks conditions for entry and exit from market                 |
//| 1) Market entry (open short position, result=80)                 |
//| 2) Market exit (close long position, result=40)                  |
//+------------------------------------------------------------------+
int CBE_BE_MFI::ShortCondition()
  {
   int result=0;
//--- idx can be used to determine Expert Advisor work mode
//--- idx=0 - in this case EA checks trade conditions at each tick
//--- idx=1 - in this case EA checks trade consition only at news bars
   int idx   =StartIndex();
//--- checking of conditions to open short position
//--- formation of Bearish Engulfing pattern and MFI>60
  if(CheckCandlestickPattern(CANDLE_PATTERN_BEARISH_ENGULFING) && (MFI(1)>60))
     result=80;
//--- checking of conditions to close long position
//--- signal line crossover of overbought/oversold levels (upward 70, downward 30)
   if(((MFI(1)>70) && (MFI(2)<70)) || ((MFI(1)<30) && (MFI(2)>30)))
     result=40;
//--- return result
   return(result);
  }
```

  
  

**2.3. Creating Expert Advisor using MQL5 Wizard**

The CBE\_BE\_MFI class isn't included in the Standard Library classes, to use it, it's necessary to download the abe\_be\_mfi.mqh file (see attachments) and save it to the client\_terminal\_data\folder\MQL5\Include\Expert\Signal\MySignals. The same should be done with the acandlepatterns.mqh file. You can use it in MQL5 Wizard after restart of the MetaEditor.

To create an Expert Advisor launch [MQL5 Wizard](https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate "https://www.metatrader5.com/en/metaeditor/help/mql5_wizard/wizard_ea_generate"):

![Fig. 4. Creating Expert Advisor using MQL5 Wizard](https://c.mql5.com/18/2/MQL5_Wizard_step_1__4.png "Fig. 4. Creating Expert Advisor using MQL5 Wizard")

Fig. 4. Creating Expert Advisor using MQL5 Wizard

Let's specify the name of the Expert Advisor:

![Fig. 5. General properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_2_BEBE_MFI.png "Fig. 5. General properties of the Expert Advisor")

Fig. 5. General properties of the Expert Advisor

After that we need to select the modules of trade signals used.

![Fig. 6. Signal properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_3__4.png "Fig. 6. Signal properties of the Expert Advisor")

Fig. 6. Signal properties of the Expert Advisor

In our case we use only one module of trade signals.

Adding the "**Signals based on Bullish Engulfing/Bearish Engulfing confrmed by MFI**" module of trading signals:

![Fig. 7. Signal properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_3_2_BEBE_MFI.png "Fig. 7. Signal properties of the Expert Advisor")

Fig. 7. Signal properties of the Expert Advisor

Module of trade signals added:

![Fig. 8. Signal properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_3_3_BEBE_MFI.png "Fig. 8. Signal properties of the Expert Advisor")

Fig. 8. Signal properties of the Expert Advisor

You can select any trailing properties, but we will use "Trailing Stop not used":

![Fig. 9. Trailing properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_4__5.png "Fig. 9. Trailing properties of the Expert Advisor")

Fig. 9. Trailing properties of the Expert Advisor

Concerning the money management properties, we will use "Trading with fixed trade volume":

![Fig. 10. Money management properties of the Expert Advisor](https://c.mql5.com/18/2/MQL5_Wizard_step_5__5.png "Fig. 10. Money management properties of the Expert Advisor")

Fig. 10. Money management properties of the Expert Advisor

By pressing the "Finish" button, we will get the code of the generated Expert Advisor, located in Expert\_ABE\_BE\_MFI.mq5, it will be saved in terminal\_data\_folder\MQL5\Experts\.

The default input parameters of the generated Expert Advisor:

```mql5
//--- inputs for main signal
input int            Signal_ThresholdOpen   =10;     // Signal threshold value to open [0...100]
input int            Signal_ThresholdClose  =10;     // Signal threshold value to close [0...100]
input double         Signal_PriceLevel      =0.0;    // Price level to execute a deal
input double         Signal_StopLevel       =50.0;   // Stop Loss level (in points)
input double         Signal_TakeLevel       =50.0;   // Take Profit level (in points)
```

must be replaced to:

```mql5
//--- inputs for main signal
input int            Signal_ThresholdOpen   =40;     // Signal threshold value to open [0...100]
input int            Signal_ThresholdClose  =20;     // Signal threshold value to close [0...100]
input double         Signal_PriceLevel      =0.0;    // Price level to execute a deal
input double         Signal_StopLevel       =0.0;    // Stop Loss level (in points)
input double         Signal_TakeLevel       =0.0;    // Take Profit level (in points)
```

The Signal\_ThresholdOpen/Signal\_ThresholdClose input parameters allow to specify threshold levels for open and close of positions.

In code of the LongCondition() and ShortCondition() methods of the trade signals class we have specified
the fixed values of the threshold:

* Open position: 80;
* Close position: 40.

The Expert Advisor, generated by MQL5 Wizard open and close position using the "votes" from the modules of trade signals. The vote of the main module (as container, it consist of all the modules added) is also used, but its LongCondition() and ShortCondition() methods always return 0.

The vote results of the main module is also used in "votes" averaging. In our case we have:  main module + 1 module of trade signals, so we need to take this fact into account when setting of the threshold values. Because of this fact the ThresholdOpen and ThresholdClose must be set as 40=(0+80)/2 and 20=(0+40)/2.

The values of  Signal\_StopLevel and Signal\_TakeLevel input parameters is set to 0, it means that closing of the positions will be done only when closing conditions will be true.

  

**2.4. History backtesting results**

Let's consider backtesting of the Expert Advisor on historical data (EURUSD H1, testing period: 2010.01.01-2011.03.16, PeriodMFI=37, MA\_period=5).

In creation of Expert Advisor we used the fixed volume ([Trading Fixed Lot](/en/docs/standardlibrary/expertclasses/samplemmclasses/cmoneyfixedlot), 0.1), Trailing Stop algorithm is not used ([Trailing not used](/en/docs/standardlibrary/expertclasses/sampletrailingclasses/ctrailingnone)).

![Fig. 11. Testing results of the Expert Advisor, based on Bullish Engulfing /Bearish Engulfing + MFI](https://c.mql5.com/18/2/BEBE_MFI_Test_Results.png "Fig. 11. Testing results of the Expert Advisor, based on Bullish Engulfing /Bearish Engulfing + MFI")

Fig. 11. Testing results of the Expert Advisor, based on Bullish Engulfing /Bearish Engulfing + MFI

  

The best set of input parameters can be found using the [Strategy Tester](https://www.metatrader5.com/en/automated-trading/strategy-tester "https://www.metatrader5.com/en/automated-trading/strategy-tester") of MetaTrader 5 client terminal.

The code of the Expert Advisor, created by MQL5 Wizard is attached in expert\_abe\_be\_mfi.mq5.

Translated from Russian by MetaQuotes Ltd.   
Original code: [https://www.mql5.com/ru/code/307](/ru/code/307)

**Last comments |
[Go to discussion](/en/forum/3379)**
(4)

![muller](https://c.mql5.com/avatar/avatar_na2.png "muller")

**[muller](/en/users/razoff)**
|
29 Mar 2011 at 15:54

all orders are only 0.1 lot (settings are default, eg 10% & min lot 0.1), so [money management](https://www.mql5.com/en/articles/4162 "Article: Money Management by Vince. Implementation as a MQL5 Wizard Module") dont work

![Jorge Cardoso](https://c.mql5.com/avatar/avatar_na2.png "Jorge Cardoso")

**[Jorge Cardoso](/en/users/jorgeemec)**
|
1 Dec 2014 at 13:51

hi,

![Automated-Trading](https://c.mql5.com/avatar/2021/6/60C759A6-5565.png "Automated-Trading")

**[Automated-Trading](/en/users/automated-trading)**
|
1 Dec 2014 at 15:11

**jorgeemec:**  
    
I use this expert advisor, but now i need to introduce a trailing stop. is it possivel to use the RSI value as a trailing stop and implement it in the expert advisor.

You can write your own trailing stop module like this [https://www.mql5.com/en/code/1723](/en/code/1723)

![](https://c.mql5.com/avatar/avatar_na2.png)

**[Deleted]**
|
19 Mar 2018 at 15:40

All that I download from the library according to the [results of optimisation](https://www.metatrader5.com/en/terminal/help/algotrading/testing "Optimisation results in MetaTrader 5 client terminal") trades worse than standard signals that are already installed in mt5.I found a really good utility-signal time filter simplified-it's a damn thing.You can safely leave the robot to trade if the goal is to enter in a small time interval.But so all advisors and signals from the library persistently give drawdowns more than standard signals mt5.

.

![MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + CCI](https://c.mql5.com/i/code/expert.png)
[MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + CCI](/en/code/306)

Trade signals based on "Bullish Engulfing/Bearish Engulfing" candlestick pattern, confirmed by Commodity Channel Index (CCI) indicator is considered. The code of the Expert Advisor based on this strategy can be generated automatically using the MQL5 Wizard.

![MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + Stochastic](https://c.mql5.com/i/code/expert.png)
[MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + Stochastic](/en/code/305)

Trade signals based on "Bullish Engulfing/Bearish Engulfing" candlestick pattern, confirmed by Stochastic indicator is considered. The code of the Expert Advisor based on this strategy can be generated automatically using the MQL5 Wizard.

![MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + RSI](https://c.mql5.com/i/code/expert.png)
[MQL5 Wizard - Trade Signals Based on Bullish Engulfing/Bearish Engulfing + RSI](/en/code/308)

Trade signals based on "Bullish Engulfing/Bearish Engulfing" candlestick pattern, confirmed by Relative Strength Index (RSI) indicator is considered. The code of the Expert Advisor based on this strategy can be generated automatically using the MQL5 Wizard.

![MQL5 Wizard - Trade Signals Based on Bullish Harami/Bearish Harami + Stochastic](https://c.mql5.com/i/code/expert.png)
[MQL5 Wizard - Trade Signals Based on Bullish Harami/Bearish Harami + Stochastic](/en/code/310)

Trade signals based on "Bullish Harami/Bearish Harami" candlestick pattern, confirmed by Stochastic indicator is considered. The code of the Expert Advisor based on this strategy can be generated automatically using the MQL5 Wizard.