# Calendar - library for MetaTrader 5

Source: https://www.mql5.com/en/code/32430

* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 5](/en/code/mt5)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt5/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt5/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt5/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt5/libraries)
* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 4](/en/code/mt4)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt4/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt4/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt4/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt4/libraries)

* [![](https://c.mql5.com/i/sidebar/storage.svg)Storage](/en/code/storage)

Watch [how to download](https://youtu.be/rloNyFVtHuA?list=PLltlMLQ7OLeKwyQwC8FhiKwjl9syKhOCK) trading robots for free

Find us on [Telegram](https://t.me/mql5dev)!  
 Join our fan page

Interesting script?  
So post a [link](/en/code/32430) to it -  
let others appraise it

You liked the script? Try it in the [MetaTrader 5](https://download.mql5.com/cdn/web/metaquotes.ltd/mt5/MetaTrader5.pkg.zip?utm_source=www.mql5.com&utm_campaign=download) terminal

[to pocket](#pocket "Pocket allows you to insert a complete content description to the appropriate comment")

![Libraries](https://c.mql5.com/i/code/library.png)

# Calendar - library for MetaTrader 5

[fxsaber](/en/users/fxsaber)  |
[English](javascript:void(false);)

[Русский](/ru/code/32430) [中文](/zh/code/32430) [Español](/es/code/32430) [Deutsch](/de/code/32430) [日本語](/ja/code/32430) [Português](/pt/code/32430) [한국어](/ko/code/32430) [Français](/fr/code/32430) [Italiano](/it/code/32430) [Türkçe](/tr/code/32430)

Views:
:   3630

Rating:
:   (30)

Published:
:   3 April 2025, 11:16
:   \MQL5\Include\fxsaber\Calendar\

    [String.mqh](/en/code/download/32430/string.mqh "String.mqh")
    (1.77 KB)
    [view](# "view")

    [Event.mqh](/en/code/download/32430/event.mqh "Event.mqh")
    (20.88 KB)
    [view](# "view")

    [Calendar.mqh](/en/code/download/32430/calendar.mqh "Calendar.mqh")
    (30.15 KB)
    [view](# "view")

    [DST.mqh](/en/code/download/32430/dst.mqh "DST.mqh")
    (11.46 KB)
    [view](# "view")

    \MQL5\Experts\

    [Calendar\_Example.mq5](/en/code/download/32430/calendar_example.mq5 "Calendar_Example.mq5")
    (7.48 KB)
    [view](# "view")

    [Download as ZIP](/en/code/download/32430.zip "Download all attachments in the single ZIP archive") [How to download code from MetaEditor](https://www.metatrader5.com/en/metaeditor/help/workspace/toolbox#codebase)

    ![MQL5 - Language of trade strategies built-in the MetaTrader 5 client terminal](https://c.mql5.com/i/registerlandings/logo-2.png)

    You are missing trading opportunities:

    * Free trading apps
    * Over 8,000 signals for copying
    * Economic news for exploring financial markets

    Registration
    Log in

    latin characters without spaces

    a password will be sent to this email

    An error occurred

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup&amp;reg=1)

    You agree to [website policy](/en/about/privacy) and [terms of use](/en/about/terms)

    If you do not have an account, please [register](https://www.mql5.com/en/auth_register)

    Allow the use of cookies to log in to the MQL5.com website.

    Please enable the necessary setting in your browser, otherwise you will not be able to log in.

     

    [Forgot your login/password?](https://www.mql5.com/en/auth_forgotten?return=popup)

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup)
:   ![MQL5 Freelance](https://c.mql5.com/i/code/icon_freelance.svg)
    Need a robot or indicator based on this code? Order it on Freelance
    [Go to Freelance](/en/job/new "Go to Freelance")

MetaTrader5 has an updated volumetric history of fundamental calendar events, any data of which can [be accessed programmatically](/ru/docs/calendar).

This library allows you to conveniently work with this data. In particular, to use them in the Tester and in real trading.

Working with the library is shown in the form of comments of the source code of the examples.

### Obtaining historical data.

```mql5
// The script outputs historical data.

#include <fxsaber\Calendar\Calendar.mqh> // Calendar - fundamental analysis on history and real-time.

void OnStart()
{
  CALENDAR Calendar;
  
  // Took events for all (NULL) currencies starting with the lowest (NONE) importance.
  const int Size = Calendar.Set(NULL, CALENDAR_IMPORTANCE_NONE, D'2020.12.07', D'2020.12.08');
  
  // Printed them out.
  for (int i = 0; i < Size; i++)
    Print(Calendar[i].ToString());
}
```

Result.

```mql5
2020.12.07 AllDay EUR 0 День Конституции(constitution-day), Испания(ES) |  |  |  | 
2020.12.07 00:00 AUD 3 Выступление председателя Резервного Банка Австралии Лоу(rba-governor-lowe-speech), Австралия(AU) |  |  |  | 
2020.12.07 01:50 JPY 1 Международные резервы(jp-foreign-reserves), Япония(JP) | $1384.6 B |  | $1384.4 B | 
2020.12.07 02:30 AUD 1 Количество объявлений о вакансиях от ANZ м/м(anz-job-advertisements-mm), Австралия(AU) | 13.9% | 0.8% | 9.4% | 
2020.12.07 ≈05:00 CNY 1 Объем импорта USD г/г(imports-usd-yy), Китай(CN) | 4.5% | 4.4% | 4.7% | 
2020.12.07 ≈05:00 CNY 1 Объем экспорта USD г/г(exports-usd-yy), Китай(CN) | 21.1% | 3.6% | 11.4% | 
2020.12.07 ≈05:00 CNY 2 Торговый баланс(trade-balance), Китай(CN) | ¥507.1 B | ¥271.31 B | ¥401.75 B | 
2020.12.07 ≈05:00 CNY 2 Торговый баланс USD(trade-balance-usd), Китай(CN) | $75.42 B | $52.55 B | $58.44 B | 
2020.12.07 ≈05:00 CNY 1 Объем импорта г/г(imports-yy), Китай(CN) | -0.8% | -0.6% | 0.9% | 
2020.12.07 ≈05:00 CNY 1 Объем экспорта г/г(exports-yy), Китай(CN) | 14.9% | 2.2% | 7.6% | 
2020.12.07 07:00 JPY 1 Индекс совпадающих индикаторов(coincident-index), Япония(JP) | 89.7 | 81.8 | 81.1 | 84.8
2020.12.07 07:00 JPY 1 Индекс ведущих экономических индикаторов(leading-index), Япония(JP) | 93.8 | 92.7 | 92.5 | 93.3
2020.12.07 07:00 JPY 1 Индекс совпадающих индикаторов Японии м/м(coincident-index-mm), Япония(JP) | 4.9% |  | 1.7% | 2.4%
2020.12.07 07:00 JPY 1 Индекс ведущих экономических индикаторов м/м(leading-index-mm), Япония(JP) | 0.5% |  | 4.0% | 4.2%
2020.12.07 08:00 ZAR 1 Валовые международные резервы(gross-international-reserves), Южно-Африканская Республика(ZA) | $53.76 B | $54.129 B | $53.658 B | 
2020.12.07 08:00 ZAR 1 Чистые международные резервы(net-international-reserves), Южно-Африканская Республика(ZA) | $51.257 B | $52.719 B | $51.364 B | 
2020.12.07 09:00 EUR 2 Промышленное производство г/г(industrial-production-yy), Германия(DE) | -3.0% | -11.4% | -7.3% | -6.7%
2020.12.07 09:00 EUR 2 Промышленное производство м/м(industrial-production-mm), Германия(DE) | 3.2% | 7.7% | 1.6% | 2.3%
2020.12.07 09:00 NOK 1 Производство в обрабатывающей промышленности м/м(manufacturing-production-mm), Норвегия(NO) | 0.6% | 0.1% | -0.5% | 
2020.12.07 09:00 NOK 1 Производство в обрабатывающей промышленности г/г(manufacturing-production-yy), Норвегия(NO) | -2.7% | -3.3% | -3.4% | 
2020.12.07 09:00 NOK 1 Промышленное производство м/м(industrial-production-mm), Норвегия(NO) | -3.5% | 0.0% | -1.7% | -1.6%
2020.12.07 09:00 NOK 1 Промышленное производство г/г(industrial-production-yy), Норвегия(NO) | 0.0% | 6.9% | 6.3% | 
```

You can compare it with the MT5-Terminal itself.

[![](https://c.mql5.com/18/160/5508312125697__1.png)](https://c.mql5.com/18/160/5508312125697.png "https://c.mql5.com/18/94/5508312125697.png")

### Getting upcoming events.

```mql5
// The script displays upcoming events.

#include <fxsaber\Calendar\Calendar.mqh> // Calendar - fundamental analysis on history and real-time.

void OnStart()
{
  CALENDAR Calendar;
  
  string Currencies[2];
  
  // Get the currencies of the current character.
  Currencies[0] = ::SymbolInfoString(_Symbol, SYMBOL_CURRENCY_BASE);
  Currencies[1] = ::SymbolInfoString(_Symbol, SYMBOL_CURRENCY_PROFIT);
    
  // Took upcoming important events by symbol currencies.
  Calendar.Set(Currencies);
  
  Print(Calendar.ToString()); // Printed them out.
}
```

Such embedding in information systems allows to inform about important upcoming calendar events in Expert Advisors/indicators.

### Backtest on fundamental data.

Below is an Expert Advisor that trades [NonFarm Payrolls](/ru/economic-calendar/united-states/nonfarm-payrolls) by comparing current and forecast values.

```mql5
// Expert Advisor for trading in MT4/5-Tester on the history of fundamental data.

#define  CALENDAR_FILENAME "Calendar.bin" // File name for reading/writing the Calendar.
#property tester_file CALENDAR_FILENAME  // Specifies that MT5-Tester picks up this file.

#include <fxsaber\Calendar\Calendar.mqh> // Calendar - fundamental analysis on history and real-time.

input group "Calendar"
input string inCurrency = "USD";        // Currency
input string inFilterName = "payrolls"; // FilterName

input group "EA"
input int inTP = 1000; // TakeProfit
input int inSL = 1000; // StopLoss
input bool inReverse = true; // Trade direction

CALENDAR Calendar; // Object with calendar data.

int OnInit()
{      
  bool Res = false;
  
  if (MQLInfoInteger(MQL_TESTER)) // If working in Tester
  {
    Res = Calendar.Load(CALENDAR_FILENAME) &&      // Loaded the events from the file.
          Calendar.FilterByCurrency(inCurrency) && // Applied a currency filter.
          Calendar.FilterByName(inFilterName);     // Applied a filter on the event name.
    
    if (!Res)                                      // If there are problems with the loaded data,
      Print("Run the EA in the MT5-Terminal!");    // reported that you need to get them by running the EA in MT5-Terminal.
  }
#ifdef __MQL5__
  // Working in the Terminal.
  else if (Calendar.Set(NULL, CALENDAR_IMPORTANCE_NONE, 0, 0) && // Loaded absolutely all events (history + future) from MT5-Terminal.
           Calendar.Save(CALENDAR_FILENAME))                     // Save them to a file.
    MessageBox("You can run the EA in the MT4/5-Tester.");       // Reported that we can now work in MT4/5-Tester.
#endif // #ifdef __MQL5__

  return(!Res);
}

void OnTick()
{
  static int Pos = Calendar.GetPosAfter(TimeCurrent()); // Get the event position in the Calendar, which is right after the current time.
  
  if ((Pos < Calendar.GetAmount()) &&       // If you haven't gone beyond the Calendar
      (Calendar[Pos].time < TimeCurrent())) // and the current time has passed the event.
  {        
    const EVENT Event = Calendar[Pos];      // Received the corresponding event.
    
    if ((Event.Actual != LONG_MIN) && (Event.Forecast != LONG_MIN)) // If the current and forecast values of the event are set
    {
      Print(Event.ToString()); // Print this event in its entirety.

      if (Event.Actual > Event.Forecast)                                                                          // If the current value is greater than the forecast value,
        PositionOpen(inReverse, "Act.(" + Event.ActualToString() + ")>(" + Event.ForecastToString() + ")For.");   // open a position of one direction.
      else
        PositionOpen(!inReverse, "Act.(" + Event.ActualToString() + ")<=(" + Event.ForecastToString() + ")For."); // Otherwise, another direction.
    }
            
    Pos = Calendar.GetPosAfter(TimeCurrent(), Pos); // Get the event position in the Calendar, which is right after the current time.
  }
}

#include <MT4Orders.mqh> // https://www.mql5.com/en/code/16006

#define  Bid SymbolInfoDouble(_Symbol, SYMBOL_BID)
#define  Ask SymbolInfoDouble(_Symbol, SYMBOL_ASK)

// Opens a position with the specified comment.
TICKET_TYPE PositionOpen( const int Type, const string comment )
{
  return(Type ? OrderSend(_Symbol, OP_SELL, 1, Bid, 0, Bid + inSL * _Point, Bid - inTP * _Point, comment)
              : OrderSend(_Symbol, OP_BUY, 1, Ask, 0, Ask - inSL * _Point, Ask + inTP * _Point, comment));
}
```

  

Run it once in MT5-Terminal to save all historical data (~60 Mb). After that they will be available in the Tester.

Result (for six years at H1 opening prices).

[![](https://c.mql5.com/18/160/6094384946649__1.png)](https://c.mql5.com/18/160/6094384946649.png "https://c.mql5.com/18/94/6094384946649.png")

[![](https://c.mql5.com/18/160/5103345618091__1.png)](https://c.mql5.com/18/160/5103345618091.png "https://c.mql5.com/18/94/5103345618091.png")

### Features.

* Does not use DLLs, can run in the Marketplace.
* Cross-platform operation: in MT4 get data from file.

Translated from Russian by MetaQuotes Ltd.   
Original code: [https://www.mql5.com/ru/code/32430](/ru/code/32430)

**Last comments |
[Go to discussion](/en/forum/484132)**
(145)

![fxsaber](https://c.mql5.com/avatar/2019/8/5D67260D-44C9.png "fxsaber")

**[fxsaber](/en/users/fxsaber)**
|
8 Oct 2025 at 11:31

**Denis Kislicyn [#](/ru/forum/357807/page14#comment_58203864):**  
 Recently I noticed that saving the entire calendar in a file to work with it in the tester started to crash on timeout, after about 50 sec.

 [The error is not reproduced.](/ru/forum/357807/page4#comment_39867714)

![fxsaber](https://c.mql5.com/avatar/2019/8/5D67260D-44C9.png "fxsaber")

**[fxsaber](/en/users/fxsaber)**
|
8 Oct 2025 at 11:32

**Denis Kislicyn CalendarValueHistory , the function starts hanging for about 50 seconds and then crashes by timeout.**

It is not reproduced.

```mql5
void OnStart()
{
  MqlCalendarValue Values[];
  
  Print(CalendarValueHistory(Values, D'2025.01.01'));
  Print(_LastError);
}
```

![Dominik Egert](https://c.mql5.com/avatar/2024/2/65db1363-e44b.jpg "Dominik Egert")

**[Dominik Egert](/en/users/dominik_egert)**
|
8 Oct 2025 at 11:50

**fxsaber [#](/en/forum/484132/page15#comment_58218562):**  
 It is not reproduced.

Seems to be related to this:

[https://www.mql5.com/en/forum/496980](https://www.mql5.com/en/forum/496980 "https://www.mql5.com/en/forum/496980")

Maybe want to add the wrapper function for reliability

![Denis Kislicyn](https://c.mql5.com/avatar/2023/10/65351c75-a2be.jpg "Denis Kislicyn")

**[Denis Kislicyn](/en/users/deniskislitsyn)**
|
8 Oct 2025 at 22:16

**fxsaber [#](/en/forum/484132/page15#comment_58218562) :**  
 It is not reproduced.

I tested it on different servers and different terminals. CalendarValueHistory always times out. A wrapper was created in a nearby branch to work around it.

[https://www.mql5.com/en/forum/496980](/en/forum/496980 "https://www.mql5.com/en/forum/496980")

![fxsaber](https://c.mql5.com/avatar/2019/8/5D67260D-44C9.png "fxsaber")

**[fxsaber](/en/users/fxsaber)**
|
9 Oct 2025 at 04:12

**Denis Kislicyn [#](/ru/forum/357807/page15#comment_58223660):**

CalendarValueHistory is always terminated with a timeout.

This is not the case for me.

![SingleTesterCache](https://c.mql5.com/i/code/library.png)
[SingleTesterCache](/en/code/27611)

Tester's single pass data.

![PSAR Zigzag (Non lagging)](https://c.mql5.com/i/code/indicator.png)
[PSAR Zigzag (Non lagging)](/en/code/57724)

A zigzag based on the trend change of the parabolic sar

![Detecting the start of a new bar or candle](https://c.mql5.com/i/code/expert.png)
[Detecting the start of a new bar or candle](/en/code/41601)

Detecting the start of a new bar or candle in an Expert Advisor's OnTick() event handler.

![New Candle or Bar formation.](https://c.mql5.com/i/code/expert.png)
[New Candle or Bar formation.](/en/code/57849)

This Bot detects the open of a new candle on any set timeframe, thereby making it easier to run a one-time code, place trades and call other functions. The code is written in the OnTick() function.