# MQL_Easy - library for MetaTrader 4

Source: https://www.mql5.com/en/code/25104

* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 5](/en/code/mt5)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt5/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt5/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt5/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt5/libraries)
* [![](https://c.mql5.com/i/sidebar/mt.svg)MetaTrader 4](/en/code/mt4)
  + [![](https://c.mql5.com/i/sidebar/expert.svg)Experts](/en/code/mt4/experts)
  + [![](https://c.mql5.com/i/sidebar/indicator.svg)Indicators](/en/code/mt4/indicators)
  + [![](https://c.mql5.com/i/sidebar/scripts.svg)Scripts](/en/code/mt4/scripts)
  + [![](https://c.mql5.com/i/sidebar/library.svg)Libraries](/en/code/mt4/libraries)

* [![](https://c.mql5.com/i/sidebar/storage.svg)Storage](/en/code/storage)

Watch [how to download](https://youtu.be/rloNyFVtHuA?list=PLltlMLQ7OLeKwyQwC8FhiKwjl9syKhOCK) trading robots for free

Find us on [Twitter](https://x.com/mql5com)!  
 Join our fan page

Interesting script?  
So post a [link](/en/code/25104) to it -  
let others appraise it

You liked the script? Try it in the [MetaTrader 5](https://download.mql5.com/cdn/web/metaquotes.ltd/mt5/mt5setup.exe?utm_source=www.mql5.com&utm_campaign=download) terminal

[to pocket](#pocket "Pocket allows you to insert a complete content description to the appropriate comment")

![Libraries](https://c.mql5.com/i/code/library.png)

# MQL\_Easy - library for MetaTrader 4

[Dionisis Nikolopoulos](/en/users/tradingso)

Views:
:   18647

Rating:
:   (39)

Published:
:   28 March 2019, 20:51

Updated:
:   17 September 2021, 13:03
:   \MQL4\Scripts\

    [MQL\_Easy\_Example.mq4](/en/code/download/25104/mql_easy_example.mq4 "MQL_Easy_Example.mq4")
    (2.27 KB)
    [view](# "view")

    \MQL4\Include\

    [MQL\_Easy\_Example.mqh](/en/code/download/25104/mql_easy_example.mqh "MQL_Easy_Example.mqh")
    (192.73 KB)
    [view](# "view")

    [MQL\_Easy.zip](/en/code/download/25104/mql_easy.zip "MQL_Easy.zip")
    (60.61 KB)

    [Download as ZIP](/en/code/download/25104.zip "Download all attachments in the single ZIP archive") [How to download code from MetaEditor](https://www.metatrader5.com/en/metaeditor/help/workspace/toolbox#codebase)

    ![MQL5 - Language of trade strategies built-in the MetaTrader 5 client terminal](https://c.mql5.com/i/registerlandings/logo-2.png)

    You are missing trading opportunities:

    * Free trading apps
    * Over 8,000 signals for copying
    * Economic news for exploring financial markets

    Registration
    Log in

    latin characters without spaces

    a password will be sent to this email

    An error occurred

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup&amp;reg=1)

    You agree to [website policy](/en/about/privacy) and [terms of use](/en/about/terms)

    If you do not have an account, please [register](https://www.mql5.com/en/auth_register)

    Allow the use of cookies to log in to the MQL5.com website.

    Please enable the necessary setting in your browser, otherwise you will not be able to log in.

     

    [Forgot your login/password?](https://www.mql5.com/en/auth_forgotten?return=popup)

    * [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup)
:   ![MQL5 Freelance](https://c.mql5.com/i/code/icon_freelance.svg)
    Need a robot or indicator based on this code? Order it on Freelance
    [Go to Freelance](/en/job/new "Go to Freelance")

### github: [https://github.com/Denn1Ro/MQL\_Easy](/go?link=https://github.com/Denn1Ro/MQL_Easy "https://github.com/Denn1Ro/MQL_Easy")

### Intro

MQL\_Easy is an open source cross platform library for developing MQL4 and MQL5 applications. The purpose of this library is to make the mql development easy, safe and fast in order to focus more on implementing complex trading ideas. The cross platform property assure that the same piece of code works on both platforms. In addition, it has implemented some unique features that make the development easier and faster. However, it does not cover the entire application-programming interface of the mql5/4 language. The goal is not to replace the entire MQL standard library which metaquotes has nicely implemented; it just fills the gap between the MQL4 and MQL5 programming and simplify the development of trading applications. 

Advantages:

* Cross platform compatibility(same piece of code works on both platforms)
* Error handling(all error codes are included with description)
* Validation checks(build in validation checks for order management)
* Hides complexity and speed up the development
* Publishing products on MQL5 market easier and safer.

The library contains the following classes:

* CExecute
* CPosition
* COrder
* CHistoryPosition
* CHistoryOrder
* CUtilities
* CError
* CPrinter
* CValidationCheck

It is important to mention that parts of code are collected from other resources on the internet. The goal is to gather any useful code into a single framework/library.   
The current documentation does not contain the whole functionality of the MQL\_Easy framework/library. It will be updated periodically in the future.

### Getting Started

To start working with the library all you need are 3 steps.

1. Download the folder MQL \_Easy.
2. Move it to Include folder inside MQL4 and MQL5 directory.
3. Copy paste the following command to your current MQL project:

```mql5
#include <MQL_Easy\MQL_Easy.mqh>
```

(The above line of code includes all the library’s files and make them available

Example:

```mql5
//+------------------------------------------------------------------+
//|                                             MQL_Easy_Example.mq5 |
//|                           Copyright 2021, Dionisis Nikolopoulos. |
//|                                                                  |
//+------------------------------------------------------------------+
#property copyright "Copyright 2021, Dionisis Nikolopoulos."
#property link      ""
#property version   "1.00"
#property strict

#include <MQL_Easy/MQL_Easy.mqh>
//-- Object that execute trades
CExecute execute;
//-- Object that manage trades
CPosition position;
//-- Object that print messages
CPrinter printer;

//+------------------------------------------------------------------+
//| Script program start function                                    |
//+------------------------------------------------------------------+
void OnStart()
  {
//---
   //-- Set the symbol and magic number to the objects
   string symbol = _Symbol;
   int magicNumber = 12345;
   execute.SetSymbol(symbol);
   execute.SetMagicNumber(magicNumber);
   position.SetGroupSymbol(symbol);
   position.SetGroupMagicNumber(magicNumber);
   
   //-- Create a trade Position BUY 
   ENUM_TYPE_POSITION type = TYPE_POSITION_BUY;
   double volume = 0.10;
   double stopLoss = 20;
   double takeProfit = 20;    
   int deviation = 10;
   string comment = "Test Position Execution";
   execute.Position(type,volume,stopLoss,takeProfit,SLTP_PIPS,deviation,comment);
   //-- delay for visual purpose
   Sleep(2000);
   //-- Check if the trade is executed
   if(position.GroupTotal() > 0 && position.SelectByIndex(0) != -1){
      //-- Collect Information about the trade
      long ticket       = position.GetTicket();
      double openPrice  = position.GetPriceOpen();  
      string message = "#Ticket: "+(string)ticket+", OpenPrice: "+(string)openPrice;
      printer.SetTitle("REPORT");
      printer.Add("Position Execution",message); 
      //-- delay for visual purposes
      Sleep(2000);
      //-- Modify the position using quick access feature
      position[0].Modify(50,50,SLTP_PIPS);
      printer.Add("Modify TP SL by 50 pips","DONE"); 
      //-- delay for visual purposes
      Sleep(2000);
      //-- Again Modify the position using quick access feature
      position[0].Modify(300,300,SLTP_POINTS);
      printer.Add("Modify TP SL by 300 points","DONE"); 
      //-- delay for visual purposes
      Sleep(2000);
      //-- Close the position
      position[0].Close();  
      printer.Add("Position Close","DONE"); 
      printer.PrintContent();
   } 
      
  }
//+------------------------------------------------------------------+
```

**This example of code is written in MQL4 editor but it runs and works properly on both platforms.**  

### 

### MQL\_Easy.mql

The MQL\_ Easy.mqh is an include file that contains all the others includes files.  
It is a quick way to get started without necessary know the names of classes. In addition, if the development requires heavy usage of the MQL\_Easy library then you can find them all in one place.    
When you get familiar with the library, you will be comfortable to include only the needed classes.

### Code overview

### Structure and design

The MQL\_Easy library has as many folders as the implemented classes. Each folder contains an include mql file and another folder called “Includes”. In order to use any class, it is just enough to include the mql include file in your project. The “Includes” folder just contains the implementation of a class for each platform. In some cases, the MQL4 and MQL5 platforms shares exactly the same code. However, in many cases the implementation is different, so the “Includes” folder has three files. One with a suffix “base” which implements the shared code and the others two with suffixes “MT4” and “MT5” for the platform dependent code. Therefore, in this way not only the platform dependent code is in separate file but also it can scale more easily with features which one platform supports and the other not.

### CExecute class

The CExecute class is responsible for trade execution. There are two kind of trades: Position (Buy,Sell) and Order(BuyLimit,SellLimit,BuyStop and SellStop). One time saver feature for this class is the ENUM\_SLTP\_TYPE enumeration, which the developer can set the type of the Stoploss and Takeprofit values (price,pips,points,percentage). Therefore, with a single line of code the library take care of converting the ENUM\_SLTP\_TYPE feature into prices, making validation checks in order to ensure that the request for the trade is a valid one before send it to the broker’s server and executes the trade in both platforms. In case of any error, a user-friendly message is displayed with the error code and details in the Expert’s tab. Besides the printed error info, the CExecute class also fills theses details into its own CError object, which the developer can use it to handle these situations, as he want.

Example:

```mql5
//-- Create an object of CExecute class and set the symbol and magic number
string symbol = _Symbol;
int magicNumber = 12345;
CExecute execute(symbol, magicNumber);
//-- or CExecute* execute = new CExecute(symbol, magicNumber);
//-- Alternative way to set the symbol and magic number
CExecute execute;
execute.SetSymbol(symbol);
execute.SetMagicNumber(magicNumber);
//-- Create a trade Position BUY 
ENUM_TYPE_POSITION type = TYPE_POSITION_BUY;
double volume = 0.10;
double stopLoss = 20;
double takeProfit = 20;    
execute.Position(type,volume,stopLoss,takeProfit,SLTP_PIPS);
//-- Create a trade Order SellStop 
execute.Order(TYPE_ORDER_SELLSTOP,0.10,1.1550,20,20,SLTP_PIPS);
```

### CPosition and COrder class

The CPosition and COrder classes are responsible to manage the active and pending trades of the account, such as collecting information about a trade or close it. With appropriate configuration, the developer can group trades by symbol, magic number, type or all together in order to manipulate these groups of trades easily. In general, the grouping property of the MQL\_Easy library saves a lot of time and give the ability to create complex trading ideas with less effort. Another unique feature that these classes has implemented is the “quick access” of trade. With just a single line of code, the developer can retrieve a property of trade such open price, open time etc.

Examples:

```mql5
   //-- create a position object which will group the trades with the same symbol, magic number //-- and type. In this example, the group contains trades of symbol EURUSD, magic number 
   //--12345 and type of trade BUY.
   string symbol =  ”EURUSD”;
   int magicNumber = 12345;
   CPosition position(symbol,magicNumber, GROUP_POSITIONS_BUYS);
   
   //-- return the total number of active trades with specific symbol, magic number and type, 
   //-- ignoring all the others 
   int totalPositions = position.GroupTotal(); 

   //-- iterate through specific group of trades
   for(int i = 0; i < totalPositions; i++){
      //-- select a position by its index
      if(position.SelectByIndex(i)){
         long ticket = position.GetTicket();
         double openPrice = position.GetPriceOpen();
         datetime openTime = position.GetTimeOpen();
         double profit = position.GetProfit();
         Print("#ticket: "+ticket+", openPrice: "+openPrice+", openTime:  +openTime+", profit:"+profit);
      }else{
         //-- if the position is not selected a default message error with user-friendly description 
         //-- will be displayed in the expert tab
         //-- also, you can retrieve the error information 
         int errorCode = position.Error.GetLastErrorCode();   
         //-- make custom actions      
      }
   }
//-- Examples with group methods
position.GroupTotalProfit(); // returns the total profit of a group 
position.GroupTotalVolume(); // returns the total volume of a group
position.GroupCloseAll(10);  // close all trades of a group. The number 10 is the tries in case of
                             // failure, by default is 20.
//-- Example of quick access by index
double openPrice = position[0].GetPriceOpen();
//-- The above code selects a position of a group with index 0(the first one) and retrieve the 
//-- open price.
//-- Here, it retrieves the open time of the last one.
datetime openTime = position[position.GroupTotal()-1].GetTimeOpen();

//-- Example of quick access by ticket
long ticket = 265748761; //-- the type long is important
double openPrice = position[ticket].GetPriceOpen();
//-- The above code selects a position by ticket number and retrieve the 
//-- open price.
```

*The difference between the “quick access by index” and “quick access by ticket” is the type of variable that is set in the brackets. Integer for indexes and long for tickets.*

### CHistoryPosition and CHistoryOrder class

The CHistoryPosition and CHistoryOrder are responsible for collecting information about active and pending trades in the past. They have the same features such as grouping and quick access as the CPosition and COrder classes. In addition, they have start date and end date in order to specify a time of period as an extra grouping filter. If you not specify the time of period then it will search the entire history of the account.

Example:

```mql5
CHistoryPosition historyPosition;
historyPosition.SetHistoryRange(D'2019.03.25 00:00:00',TimeCurrent());
for(int i =0; i < historyPosition.GroupTotal(); i++){
   Print("#ticket: "+historyPosition[i].GetTicket()+" time close :
   "+historyPosition[i].GetTimeClose());
   }
//-- The above code prints the ticket and time close of all positions from date 2019.03.25 
//-- 00:00:00 until now.
```

### CUtilities class

The CUtilitities class apply some common useful functions, which a trading application may needs.

Example:

```mql5
CUtilities utils("EURUSD");   
if(utils.IsNewBar(PERIOD_M1)){
    Print("One Minute Passed!!!");
}
```

### CPrinter class

The CPrinter class implements a quick and nice way for custom formatted messages to the terminal. It could be useful for error messages and debugging.

Example:

```mql5
CPrinter printer;   
printer.SetTitle("ATTENTION");
printer.SetContainer("-");
printer.Add("Action For The User","You need to enable Auto Trading!!!");
printer.Add("Steps","Press the Auto Trading Button at the top of the terminal");
printer.Print();
```

The above code display in the Expert’s tab:

--------------- ATTENTION ---------------

Action For The User: You need to enable Auto Trading!!!

Steps: Press the Auto Trading Button at the top of the terminal

--------------- ATTENTION ---------------

### CError class

The CError class is responsible for handling the errors. By default, it includes all the available error codes and their description. Therefore, the developer can deals with errors faster or alert the user with meaningful information.

Example:

```mql5
double ask = SymbolInfoDouble("WRONG_SYMBOL",SYMBOL_ASK); //this line produce an error
CError error;
error.CreateErrorCustom("An Error occured!!!",true);
```

The above code will display:

--------------- ERROR ---------------

Message : An Error occured!!!

Error(4301) : Unknown symbol

--------------- ERROR ---------------

### CValidationCheck class

The CValidationCheck class implements useful functions for order’s checks and validations. Before a request for a trade send to the server, it is important to pass some checks. Most of them are well known, that is why the CExecute class use them by default. Therefore, it is quite useful to able to use this class in any case it is needed.

Example:

```mql5
//-- Function to check if the volume(lotsize) of a trade is valid
CheckVolumeValue(string symbol, double volume)
//-- Function to check if there is enough available balance for make another trade
CheckMoneyForTrade(string symbol,double volume,ENUM_ORDER_TYPE type)
```

### Final thoughts

The power of any library/framework is not only when it delivers its duties properly but also when it builds a community around it.  
The community should lead the MQL\_Easy framework as any other open source project. Feel free to contribute in any levels(github: [https://github.com/Denn1Ro/MQL\_Easy](/go?link=https://github.com/Denn1Ro/MQL_Easy "https://github.com/Denn1Ro/MQL_Easy")).

**Last comments |
[Go to discussion](/en/forum/308733)**
(17)

![Kim](https://c.mql5.com/avatar/avatar_na2.png "Kim")

**[Kim](/en/users/kimh)**
|
7 Dec 2021 at 10:38

When creating a position object using the command,

   CPosition positionBuy(symbol,magicNumber, GROUP\_POSITIONS\_BUYS);

Is it possible to have it ignore the magicNumber so that any magic number is included?  
  
Thanks

Kim

![Dionisis Nikolopoulos](https://c.mql5.com/avatar/2017/6/594932DD-9524.png "Dionisis Nikolopoulos")

**[Dionisis Nikolopoulos](/en/users/tradingso)**
|
7 Dec 2021 at 17:20

**Kim [#](/en/forum/308733/page2#comment_26333442):**  

When creating a position object using the command,

   CPosition positionBuy(symbol,magicNumber, GROUP\_POSITIONS\_BUYS);

Is it possible to have it ignore the magicNumber so that any magic number is included?  
  
Thanks

Kim

Hi,

if you don't set the magic number of the position object (like position.SetGroupMagicNumber(12345)), then the magic number will be ignored.

```mql5
CPosition position;
position.setGroup(GROUP_POSITIONS_BUYS);
int total = position.GroupTotal();
Print(total);
```

The above example print the total number of BUY positions regardless the magic number.

![Kim](https://c.mql5.com/avatar/avatar_na2.png "Kim")

**[Kim](/en/users/kimh)**
|
8 Dec 2021 at 04:52

**Dionisis Nikolopoulos [#](/en/forum/308733/page2#comment_26343477):**  

Hi,

if you don't set the magic number of the position object (like position.SetGroupMagicNumber(12345)), then the magic number will be ignored.

The above example print the total number of BUY positions regardless the magic number.

Thanks

![Bakar Khan](https://c.mql5.com/avatar/2024/9/66E87B65-FC2C.jpg "Bakar Khan")

**[Bakar Khan](/en/users/bakarkhan1337)**
|
6 Oct 2024 at 00:41

The best thing that ever happened to MQL development :)

![Salman Soltaniyan](https://c.mql5.com/avatar/2022/7/62E529A2-6D94.jpg "Salman Soltaniyan")

**[Salman Soltaniyan](/en/users/salmansoltaniyan)**
|
3 Mar 2025 at 12:27

This library's clarity and ease of use have made a remarkable difference in my development process. I'm grateful to have discovered such a well-designed tool after three years of coding, and I appreciate the effort that went into its creation.

![Gann Grid](https://c.mql5.com/i/code/expert.png)
[Gann Grid](/en/code/25065)

"Gann Grid" EA draws Gann Grid lines on chart and trades with price breakthrough,has Trailing Stop Loss &Take Profit works with all time frames major forex pairs and stocks NASDAQ.

![DayPivotPoint](https://c.mql5.com/i/code/indicator.png)
[DayPivotPoint](/en/code/25042)

Indicator DayPivotPoint System with Signal and Alert for MetaTrader 4

![Killer EURUSD_sell](https://c.mql5.com/i/code/expert.png)
[Killer EURUSD\_sell](/en/code/25105)

Advanced EA optimized on selling positions on EURUSD.

![TheWardenFX](https://c.mql5.com/i/code/expert.png)
[TheWardenFX](/en/code/25122)

Expert with advanced and optimized indicators aiming for best entry.