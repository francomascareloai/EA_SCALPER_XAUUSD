//@version=5
//$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
//$                                                                                 $
//$ Благодарность приветствуется!                                                   $
//$ Gratitude is appreciated!                                                       $
//$                                                                                 $
//$ Вы можете сделать пожертвование через:                                          $
//$ You may donate via:                                                             $
//$                                                                                 $
//$ Крипто-мультивалютный кошелек                                                   $
//$ Crypto-multi-currency wallet                                                    $
//$                                                                                 $
//$ https://tinyurl.com/4f9dr9nw                                                    $
//$                                                                                 $
//$    - BTC: 1EpDN7pg1WqURcK7te5kQtgx3H17wuHiV7                                    $
//$                                                                                 $
//$    - LTC: MJyEtVt7EFmA18Bo1vjRMbguivHnVbbWvp                                    $
//$                                                                                 $
//$ - Tether TRC20 (USDT): TWQdh36osEJ3mDrG7kGEHgSVHQSgSR61ZY                       $
//$                                                                                 $
//$ Пожертвуйте столько, сколько считаете нужным.                                   $
//$ Donate as much as you see fit.                                                  $
//$                                                                                 $
//$ Сделайте это, если найдете этот материал полезным!                              $
//$ Do it if you find the stuff useful!                                             $
//$                                                                                 $
//$ Disclaimer: I am not a financial advisor.                                       $
//$ Отказ от ответственности: я не финансовый консультант.                          $
//$                                                                                 $
//$ For purpose educate only. Use at your own risk.                                 $
//$ Только в образовательных целях. Используйте на свой страх и риск.               $
//$                                                                                 $
//$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$

indicator(title='<[Osc] Aroon Meter Up & Down]>', shorttitle='<[AMUD]>', overlay=false)

// Входные параметры для настройки отображения
tooltipBarColors="Bar colors:\n\nGreen = long\nRed = short\nGray = neutral\n\nAroon bar colors:\n\nBlaze orange = long\nAzure = short\nGray = neutral"

Group1                         = 'Aroon Trend Matching - Show'
trendAroonMatchOffChartDisplay = input.bool(true,  group=Group1, title='Show off main chart')
trendAroonMatchShowLength      = input.bool(true,  group=Group1, title='  Show length')
trendAroonMatchOnChartDisplay  = input.bool(false, group=Group1, title='Show on main chart', tooltip=tooltipBarColors)
trendAroonMatchAroonColors     = input.bool(false, group=Group1, title='Use Aroon colors')

// Параметры для настройки порогов и длины
Group2                         = 'Aroon Trend Matching - Settings'
trendAroonMatchHighThreshold   = input.float(60, group=Group2, minval=0, maxval=100, step=5, title='High threshold %')
trendAroonMatchLowThreshold    = input.float(40, group=Group2, minval=0, maxval=100, step=5, title='Low threshold %')
trendAroonMatchLength          = input.int(6,    group=Group2, minval=1,                     title='Length')

// Расчет Aroon
trendAroonMatchUpper = 100 * (ta.highestbars(high, trendAroonMatchLength + 1) + trendAroonMatchLength) / trendAroonMatchLength
trendAroonMatchLower = 100 * (ta.lowestbars(low, trendAroonMatchLength + 1) + trendAroonMatchLength) / trendAroonMatchLength

// Логика определения направления тренда
trendAroonMatchDir = 0
if trendAroonMatchUpper >= trendAroonMatchHighThreshold and trendAroonMatchLower <= trendAroonMatchLowThreshold
    trendAroonMatchDir :=  1
if trendAroonMatchLower >= trendAroonMatchHighThreshold and trendAroonMatchUpper <= trendAroonMatchLowThreshold
    trendAroonMatchDir := -1

// Настройка цветов
trendAroonMatchUpperColor   = color.green
trendAroonMatchLowerColor   = color.red
trendAroonMatchUpperColor10 = #4caf50e6
trendAroonMatchLowerColor10 = #ff5252e6
trendAroonMatchUpperColor70 = #4caf504d
trendAroonMatchLowerColor70 = #ff52524d
trendAroonMatchUpperColor90 = #4caf501a
trendAroonMatchLowerColor90 = #ff52521a

if trendAroonMatchAroonColors
    trendAroonMatchUpperColor   := #FF6A00
    trendAroonMatchLowerColor   := #0094FF
    trendAroonMatchUpperColor10 := #ff6a00e6
    trendAroonMatchLowerColor10 := #0094ffe6
    trendAroonMatchUpperColor70 := #ff6a004d
    trendAroonMatchLowerColor70 := #0094ff4d
    trendAroonMatchUpperColor90 := #ff6a001a
    trendAroonMatchLowerColor90 := #0094ff1a

trendAroonMatchDirColor = switch trendAroonMatchDir
    1 => trendAroonMatchUpperColor
    -1 => trendAroonMatchLowerColor
    => #787b86

// Сигналы Aroon
length = input.int(14, minval=1)
showReg = input.bool(true, "Show Regular Signals", group = "Signal Options")
showCont = input.bool(true, "Show Continuation Signals", group = "Signal Options")

greencolor = color.lime
redcolor = color.red

upper = 100 * (ta.highestbars(high, length + 1) + length) / length
lower = 100 * (ta.lowestbars(low, length + 1) + length) / length

// Условия для сигналов
up_con = ta.crossover(upper, lower) and showReg
down_con = ta.crossunder(upper, lower) and showReg

cont_up = upper >= lower and ta.crossover(upper, upper[1]) and not up_con and showCont
cont_down = lower >= upper and ta.crossunder(lower, lower[1]) and not down_con and showCont

// Отображение сигналов
plotshape(up_con, title = "UP", color = greencolor, textcolor = greencolor, text = "U", style = shape.triangleup, location = location.bottom, size = size.auto)
plotshape(down_con, title = "Down", color = redcolor, textcolor = redcolor, text = "D", style = shape.triangledown, location = location.top, size = size.auto)

plotshape(cont_up, title = "Continuation UP", color = greencolor, textcolor = greencolor, text = "CU", style = shape.triangleup, location = location.bottom, size = size.auto)
plotshape(cont_down, title = "Continuation Down", color = redcolor, textcolor = redcolor, text = "CD", style = shape.triangledown, location = location.top, size = size.auto)

// Графическое отображение Aroon
trendAroonMatchPlotTop   = plot(100, color=trendAroonMatchUpperColor10, style=plot.style_line, display=display.none, title='Aroon trend matching: Top')
trendAroonMatchPlotBot   = plot(0,   color=trendAroonMatchLowerColor10, style=plot.style_line, display=display.none, title='Aroon trend matching: Bottom')

trendAroonMatchPlotUpTop = plot(trendAroonMatchOffChartDisplay and trendAroonMatchDir ==  1 ? trendAroonMatchHighThreshold : na, color=trendAroonMatchUpperColor70, style=plot.style_line, display=display.all,  title='Aroon trend matching: Up Top')
trendAroonMatchPlotUpBot = plot(trendAroonMatchOffChartDisplay and trendAroonMatchDir ==  1 ? trendAroonMatchLowThreshold  : na, color=trendAroonMatchUpperColor70, style=plot.style_line, display=display.all,  title='Aroon trend matching: Up Bottom')
trendAroonMatchPlotDnTop = plot(trendAroonMatchOffChartDisplay and trendAroonMatchDir == -1 ? trendAroonMatchHighThreshold : na, color=trendAroonMatchLowerColor70, style=plot.style_line, display=display.all,  title='Aroon trend matching: Down Top')
trendAroonMatchPlotDnBot = plot(trendAroonMatchOffChartDisplay and trendAroonMatchDir == -1 ? trendAroonMatchLowThreshold  : na, color=trendAroonMatchLowerColor70, style=plot.style_line, display=display.all,  title='Aroon trend matching: Down Bottom')

if trendAroonMatchShowLength
    trendAroonMatchLine = line.new(
      bar_index,                         50,
      bar_index - trendAroonMatchLength, 50,
      xloc   = xloc.bar_index, 
      color  = #e5ff00, 
      style  = line.style_solid, 
      width  = 1)
    line.delete(trendAroonMatchLine[1])

fill(trendAroonMatchPlotTop, trendAroonMatchPlotUpTop, color=trendAroonMatchUpperColor90, fillgaps=false, title='Fill: Aroon trend matching: Upper Top')
fill(trendAroonMatchPlotBot, trendAroonMatchPlotUpBot, color=trendAroonMatchUpperColor90, fillgaps=false, title='Fill: Aroon trend matching: Upper Bot')
fill(trendAroonMatchPlotTop, trendAroonMatchPlotDnTop, color=trendAroonMatchLowerColor90, fillgaps=false, title='Fill: Aroon trend matching: Lower Top')
fill(trendAroonMatchPlotBot, trendAroonMatchPlotDnBot, color=trendAroonMatchLowerColor90, fillgaps=false, title='Fill: Aroon trend matching: Lower Bot')

plot(trendAroonMatchOffChartDisplay ? trendAroonMatchUpper : na, color=trendAroonMatchUpperColor, title='Aroon trend matching: Upper')
plot(trendAroonMatchOffChartDisplay ? trendAroonMatchLower : na, color=trendAroonMatchLowerColor, title='Aroon trend matching: Lower')

barcolor(trendAroonMatchOnChartDisplay ? trendAroonMatchDirColor : na)

// Таблица для отображения значений Aroon
var meter = table.new(position.top_right, 2, 2)
if barstate.islast
    table.cell(meter, 0, 0, ' Aroon UP ', text_color = #eaff00, bgcolor = na)
    table.cell(meter, 1, 0, 'Aroon Down', text_color = #eaff00, bgcolor = na)
    table.cell(meter, 0, 1, str.tostring(trendAroonMatchUpper, '#.##') + '%', text_color = trendAroonMatchUpperColor, bgcolor = color.new(trendAroonMatchUpperColor, 90))
    table.cell(meter, 1, 1, str.tostring(trendAroonMatchLower, '#.##') + '%', text_color = trendAroonMatchLowerColor, bgcolor = color.new(trendAroonMatchLowerColor, 90))

var table logo = table.new(position.bottom_right, 1, 1)
if barstate.islast
    table.cell(logo, 0, 0, 'DM', text_size=size.normal, text_color=#00897b)

//--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------//   